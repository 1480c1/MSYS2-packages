# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=file
pkgver=5.42
pkgrel=2
pkgdesc="File type identification utility"
arch=('i686' 'x86_64')
license=('custom')
url="https://www.darwinsys.com/file/"
depends=('gcc-libs' 'zlib' 'libbz2')
makedepends=('python' 'zlib-devel' 'libbz2-devel' 'autotools' 'gcc')
options=('!libtool')
source=("https://astron.com/pub/$pkgname/$pkgname-$pkgver.tar.gz"{,.asc})
sha256sums=('c076fb4d029c74073f15c43361ef572cfb868407d347190ba834af3b1639b0e4'
            'SKIP')
validpgpkeys=('BE04995BA8F90ED0C0C176C471112AB16CB33B3A') # Christos Zoulas <christos@zoulas.com>

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  autoreconf -fiv
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  local CYGWIN_CHOST="${CHOST/-msys/-cygwin}"
  ./configure \
    --prefix=/usr \
    --build=${CYGWIN_CHOST} \
    --host=${CYGWIN_CHOST} \
    --target=${CYGWIN_CHOST} \
    --enable-fsect-man5

  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR=${pkgdir} install

  install -Dm644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/COPYING

  PYTHON_SITELIB=$(/usr/bin/python -c 'from distutils.sysconfig import * ; print(get_python_lib(0,0));')
  mkdir -p ${pkgdir}/${PYTHON_SITELIB}
  cp -f ${srcdir}/${pkgname}-${pkgver}/python/magic.py ${pkgdir}/$PYTHON_SITELIB
}
