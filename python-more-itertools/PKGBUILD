# Maintainer: J. Peter Mugaas <jpmugaas@suddenlink.net>

_realname=more-itertools
pkgbase=python-${_realname}
pkgname=("python-${_realname}")
provides=("python3-${_realname}")
replaces=("python3-${_realname}")
conflicts=("python3-${_realname}")
pkgver=8.3.0
pkgrel=1
pkgdesc='More routines for operating on iterables, beyond itertools'
arch=('any')
url='https://github.com/erikrose/more-itertools'
#url='https://pypi.python.org/pypi/more-itertools'
license=('MIT')
makedepends=('python-setuptools')
depends=('python')
source=("https://files.pythonhosted.org/packages/source/m/more-itertools/more-itertools-${pkgver}.tar.gz")
sha512sums=('34e5489277477a2b8a51de3e28c3d010f06c9071ad98e58901734431d5640a6a03e4239cf405f75e4dd0838e5e3fe8910b64ad542759874c0f25725e4b9e78ed')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  # Set version for setuptools_scm
  export SETUPTOOLS_SCM_PRETEND_VERSION=${pkgver}
}

build() {
  cd "${srcdir}/${_realname}-${pkgver}"
  /usr/bin/python setup.py build
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"
  /usr/bin/python setup.py install --root="${pkgdir}/" --optimize=0 --skip-build
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
