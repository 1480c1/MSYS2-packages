# Maintainer: J. Peter Mugaas <jpmugaas@suddenlink.net>

pkgbase=libnghttp2
pkgname=("nghttp2" "${pkgbase}" "${pkgbase}-devel")
pkgver=1.54.0
pkgrel=1
pkgdesc='Framing layer of HTTP/2 is implemented as a reusable C library'
arch=(i686 x86_64)
url='https://nghttp2.org/'
depends=("jansson"
         "libevent"
         "libxml2"
         "openssl"
         "libcares"
         "gcc-libs")
makedepends=('jansson-devel' 'libevent-devel' 'openssl-devel' 'libcares-devel' 'zlib-devel' 'autotools' 'gcc' 'make')
license=(MIT)
source=(https://github.com/nghttp2/nghttp2/releases/download/v$pkgver/nghttp2-${pkgver}.tar.xz
        "https://github.com/nghttp2/nghttp2/commit/9fc1f85102cb57f5346c13108e577d2cb4e081cb.patch")
sha256sums=('20533c9354fbb6aa689b6aa0ddb77f91da1d242587444502832e1864308152df'
            '926703e310b402619ad77291b3b1c4420aa85689a60cbb9cab8db8471ac1743f')

prepare() {
  cd nghttp2-${pkgver}

  patch -Np1 -i "${srcdir}/9fc1f85102cb57f5346c13108e577d2cb4e081cb.patch"

  autoreconf -fiv
}

build() {
  cd nghttp2-${pkgver}

  local CYGWIN_CHOST="${CHOST/-msys/-cygwin}"
  ./configure \
    --prefix=/usr \
    --build=${CYGWIN_CHOST} \
    --enable-static=yes \
    --enable-shared=yes \
    --disable-examples \
    --disable-python-bindings

  make
  make DESTDIR="${srcdir}/dest" install
}

package_nghttp2() {
  depends=('gcc-libs' 'jansson' "libnghttp2=${pkgver}")
  mkdir -p ${pkgdir}/usr/bin
  cp -f ${srcdir}/dest/usr/bin/*.exe ${pkgdir}/usr/bin/
}

package_libnghttp2() {
  pkgdesc='Framing layer of HTTP/2 is implemented as a reusable C library (runtime)'
  depends=('gcc-libs' )
  groups=('libraries')

  mkdir -p ${pkgdir}/usr/bin
  cp -f ${srcdir}/dest/usr/bin/*.dll ${pkgdir}/usr/bin/
  install -Dm644 ${srcdir}/nghttp2-${pkgver}/COPYING "${pkgdir}/usr/share/licenses/libnghttp2/COPYING"
}

package_libnghttp2-devel() {
  pkgdesc="nghttp2 headers and libraries"
  options=('staticlibs')
  depends=("libnghttp2=${pkgver}")
  groups=('development')
  depends+=('jansson-devel' 'libevent-devel' 'openssl-devel' 'libcares-devel')

  mkdir -p ${pkgdir}/usr/bin
  cp -rf ${srcdir}/dest/usr/include ${pkgdir}/usr/
  cp -rf ${srcdir}/dest/usr/lib ${pkgdir}/usr/
  cp -rf ${srcdir}/dest/usr/share ${pkgdir}/usr/
}
