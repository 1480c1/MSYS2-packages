# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Ray Donnelly <mingw.android@gmail.com>

pkgbase=msys2-runtime
pkgname=('msys2-runtime' 'msys2-runtime-devel')
pkgver=3.4.8
pkgrel=1
pkgdesc="Cygwin POSIX emulation engine"
arch=('x86_64')
url="https://www.cygwin.com/"
license=('GPL')
makedepends=('cocom'
             'git'
             'perl'
             'gcc'
             'mingw-w64-cross-crt'
             'mingw-w64-cross-gcc'
             'mingw-w64-cross-zlib'
             'zlib-devel'
             'gettext-devel'
             'libiconv-devel'
             'autotools'
             'xmlto'
             'docbook-xsl')
# re zipman: https://github.com/msys2/MSYS2-packages/pull/2687#issuecomment-965714874
options=('!zipman')
source=('msys2-runtime'::git://sourceware.org/git/newlib-cygwin.git#tag=cygwin-${pkgver}
        0001-Add-MSYS2-triplet.patch
        0002-Fix-msys-library-name-in-import-libraries.patch
        0003-Rename-dll-from-cygwin-to-msys.patch
        0004-Add-functionality-for-converting-UNIX-paths-in-argum.patch
        0005-Add-functionality-for-changing-OS-name-via-MSYSTEM-e.patch
        0006-Move-root-to-usr.-Change-sorting-mount-points.-By-de.patch
        0007-Instead-of-creating-Cygwin-symlinks-use-deep-copy-by.patch
        0008-Automatically-rewrite-TERM-msys-to-TERM-cygwin.patch
        0009-Do-not-convert-environment-for-strace.patch
        0010-path_conv-special-case-root-directory-to-have-traili.patch
        0011-dcrt0.cc-Untangle-allow_glob-from-winshell.patch
        0012-dcrt0.cc-globify-Don-t-quote-literal-strings-differe.patch
        0013-strace.cc-Don-t-set-MSYS-noglob.patch
        0014-Add-debugging-for-build_argv.patch
        0015-Add-debugging-for-strace-make_command_line.patch
        0016-environ.cc-New-facility-environment-variable-MSYS2_E.patch
        0017-Fix-native-symbolic-link-spawn-passing-wrong-arg0.patch
        0018-strace-quiet-be-really-quiet.patch
        0019-Introduce-the-enable_pcon-value-for-MSYS.patch
        0020-popen-call-usr-bin-sh-instead-of-bin-sh.patch
        0021-Expose-full-command-lines-to-other-Win32-processes-b.patch
        0022-Do-not-show-Error-dialogs-by-default.patch
        0023-Add-a-helper-to-obtain-a-function-s-address-in-kerne.patch
        0024-Emulate-GenerateConsoleCtrlEvent-upon-Ctrl-C.patch
        0025-kill-kill-Win32-processes-more-gently.patch
        0026-Cygwin-make-option-for-native-inner-link-handling.patch
        0027-docs-skip-building-texinfo-and-PDF-files.patch
        0028-install-libs-depend-on-the-toollibs.patch
        0029-POSIX-ify-the-SHELL-variable.patch
        0030-Handle-ORIGINAL_PATH-just-like-PATH.patch
        0031-uname-allow-setting-the-system-name-to-CYGWIN.patch
        0032-Pass-environment-variables-with-empty-values.patch
        0033-Optionally-disallow-empty-environment-values-again.patch
        0034-build_env-respect-the-MSYS-environment-variable.patch
        0035-When-converting-to-a-Unix-path-avoid-double-trailing.patch
        0036-msys2_path_conv-pass-PC_NOFULL-to-path_conv.patch
        0037-Revert-Cygwin-Enable-dynamicbase-on-the-Cygwin-DLL-b.patch
        0038-dumper-avoid-linker-problem-when-libbfd-depends-on-l.patch
        0039-Cygwin-configure-Add-option-to-disable-building-dump.patch)
sha256sums=('SKIP'
            '18da8bc0a566c4df4154d6413c586ac1f6e21927fa137c58b14d691f040f907a'
            '7a8270c2670745e97987f19ff6c9a4a4d7231b5be99efd3a8d2a5f34102c9daa'
            '762ce0dd60f9287b3375a1aa0b62ebf7d7f67dc8f957328838cbdf7a30a16798'
            'e80110cf0aeaf653d43a38102b561dded9bbfab6675d3cee6aad23673af93a19'
            'cc1cc4e5e36b5881888df6bbfab5cdaa5bb256942d85b4d7f1fcfd1673504b6f'
            '6c4af72af5b0d6301cb40e9159cad3460299d42bac6c31b2a599a80ab99e09a1'
            'b4ff3c8194411fe1e4670b9cf3cc90d974dd23622c68fd78f72f96ea90274561'
            'a0033973fe9a715e445af195e47301ebb81f2a353d14436eacd48b77de0ccdb5'
            '75aaeca502dceab26d61df8a5c8d140fbb47d29a36f116b3c8b1f81acc35d1ed'
            '6e4efda1d586e3642450ef62c328620b67ebff971e64847de368fc58d1b05c43'
            '11ab000f7c2249b0ac17ea74a0d5306c48e6e8bd98a0f9cf6f9f3d01893e1552'
            'fcfb9a07c15c4f0f4940fee0e65da10fad4f0dfbbb9b4e5dce11068c1c24e6b9'
            '066fab31ac3f55544162fa9045b955e4464d379fa128c0ac34b5b90ccc6de8ea'
            'e6982a527f3a89a332e8e688d1a6564563b621d2c02da0108f4168942b8fb2cf'
            '1c4d6221b12d1a9f1dff20e175648495a8aeb69ee4f612280e6dfd612355842e'
            '667cb545e903c71ae7aa85bf5bdd937219196ef9644509e997574ca865fbec8d'
            'd83bef6ba05a445ac4a74524dca8b3dda4129a8bde459eba6818772bfec9117b'
            '770aecfe22bc8809d72819bab68462ec7f8f9a5ee6b0fe8d97000a0a6d11e380'
            '96088feec8dc2d71728164b7748b726e528d2b70ede81ded1bde7bf7570acdbd'
            'c606fec704cdd5927c0410ae4a006e0c97bb98a5649608b08d360dfd128249d9'
            'e5205a2e74647c75d157d67e2751009c7ecd430bdb146df904d52dd3c049e625'
            '58c3b440f469f88ca22aab4f38719b940165a432a1798eceb359811300395661'
            '6ba1c7100c122d0f2640f9d01f9d570e9388a755db6ce6f722b704df93169a04'
            '1f196162cde33852b48e92b62ba743099e7b78c7d67896122ce3d6a114237423'
            'a7ed00fb34ddb8bd244436d5c7c9c466ae24c4e7f55951b9e1de6e46d534b092'
            'ece9ab38ddb0d19aba5fde2c8d4d0eef864e04b85eb953a6a3347dd45044b1d2'
            'c1ef89a1c2ce22c2ecdf7403fc0ec329a613f078548215c84cf00c496eab8d5f'
            '25590f578958020ad352bb49f4c3dcac9cbd9ae68dabb6c148f18e4c73d2f3f1'
            'a17009351228208530a1cd0e6b181b22e27f7323a8fc68b63c8732ffc69b2412'
            'ecf41b27a312797c56f50b45bc1653135c9b5bedaffd358dae926e33caa65c45'
            '5e526b963c8fb3af50000801836c8ae41bf9aa6bd8eff1fed4e5cff10f531240'
            '94311c102c23d86e3bfd459d506563930400d7285a758aeea70192c54dde87f0'
            'c050d8c7a705001ef0459aad40711f89ff4949c9f10bd754e7ca185abc238ca1'
            '71bd94a79ce7a9106025b8959c76f9d8f46b25354cb36fb7ebece889c0b37e1e'
            '549cd3b984ebf796a798d21dbfffb9c50ab87c0bfacdd15fc1c754703febd1d8'
            '2e5e355552df645914f28593ab085a0dc495ba2a50f40bb45340248f1a469c18'
            '146aaa43416c4ef4135eda6a7a9b124c79312e6dee0eeccc651f7ec119da1d68'
            '02019b3793b5f5f8c41c6836c95db3efab8c4b2f70234ece1bcbf0e59351337f'
            'e0b93bbdbdbbb5179ecd20ca27e7eb9ab7989f51ab100c3bc6b20ecc6bdf395c')

# Helper macros to help make tasks easier #
apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying $_patch"
    patch -Nbp1 -i "${srcdir}/${_patch}"
  done
}

apply_git_am_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying $_patch"
    git apply "${srcdir}/${_patch}"
  done
}

del_file_exists() {
  for _fname in "$@"
  do
    if [ -f $_fname ]; then
      rm -rf $_fname
    fi
  done
}
# =========================================== #

prepare() {
  cd "${srcdir}"/msys2-runtime
  if test true != "$(git config core.symlinks)"
  then
    git config core.symlinks true &&
    /usr/bin/git reset --hard
  fi
  del_file_exists winsup/cygwin/msys2_path_conv.cc \
    winsup/cygwin/msys2_path_conv.h
 apply_git_am_with_msg 0001-Add-MSYS2-triplet.patch \
  0002-Fix-msys-library-name-in-import-libraries.patch \
  0003-Rename-dll-from-cygwin-to-msys.patch \
  0004-Add-functionality-for-converting-UNIX-paths-in-argum.patch \
  0005-Add-functionality-for-changing-OS-name-via-MSYSTEM-e.patch \
  0006-Move-root-to-usr.-Change-sorting-mount-points.-By-de.patch \
  0007-Instead-of-creating-Cygwin-symlinks-use-deep-copy-by.patch \
  0008-Automatically-rewrite-TERM-msys-to-TERM-cygwin.patch \
  0009-Do-not-convert-environment-for-strace.patch \
  0010-path_conv-special-case-root-directory-to-have-traili.patch \
  0011-dcrt0.cc-Untangle-allow_glob-from-winshell.patch \
  0012-dcrt0.cc-globify-Don-t-quote-literal-strings-differe.patch \
  0013-strace.cc-Don-t-set-MSYS-noglob.patch \
  0014-Add-debugging-for-build_argv.patch \
  0015-Add-debugging-for-strace-make_command_line.patch \
  0016-environ.cc-New-facility-environment-variable-MSYS2_E.patch \
  0017-Fix-native-symbolic-link-spawn-passing-wrong-arg0.patch \
  0018-strace-quiet-be-really-quiet.patch \
  0019-Introduce-the-enable_pcon-value-for-MSYS.patch \
  0020-popen-call-usr-bin-sh-instead-of-bin-sh.patch \
  0021-Expose-full-command-lines-to-other-Win32-processes-b.patch \
  0022-Do-not-show-Error-dialogs-by-default.patch \
  0023-Add-a-helper-to-obtain-a-function-s-address-in-kerne.patch \
  0024-Emulate-GenerateConsoleCtrlEvent-upon-Ctrl-C.patch \
  0025-kill-kill-Win32-processes-more-gently.patch \
  0026-Cygwin-make-option-for-native-inner-link-handling.patch \
  0027-docs-skip-building-texinfo-and-PDF-files.patch \
  0028-install-libs-depend-on-the-toollibs.patch \
  0029-POSIX-ify-the-SHELL-variable.patch \
  0030-Handle-ORIGINAL_PATH-just-like-PATH.patch \
  0031-uname-allow-setting-the-system-name-to-CYGWIN.patch \
  0032-Pass-environment-variables-with-empty-values.patch \
  0033-Optionally-disallow-empty-environment-values-again.patch \
  0034-build_env-respect-the-MSYS-environment-variable.patch \
  0035-When-converting-to-a-Unix-path-avoid-double-trailing.patch \
  0036-msys2_path_conv-pass-PC_NOFULL-to-path_conv.patch \
  0037-Revert-Cygwin-Enable-dynamicbase-on-the-Cygwin-DLL-b.patch \
  0038-dumper-avoid-linker-problem-when-libbfd-depends-on-l.patch \
  0039-Cygwin-configure-Add-option-to-disable-building-dump.patch
}

build() {
  [[ -d "${srcdir}"/build-${CHOST} ]] && rm -rf "${srcdir}"/build-${CHOST}
  mkdir -p "${srcdir}"/build-${CHOST} && cd "${srcdir}"/build-${CHOST}

  # Gives more verbose compile output when debugging.
  local -a extra_config
  if check_option "debug" "y"; then
    export CCWRAP_VERBOSE=1
    OPTIM="-O0"
    extra_config+=(--enable-debugging)
  else
    OPTIM="-O2"
  fi

  CFLAGS="$OPTIM -pipe -ggdb -Wno-error=deprecated -Wno-error=stringop-truncation -Wno-error=missing-attributes -Wno-error=maybe-uninitialized" #-Wno-error=class-memaccess
  CXXFLAGS="$OPTIM -pipe -ggdb -Wno-error=deprecated -Wno-error=stringop-truncation -Wno-error=missing-attributes -Wno-error=maybe-uninitialized" #-Wno-error=class-memaccess

  # otherwise it asks git which appends "-dirty" because of our uncommited patches
  CFLAGS+=" -DCYGPORT_RELEASE_INFO=${pkgver}"

  (cd "${srcdir}/msys2-runtime/winsup" && ./autogen.sh)

  "${srcdir}"/msys2-runtime/configure \
    --prefix=/usr \
    --build=${CHOST} \
    --sysconfdir=/etc \
    "${extra_config[@]}"
  LC_ALL=C make
  LC_ALL=C make -j1 DESTDIR="${srcdir}"/dest install

  #pushd ${CHOST}/winsup/cygwin > /dev/null
  #LANG=C make libmsys2_s.a
  #cp libmsys2_s.a "${srcdir}"/dest/usr/${CHOST}/lib/
  #popd > /dev/null

  rm -rf "${srcdir}"/dest/etc
}

package_msys2-runtime() {
  pkgdesc="Posix emulation engine for Windows"
  conflicts=('catgets' 'libcatgets' 'msys2-runtime-3.4')
  replaces=('catgets' 'libcatgets' 'msys2-runtime-3.4')

  mkdir -p "${pkgdir}"/usr
  cp -rf "${srcdir}"/dest/usr/bin "${pkgdir}"/usr/
  cp -rf "${srcdir}"/dest/usr/libexec "${pkgdir}"/usr/
  rm -f "${pkgdir}"/usr/bin/cyglsa-config
  rm -f "${pkgdir}"/usr/bin/cyglsa.dll
  rm -f "${pkgdir}"/usr/bin/cyglsa64.dll
  rm -f "${pkgdir}"/usr/bin/cygserver-config
  cp -rf "${srcdir}"/dest/usr/share "${pkgdir}"/usr/
}

package_msys2-runtime-devel() {
  pkgdesc="MSYS2 headers and libraries"
  depends=("msys2-runtime=${pkgver}")
  conflicts=('libcatgets-devel' 'msys2-runtime-3.4-devel')
  replaces=('libcatgets-devel' 'msys2-runtime-3.4-devel')

  mkdir -p "${pkgdir}"/usr/bin
  cp -rLf "${srcdir}"/dest/usr/${CHOST}/include "${pkgdir}"/usr/
  rm -f "${pkgdir}"/usr/include/iconv.h
  rm -f "${pkgdir}"/usr/include/unctrl.h
  # provided by libtirpc
  rm -fr "${pkgdir}"/usr/include/rpc/

  cp -rLf "${srcdir}"/dest/usr/${CHOST}/lib "${pkgdir}"/usr/
}

# return 0
# To hack on this:
# cd /c/repo-MSYS2/msys2-runtime/
# pushd src/build-i686-pc-msys
# LANG=C make && make -j1 DESTDIR=/c/repo-MSYS2/msys2-runtime/src/dest install
# popd
# makepkg -sRLf
# pacman -U msys2-runtime*.xz

# Quicker:
# open cmd.exe
# set "PATH=C:\\msys32\\usr\\bin;%PATH%"
# E:
# pushd E:\m2\repo-MSYS2\msys2-runtime\src\build-i686-pc-msys\i686-pc-msys\winsup\cygwin
# C:/msys32/usr/bin/bash -c "LANG=C && make"
# copy /y new-msys-2.0.dll C:\msys32\usr\bin\msys-2.0.dll
# popd
# C:
# C:/msys32/usr/bin/strace ls / > C:/strace.txt 2>&1
#
