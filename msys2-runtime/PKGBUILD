# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Ray Donnelly <mingw.android@gmail.com>

pkgbase=msys2-runtime
pkgname=('msys2-runtime' 'msys2-runtime-devel')
pkgver=3.3.4
pkgrel=3
pkgdesc="Cygwin POSIX emulation engine"
arch=('i686' 'x86_64')
url="https://www.cygwin.com/"
license=('GPL')
makedepends=('cocom'
             'git'
             'perl'
             'gcc'
             'mingw-w64-cross-crt-git'
             'mingw-w64-cross-gcc'
             'mingw-w64-cross-zlib'
             'zlib-devel'
             'gettext-devel'
             'libiconv-devel'
             'autotools'
             'xmlto'
             'docbook-xsl')
# re zipman: https://github.com/msys2/MSYS2-packages/pull/2687#issuecomment-965714874
options=('!zipman')
source=('msys2-runtime'::git://sourceware.org/git/newlib-cygwin.git#tag=cygwin-${pkgver//./_}-release
        0001-Add-MSYS2-triplet.patch
        0002-Fix-msys-library-name-in-import-libraries.patch
        0003-Rename-dll-from-cygwin-to-msys.patch
        0004-Add-functionality-for-converting-UNIX-paths-in-argum.patch
        0005-Add-functionality-for-changing-OS-name-via-MSYSTEM-e.patch
        0006-Move-root-to-usr.-Change-sorting-mount-points.-By-de.patch
        0007-Instead-of-creating-Cygwin-symlinks-use-deep-copy-by.patch
        0008-Automatically-rewrite-TERM-msys-to-TERM-cygwin-With-.patch
        0009-Do-not-convert-environment-for-strace.patch
        0010-Special-case-for-converting-root-directory-to-have-t.patch
        0011-dcrt0.cc-Untangle-allow_glob-from-winshell.patch
        0012-dcrt0.cc-globify-Don-t-quote-literal-strings-differe.patch
        0013-strace.cc-Don-t-set-MSYS-noglob.patch
        0014-Add-debugging-for-build_argv.patch
        0015-Add-debugging-for-strace-make_command_line.patch
        0016-environ.cc-New-facility-environment-variable-MSYS2_E.patch
        0017-Fix-native-symbolic-link-spawn-passing-wrong-arg0.patch
        0018-QueryUnbiasedInterruptTime-must-be-load-from-kernel3.patch
        0019-strace-quiet-be-really-quiet.patch
        0020-Default-to-disable_pcon.patch
        0021-Introduce-the-enable_pcon-value-for-MSYS.patch
        0022-popen-call-usr-bin-sh-instead-of-bin-sh.patch
        0023-CI-add-a-GHA-for-doing-a-basic-build-test.patch
        0024-Set-up-a-GitHub-Action-to-keep-in-sync-with-Cygwin.patch
        0025-Expose-full-command-lines-to-other-Win32-processes-b.patch
        0026-Disable-the-cygwin-GitHub-workflow.patch
        0027-Do-not-show-Error-dialogs-by-default.patch
        0028-Add-a-helper-to-obtain-a-function-s-address-in-kerne.patch
        0029-Emulate-GenerateConsoleCtrlEvent-upon-Ctrl-C.patch
        0030-kill-kill-Win32-processes-more-gently.patch
        0031-Cygwin-make-option-for-native-inner-link-handling.patch
        0032-docs-skip-building-texinfo-and-PDF-files.patch
        0033-install-libs-depend-on-the-toollibs.patch
        0034-POSIX-ify-the-SHELL-variable.patch
        0035-Handle-ORIGINAL_PATH-just-like-PATH.patch
        0036-Cygwin-console-Maintain-ENABLE_-INSERT-QUICK_EDIT-_M.patch
        0037-Cygwin-pty-console-Fix-Ctrl-C-handling-for-non-cygwi.patch
        0038-Cygwin-pty-Pass-Ctrl-Z-EOF-to-non-cygwin-apps-with-d.patch
        0039-Cygwin-pty-Prevent-deadlock-on-echo-output.patch
        0040-Cygwin-pty-Revise-the-code-to-wait-for-completion-of.patch
        0041-Cygwin-pty-Discard-input-in-from_master_nat-pipe-on-.patch
        0042-Cygwin-pty-Fix-a-bug-in-tty_min-segpgid.patch
        0043-Cygwin-console-Fix-console-mode-for-non-cygwin-infer.patch
        0044-Cygwin-console-Set-console-mode-even-if-stdin-stdout.patch
        0045-Cygwin-Fix-build-with-w32api-10.0.0.patch
        0046-Cygwin-Fix-typo-KERB_S4U_LOGON_FLAG_IDENTITY-IDENTIF.patch)
sha256sums=('SKIP'
            '61d44e17527d3ccea3e7e8bbfae958b888dc62c5ea91ec8aaf8d4b313dee7e44'
            '76bb6a975d1faca66e1cc44be9139556dc731501ade1af229b6e6fe5f47bb15c'
            '7325d07a0895819d49bc9541cb896b0884ed4c30ef81ca6e09a5d37f47bf0e31'
            '04469319c81343c06cf946b4e0a390928fa005edfc687c34ec757bf18f21ec70'
            '6a5337e1d38a02d41ad7ad304abe4a85881a2c80a51b19bdf11c86cc1a0b9be8'
            'b23f06173c09882f9bdb55d3a8deadf19919fe18a2406f41e416105b61b7fed6'
            'b08b1d7254454698ae852157f9b159a3ca80e9c97182e5437087f6b3724cc88f'
            'cb6896acc49351fcc19a572cda0d3349e7a89b5e2f5dcdb70b8e3ac4f0575b44'
            'a9f549a108fd5674223a9077e20f7a428b259cc582bbe290ed44b0201834178a'
            '6abe08304ceab8f44e39e7f26d8afbbe0b43234f8f80f696303f25b7960a384f'
            'a5be86f427d5909e5316218fde29a5da63f31e5d8baf099fe564866329bff636'
            '329c9f5ba415256f2044a2b3ea59b2cb48bd4e0d3dbae5a23a9ee9764f1332c1'
            'c798c3bc051b4461fc75694ef5bd062ab0d94d4b46bfd69929926fb154694687'
            '94f0e53371075077d3ba00bab8d51ee80ba990a23ecc8651f8e5aeb126bbabe0'
            '6ba236f8f1dabf1b6fdc40366b07982b239f394760256af680e098b36d674d19'
            'e75ecfbef4a4ed184818b9ae59a7baf083226ba683b8e49dcded6195c5973162'
            '790b723d92f55fef23a91b1ae739ebcc9052ba14c20640d85ea61c3fcdd73afd'
            '01e227e0a6daf56d6f291778b4879510e3251836181a1dd25fbea50b8b5c79fb'
            '1f494d420e34559e6b5236272560d7d54b23286bcc04c55e2d51623b0a46754e'
            '7f96864d11ed06ad428f4e1032f332d7e4a4862669029ece1492de1f319c9208'
            'fef66285fb3ad5ebbe2b2c5495d0b91d7ba7da0c428dcf87afe34d890a68c627'
            'f31d05080b743db8bc90c124bb4600d14d5cd0377c682ced740533260b76022d'
            'dfcef25a0a550eea87d7f65938f36f199d62ad53bebac64e7670e80a8458ba17'
            'b9b4d423c08566b8e352b67bc894c73d8bbddfb6392b311de85e8de4ef39cafc'
            'c4f76cf8dcff42a281ad2ece93c0180a748d110514e901165c903b00479d59de'
            '169e54aa91b41fc794381a28b8d02984645447e457f2df5c0e0b817051c6ebd9'
            'e01d62ee0830817b754f68964dcdb01aff38c73df3c30c207858aa71e5acd986'
            '448cc20cbb9df301895351c0ec090a735e21112a6fd8f6a4f795ccaf63484172'
            '8783ce29ee0af9d2967be9e1232379909a5eccc4ce8b1bede5a34e2dd1718b34'
            '129b800039f1f7fd12355ceda471b7196f5da327ad7cda2c1655ae0628d21813'
            'a1ae2e8cd77ca20093f39447d8146ce528dec860c43b003476e3f59e152f11c0'
            'deca3a5797349c3648b205c0a8d5b501fac517d987b767537e6a1315e55af07e'
            '3bcb80f61d048c9ccf9545ae9bbd19ec757d717828e69b704ae192fa88519ff4'
            '2933e75c27bc52cb522f0b1cd7f8e5753bb05e6362d9ad2234daaf672dabc505'
            '773f7a720c5b30861c75aca54cd20a705b8f22458d800fd6458ea3f0b0741b1a'
            '1fa1cc394996ddc2ec9ef2a2c685ba95992ffea101100e79b6880517000268e7'
            'a8dbbeb96fd01cf03780ac6e9b62e65a5d1457b6d0d18cd93a62adf5a5ccca3f'
            '3d1bcb5985a59850505729f83068970160e20c74fc6f18cd587163999d74be63'
            'b8b1d7402f7b0c86ffb0e60c52d7072d62a16932f431cd1c898735f541601212'
            '47639ee79778a0fc57b1a2eb42d97507734b94be36dbf138b36883879992e6c1'
            '2c94e88f8632947ef9e808cd0605a84fed7d6dc4a6ba63cbc4d7a9011e97dc91'
            '0ab62508bc424e03c301f6ba0c30af6a53966f78afb73badeff394ec7d2ebfa2'
            'ce751208e3bb383fc110c67ac023f37b2e38b82766616a42b7c08422afb19d27'
            'bacab808e8fd327071a1f48d0362d67b7da2d4a269c5cf26d713bcacd345cd7e'
            '5a655fe2eb6161a3452217970915ba64dba40e1cea74ea264a2e97d8eed0d1b5'
            '4a97e30de8da68533e22b5627d586d98a04d8adceb527b8814019b2ce3b86f30')

# Helper macros to help make tasks easier #
apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying $_patch"
    patch -Nbp1 -i "${srcdir}/${_patch}"
  done
}

apply_git_am_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying $_patch"
    git apply "${srcdir}/${_patch}"
  done
}

del_file_exists() {
  for _fname in "$@"
  do
    if [ -f $_fname ]; then
      rm -rf $_fname
    fi
  done
}
# =========================================== #

prepare() {
  cd "${srcdir}"/msys2-runtime
  if test true != "$(git config core.symlinks)"
  then
    git config core.symlinks true &&
    /usr/bin/git reset --hard
  fi
  del_file_exists winsup/cygwin/msys2_path_conv.cc \
    winsup/cygwin/msys2_path_conv.h
 apply_git_am_with_msg 0001-Add-MSYS2-triplet.patch \
  0002-Fix-msys-library-name-in-import-libraries.patch \
  0003-Rename-dll-from-cygwin-to-msys.patch \
  0004-Add-functionality-for-converting-UNIX-paths-in-argum.patch \
  0005-Add-functionality-for-changing-OS-name-via-MSYSTEM-e.patch \
  0006-Move-root-to-usr.-Change-sorting-mount-points.-By-de.patch \
  0007-Instead-of-creating-Cygwin-symlinks-use-deep-copy-by.patch \
  0008-Automatically-rewrite-TERM-msys-to-TERM-cygwin-With-.patch \
  0009-Do-not-convert-environment-for-strace.patch \
  0010-Special-case-for-converting-root-directory-to-have-t.patch \
  0011-dcrt0.cc-Untangle-allow_glob-from-winshell.patch \
  0012-dcrt0.cc-globify-Don-t-quote-literal-strings-differe.patch \
  0013-strace.cc-Don-t-set-MSYS-noglob.patch \
  0014-Add-debugging-for-build_argv.patch \
  0015-Add-debugging-for-strace-make_command_line.patch \
  0016-environ.cc-New-facility-environment-variable-MSYS2_E.patch \
  0017-Fix-native-symbolic-link-spawn-passing-wrong-arg0.patch \
  0018-QueryUnbiasedInterruptTime-must-be-load-from-kernel3.patch \
  0019-strace-quiet-be-really-quiet.patch \
  0020-Default-to-disable_pcon.patch \
  0021-Introduce-the-enable_pcon-value-for-MSYS.patch \
  0022-popen-call-usr-bin-sh-instead-of-bin-sh.patch \
  0023-CI-add-a-GHA-for-doing-a-basic-build-test.patch \
  0024-Set-up-a-GitHub-Action-to-keep-in-sync-with-Cygwin.patch \
  0025-Expose-full-command-lines-to-other-Win32-processes-b.patch \
  0026-Disable-the-cygwin-GitHub-workflow.patch \
  0027-Do-not-show-Error-dialogs-by-default.patch \
  0028-Add-a-helper-to-obtain-a-function-s-address-in-kerne.patch \
  0029-Emulate-GenerateConsoleCtrlEvent-upon-Ctrl-C.patch \
  0030-kill-kill-Win32-processes-more-gently.patch \
  0031-Cygwin-make-option-for-native-inner-link-handling.patch \
  0032-docs-skip-building-texinfo-and-PDF-files.patch \
  0033-install-libs-depend-on-the-toollibs.patch \
  0034-POSIX-ify-the-SHELL-variable.patch \
  0035-Handle-ORIGINAL_PATH-just-like-PATH.patch \
  0036-Cygwin-console-Maintain-ENABLE_-INSERT-QUICK_EDIT-_M.patch \
  0037-Cygwin-pty-console-Fix-Ctrl-C-handling-for-non-cygwi.patch \
  0038-Cygwin-pty-Pass-Ctrl-Z-EOF-to-non-cygwin-apps-with-d.patch \
  0039-Cygwin-pty-Prevent-deadlock-on-echo-output.patch \
  0040-Cygwin-pty-Revise-the-code-to-wait-for-completion-of.patch \
  0041-Cygwin-pty-Discard-input-in-from_master_nat-pipe-on-.patch \
  0042-Cygwin-pty-Fix-a-bug-in-tty_min-segpgid.patch \
  0043-Cygwin-console-Fix-console-mode-for-non-cygwin-infer.patch \
  0044-Cygwin-console-Set-console-mode-even-if-stdin-stdout.patch \
  0045-Cygwin-Fix-build-with-w32api-10.0.0.patch \
  0046-Cygwin-Fix-typo-KERB_S4U_LOGON_FLAG_IDENTITY-IDENTIF.patch
}

build() {
  [[ -d "${srcdir}"/build-${CHOST} ]] && rm -rf "${srcdir}"/build-${CHOST}
  mkdir -p "${srcdir}"/build-${CHOST} && cd "${srcdir}"/build-${CHOST}

  # Gives more verbose compile output when debugging.
  local -a extra_config
  if check_option "debug" "y"; then
    export CCWRAP_VERBOSE=1
    OPTIM="-O0"
    extra_config+=(--enable-debugging)
  else
    OPTIM="-O2"
  fi

  CFLAGS="$OPTIM -pipe -ggdb -Wno-error=deprecated -Wno-error=stringop-truncation -Wno-error=missing-attributes -Wno-error=maybe-uninitialized" #-Wno-error=class-memaccess
  CXXFLAGS="$OPTIM -pipe -ggdb -Wno-error=deprecated -Wno-error=stringop-truncation -Wno-error=missing-attributes -Wno-error=maybe-uninitialized" #-Wno-error=class-memaccess

  (cd "${srcdir}/msys2-runtime/winsup" && ./autogen.sh)

  "${srcdir}"/msys2-runtime/configure \
    --prefix=/usr \
    --build=${CHOST} \
    --sysconfdir=/etc \
    "${extra_config[@]}"
  LC_ALL=C make
  LC_ALL=C make -j1 DESTDIR="${srcdir}"/dest install

  #pushd ${CHOST}/winsup/cygwin > /dev/null
  #LANG=C make libmsys2_s.a
  #cp libmsys2_s.a "${srcdir}"/dest/usr/${CHOST}/lib/
  #popd > /dev/null

  rm -rf "${srcdir}"/dest/etc
}

package_msys2-runtime() {
  pkgdesc="Posix emulation engine for Windows"
  options=('!strip')
  conflicts=('catgets' 'libcatgets')
  replaces=('catgets' 'libcatgets')
  #install=msys2-runtime.install

  mkdir -p "${pkgdir}"/usr
  cp -rf "${srcdir}"/dest/usr/bin "${pkgdir}"/usr/
  cp -rf "${srcdir}"/dest/usr/libexec "${pkgdir}"/usr/
  rm -f "${pkgdir}"/usr/bin/msys-2.0.dbg
  rm -f "${pkgdir}"/usr/bin/cyglsa-config
  rm -f "${pkgdir}"/usr/bin/cyglsa.dll
  rm -f "${pkgdir}"/usr/bin/cyglsa64.dll
  rm -f "${pkgdir}"/usr/bin/cygserver-config
  cp -rf "${srcdir}"/dest/usr/share "${pkgdir}"/usr/
}

package_msys2-runtime-devel() {
  pkgdesc="MSYS2 headers and libraries"
  depends=("msys2-runtime=${pkgver}")
  options=('staticlibs' '!strip')
  conflicts=('libcatgets-devel')
  replaces=('libcatgets-devel')

  mkdir -p "${pkgdir}"/usr/bin
  cp -f "${srcdir}"/dest/usr/bin/msys-2.0.dbg "${pkgdir}"/usr/bin/
  cp -rLf "${srcdir}"/dest/usr/${CHOST}/include "${pkgdir}"/usr/
  rm -f "${pkgdir}"/usr/include/iconv.h
  rm -f "${pkgdir}"/usr/include/unctrl.h
  # provided by libtirpc
  rm -fr "${pkgdir}"/usr/include/rpc/

  cp -rLf "${srcdir}"/dest/usr/${CHOST}/lib "${pkgdir}"/usr/
}

# return 0
# To hack on this:
# cd /c/repo-MSYS2/msys2-runtime/
# pushd src/build-i686-pc-msys
# LANG=C make && make -j1 DESTDIR=/c/repo-MSYS2/msys2-runtime/src/dest install
# popd
# makepkg -sRLf
# pacman -U msys2-runtime*.xz

# Quicker:
# open cmd.exe
# set "PATH=C:\\msys32\\usr\\bin;%PATH%"
# E:
# pushd E:\m2\repo-MSYS2\msys2-runtime\src\build-i686-pc-msys\i686-pc-msys\winsup\cygwin
# C:/msys32/usr/bin/bash -c "LANG=C && make"
# copy /y new-msys-2.0.dll C:\msys32\usr\bin\msys-2.0.dll
# popd
# C:
# C:/msys32/usr/bin/strace ls / > C:/strace.txt 2>&1
#
