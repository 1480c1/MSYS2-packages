# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Ray Donnelly <mingw.android@gmail.com>

pkgbase=msys2-runtime
pkgname=('msys2-runtime' 'msys2-runtime-devel')
pkgver=3.4.3
pkgrel=5
pkgdesc="Cygwin POSIX emulation engine"
arch=('x86_64')
url="https://www.cygwin.com/"
license=('GPL')
makedepends=('cocom'
             'git'
             'perl'
             'gcc'
             'mingw-w64-cross-crt-git'
             'mingw-w64-cross-gcc'
             'mingw-w64-cross-zlib'
             'zlib-devel'
             'gettext-devel'
             'libiconv-devel'
             'autotools'
             'xmlto'
             'docbook-xsl')
# re zipman: https://github.com/msys2/MSYS2-packages/pull/2687#issuecomment-965714874
options=('!zipman')
source=('msys2-runtime'::git://sourceware.org/git/newlib-cygwin.git#tag=cygwin-${pkgver}
        0001-Add-MSYS2-triplet.patch
        0002-Fix-msys-library-name-in-import-libraries.patch
        0003-Rename-dll-from-cygwin-to-msys.patch
        0004-Add-functionality-for-converting-UNIX-paths-in-argum.patch
        0005-Add-functionality-for-changing-OS-name-via-MSYSTEM-e.patch
        0006-Move-root-to-usr.-Change-sorting-mount-points.-By-de.patch
        0007-Instead-of-creating-Cygwin-symlinks-use-deep-copy-by.patch
        0008-Automatically-rewrite-TERM-msys-to-TERM-cygwin.patch
        0009-Do-not-convert-environment-for-strace.patch
        0010-path_conv-special-case-root-directory-to-have-traili.patch
        0011-dcrt0.cc-Untangle-allow_glob-from-winshell.patch
        0012-dcrt0.cc-globify-Don-t-quote-literal-strings-differe.patch
        0013-strace.cc-Don-t-set-MSYS-noglob.patch
        0014-Add-debugging-for-build_argv.patch
        0015-Add-debugging-for-strace-make_command_line.patch
        0016-environ.cc-New-facility-environment-variable-MSYS2_E.patch
        0017-Fix-native-symbolic-link-spawn-passing-wrong-arg0.patch
        0018-strace-quiet-be-really-quiet.patch
        0019-Introduce-the-enable_pcon-value-for-MSYS.patch
        0020-popen-call-usr-bin-sh-instead-of-bin-sh.patch
        0021-Expose-full-command-lines-to-other-Win32-processes-b.patch
        0022-Do-not-show-Error-dialogs-by-default.patch
        0023-Add-a-helper-to-obtain-a-function-s-address-in-kerne.patch
        0024-Emulate-GenerateConsoleCtrlEvent-upon-Ctrl-C.patch
        0025-kill-kill-Win32-processes-more-gently.patch
        0026-Cygwin-make-option-for-native-inner-link-handling.patch
        0027-docs-skip-building-texinfo-and-PDF-files.patch
        0028-install-libs-depend-on-the-toollibs.patch
        0029-POSIX-ify-the-SHELL-variable.patch
        0030-Handle-ORIGINAL_PATH-just-like-PATH.patch
        0031-uname-allow-setting-the-system-name-to-CYGWIN.patch
        0032-Pass-environment-variables-with-empty-values.patch
        0033-Optionally-disallow-empty-environment-values-again.patch
        0034-build_env-respect-the-MSYS-environment-variable.patch
        0035-When-converting-to-a-Unix-path-avoid-double-trailing.patch
        0036-msys2_path_conv-pass-PC_NOFULL-to-path_conv.patch
        0037-Revert-Cygwin-Enable-dynamicbase-on-the-Cygwin-DLL-b.patch)
sha256sums=('SKIP'
            '26fce1982a4f7e932fe302ca053d4bcf1fbe19a91c54ba5e611f7850d16f795e'
            'd99c884591a640e018f4a03f9b67e04e73cb33998ad0a8c9dd40373383e99c9b'
            '2509540dcd0669f31fc943c37f03021694638a5507666f20ca9dcdf83e3eac7b'
            'bb47dbf2fef809a15f2d8df16e2359b877fa3a999e708ed9388b0dd0b8ccee98'
            '76da9fd4ae8443944d1bf3f23035e861946c4500d4784649cf61a7ef4c894ac4'
            '77ce24434187676b18ac2d5387c6b9b293a1f43d709f5fc93cb5f142600b041d'
            '397e61ea91710d3a516c99b721c72c611c4eb5e4536617a2ec0ea3fb45c640da'
            '2ab180de61314ef7da51c947166328e6769bf70fffbb61f581f91b3b082bb4ec'
            'f7d498d8c4357064c26bcf63eaceca9970d6fb081e9c6714938f50840ad21335'
            'ed33d336a3b6183c81ca674dd4fcadc8a1cdf4e766d08c5f983977382f1a9cdc'
            '431aedc2296105d1f399da48564480aad01cd1a1cea13ce6c203f59beec48e5e'
            'eb9ed42fbbb0436da0aa33ad584718918cbb45f6f05bf0b29fc36649915fe696'
            '00c100cce5a9f1dca43ead3e8b916ea3de0adb92d6e14d0cfd4591ca06b2d25c'
            '4e42d075971634b33876b616f221cf1e76fb9c249f1b5e0a30014e9cb6133475'
            '17d3f03001ae2a6aa325f10b9581e7ec2b9bff4bef0c2ba05eb431d3e4d2ccbd'
            '667e630f66190c6af94c9512c0bbd9271f711f290598845ddf9fc56af6ba4daf'
            '9acad8f8654c4fa1bc34226f42036543bc5a867ff6b245e718b4fb96fc2bc9c1'
            'abc3832279dc0edb720da7b03d73a00151c1a9f4802e7b2595bf5e93992b3129'
            'f8552defb45d78f84f8caffa03b0789d6ee8741630cee6be1ec09b1aedc11127'
            '0a46aa72027163939dacd40e8e47d9b1e93584082f25a027f33af43afbc30aad'
            'f3576bf8d6eda00a614ed5c636f947b08310d2f9b57f1684b40138831ea22180'
            '6f9ef4089710b7595afc2722ce068c1d993ca4b7037049493c2383884cbd6ca2'
            '6a0162cd9f552d7a069d4730d73935755167a52a4aba727c80a230fe85a7d6f4'
            '23b57f96ba092e19fd633f0862d506d572f764848514a5d9b135178b5f267cd1'
            '87bfea288b66b05a47bb1332b0bb9f7b8bd0c29682045a31d942d733a894224e'
            '8335b5eb6b0108cb4519a7367e2dc842d201b9a1ce2d6ae7f0c100fc506c5438'
            'baa1fbcc1ab9a7e71a1e3b3f0ff1dd51ba554e312fd459ac3466893dd9b397a5'
            'f41c098c7528b2ed7525882b790c3a6fe53a24c6b73040380ce8dc90d1fc8aec'
            '410c71723374a91c0b2f760acdcb2287112a0302267f7131ffb4adae712b1e05'
            '5988a8a4a6527477fcfd2bee177c805cfbd50920fd0ae5011eb4ab40793a50d8'
            'c0dd98360b3a841ccbb331ae396fa52aa2a397e69ec7b3316d02a8d9eb3ea72d'
            '340fe2d262514d7ae8164ecf34d1696442263f063b9594aa10ebfb9bc0794eed'
            '6793a01abc766c911fd774ec732889d744591b22df3aa4035c8a7ada89bd8f0f'
            '7d6500214add9ed8735cd7d8520a9b9920f87dded1788e4961d12fcba3cb5a85'
            '0b2f3a158ce75369503eb55ff1d0182a33968228de33f30fdc89e506e5959662'
            '2508a119e5fd158a883059668660317e7fc60f44a1d871ec59301a5541b73626'
            'c1cb4531f83d51dc496ecc7b1cf735f7abf985fb1066bc8bbab6283992d71e9e')

# Helper macros to help make tasks easier #
apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying $_patch"
    patch -Nbp1 -i "${srcdir}/${_patch}"
  done
}

apply_git_am_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying $_patch"
    git apply "${srcdir}/${_patch}"
  done
}

del_file_exists() {
  for _fname in "$@"
  do
    if [ -f $_fname ]; then
      rm -rf $_fname
    fi
  done
}
# =========================================== #

prepare() {
  cd "${srcdir}"/msys2-runtime
  if test true != "$(git config core.symlinks)"
  then
    git config core.symlinks true &&
    /usr/bin/git reset --hard
  fi
  del_file_exists winsup/cygwin/msys2_path_conv.cc \
    winsup/cygwin/msys2_path_conv.h
 apply_git_am_with_msg 0001-Add-MSYS2-triplet.patch \
  0002-Fix-msys-library-name-in-import-libraries.patch \
  0003-Rename-dll-from-cygwin-to-msys.patch \
  0004-Add-functionality-for-converting-UNIX-paths-in-argum.patch \
  0005-Add-functionality-for-changing-OS-name-via-MSYSTEM-e.patch \
  0006-Move-root-to-usr.-Change-sorting-mount-points.-By-de.patch \
  0007-Instead-of-creating-Cygwin-symlinks-use-deep-copy-by.patch \
  0008-Automatically-rewrite-TERM-msys-to-TERM-cygwin.patch \
  0009-Do-not-convert-environment-for-strace.patch \
  0010-path_conv-special-case-root-directory-to-have-traili.patch \
  0011-dcrt0.cc-Untangle-allow_glob-from-winshell.patch \
  0012-dcrt0.cc-globify-Don-t-quote-literal-strings-differe.patch \
  0013-strace.cc-Don-t-set-MSYS-noglob.patch \
  0014-Add-debugging-for-build_argv.patch \
  0015-Add-debugging-for-strace-make_command_line.patch \
  0016-environ.cc-New-facility-environment-variable-MSYS2_E.patch \
  0017-Fix-native-symbolic-link-spawn-passing-wrong-arg0.patch \
  0018-strace-quiet-be-really-quiet.patch \
  0019-Introduce-the-enable_pcon-value-for-MSYS.patch \
  0020-popen-call-usr-bin-sh-instead-of-bin-sh.patch \
  0021-Expose-full-command-lines-to-other-Win32-processes-b.patch \
  0022-Do-not-show-Error-dialogs-by-default.patch \
  0023-Add-a-helper-to-obtain-a-function-s-address-in-kerne.patch \
  0024-Emulate-GenerateConsoleCtrlEvent-upon-Ctrl-C.patch \
  0025-kill-kill-Win32-processes-more-gently.patch \
  0026-Cygwin-make-option-for-native-inner-link-handling.patch \
  0027-docs-skip-building-texinfo-and-PDF-files.patch \
  0028-install-libs-depend-on-the-toollibs.patch \
  0029-POSIX-ify-the-SHELL-variable.patch \
  0030-Handle-ORIGINAL_PATH-just-like-PATH.patch \
  0031-uname-allow-setting-the-system-name-to-CYGWIN.patch \
  0032-Pass-environment-variables-with-empty-values.patch \
  0033-Optionally-disallow-empty-environment-values-again.patch \
  0034-build_env-respect-the-MSYS-environment-variable.patch \
  0035-When-converting-to-a-Unix-path-avoid-double-trailing.patch \
  0036-msys2_path_conv-pass-PC_NOFULL-to-path_conv.patch \
  0037-Revert-Cygwin-Enable-dynamicbase-on-the-Cygwin-DLL-b.patch
}

build() {
  [[ -d "${srcdir}"/build-${CHOST} ]] && rm -rf "${srcdir}"/build-${CHOST}
  mkdir -p "${srcdir}"/build-${CHOST} && cd "${srcdir}"/build-${CHOST}

  # Gives more verbose compile output when debugging.
  local -a extra_config
  if check_option "debug" "y"; then
    export CCWRAP_VERBOSE=1
    OPTIM="-O0"
    extra_config+=(--enable-debugging)
  else
    OPTIM="-O2"
  fi

  CFLAGS="$OPTIM -pipe -ggdb -Wno-error=deprecated -Wno-error=stringop-truncation -Wno-error=missing-attributes -Wno-error=maybe-uninitialized" #-Wno-error=class-memaccess
  CXXFLAGS="$OPTIM -pipe -ggdb -Wno-error=deprecated -Wno-error=stringop-truncation -Wno-error=missing-attributes -Wno-error=maybe-uninitialized" #-Wno-error=class-memaccess

  # otherwise it asks git which appends "-dirty" because of our uncommited patches
  CFLAGS+=" -DCYGPORT_RELEASE_INFO=${pkgver}"

  (cd "${srcdir}/msys2-runtime/winsup" && ./autogen.sh)

  "${srcdir}"/msys2-runtime/configure \
    --prefix=/usr \
    --build=${CHOST} \
    --sysconfdir=/etc \
    "${extra_config[@]}"
  LC_ALL=C make
  LC_ALL=C make -j1 DESTDIR="${srcdir}"/dest install

  #pushd ${CHOST}/winsup/cygwin > /dev/null
  #LANG=C make libmsys2_s.a
  #cp libmsys2_s.a "${srcdir}"/dest/usr/${CHOST}/lib/
  #popd > /dev/null

  rm -rf "${srcdir}"/dest/etc
}

package_msys2-runtime() {
  pkgdesc="Posix emulation engine for Windows"
  conflicts=('catgets' 'libcatgets' 'msys2-runtime-3.4')
  replaces=('catgets' 'libcatgets' 'msys2-runtime-3.4')

  mkdir -p "${pkgdir}"/usr
  cp -rf "${srcdir}"/dest/usr/bin "${pkgdir}"/usr/
  cp -rf "${srcdir}"/dest/usr/libexec "${pkgdir}"/usr/
  rm -f "${pkgdir}"/usr/bin/cyglsa-config
  rm -f "${pkgdir}"/usr/bin/cyglsa.dll
  rm -f "${pkgdir}"/usr/bin/cyglsa64.dll
  rm -f "${pkgdir}"/usr/bin/cygserver-config
  cp -rf "${srcdir}"/dest/usr/share "${pkgdir}"/usr/
}

package_msys2-runtime-devel() {
  pkgdesc="MSYS2 headers and libraries"
  depends=("msys2-runtime=${pkgver}")
  conflicts=('libcatgets-devel' 'msys2-runtime-3.4-devel')
  replaces=('libcatgets-devel' 'msys2-runtime-3.4-devel')

  mkdir -p "${pkgdir}"/usr/bin
  cp -rLf "${srcdir}"/dest/usr/${CHOST}/include "${pkgdir}"/usr/
  rm -f "${pkgdir}"/usr/include/iconv.h
  rm -f "${pkgdir}"/usr/include/unctrl.h
  # provided by libtirpc
  rm -fr "${pkgdir}"/usr/include/rpc/

  cp -rLf "${srcdir}"/dest/usr/${CHOST}/lib "${pkgdir}"/usr/
}

# return 0
# To hack on this:
# cd /c/repo-MSYS2/msys2-runtime/
# pushd src/build-i686-pc-msys
# LANG=C make && make -j1 DESTDIR=/c/repo-MSYS2/msys2-runtime/src/dest install
# popd
# makepkg -sRLf
# pacman -U msys2-runtime*.xz

# Quicker:
# open cmd.exe
# set "PATH=C:\\msys32\\usr\\bin;%PATH%"
# E:
# pushd E:\m2\repo-MSYS2\msys2-runtime\src\build-i686-pc-msys\i686-pc-msys\winsup\cygwin
# C:/msys32/usr/bin/bash -c "LANG=C && make"
# copy /y new-msys-2.0.dll C:\msys32\usr\bin\msys-2.0.dll
# popd
# C:
# C:/msys32/usr/bin/strace ls / > C:/strace.txt 2>&1
#
