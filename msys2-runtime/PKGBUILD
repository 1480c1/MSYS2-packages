# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Ray Donnelly <mingw.android@gmail.com>

pkgbase=msys2-runtime
pkgname=('msys2-runtime' 'msys2-runtime-devel')
pkgver=3.3.6
pkgrel=2
pkgdesc="Cygwin POSIX emulation engine"
arch=('i686' 'x86_64')
url="https://www.cygwin.com/"
license=('GPL')
makedepends=('cocom'
             'git'
             'perl'
             'gcc'
             'mingw-w64-cross-crt-git'
             'mingw-w64-cross-gcc'
             'mingw-w64-cross-zlib'
             'zlib-devel'
             'gettext-devel'
             'libiconv-devel'
             'autotools'
             'xmlto'
             'docbook-xsl')
# re zipman: https://github.com/msys2/MSYS2-packages/pull/2687#issuecomment-965714874
options=('!zipman')
source=('msys2-runtime'::git://sourceware.org/git/newlib-cygwin.git#tag=cygwin-${pkgver//./_}-release
        0001-Add-MSYS2-triplet.patch
        0002-Fix-msys-library-name-in-import-libraries.patch
        0003-Rename-dll-from-cygwin-to-msys.patch
        0004-Add-functionality-for-converting-UNIX-paths-in-argum.patch
        0005-Add-functionality-for-changing-OS-name-via-MSYSTEM-e.patch
        0006-Move-root-to-usr.-Change-sorting-mount-points.-By-de.patch
        0007-Instead-of-creating-Cygwin-symlinks-use-deep-copy-by.patch
        0008-Automatically-rewrite-TERM-msys-to-TERM-cygwin-With-.patch
        0009-Do-not-convert-environment-for-strace.patch
        0010-Special-case-for-converting-root-directory-to-have-t.patch
        0011-dcrt0.cc-Untangle-allow_glob-from-winshell.patch
        0012-dcrt0.cc-globify-Don-t-quote-literal-strings-differe.patch
        0013-strace.cc-Don-t-set-MSYS-noglob.patch
        0014-Add-debugging-for-build_argv.patch
        0015-Add-debugging-for-strace-make_command_line.patch
        0016-environ.cc-New-facility-environment-variable-MSYS2_E.patch
        0017-Fix-native-symbolic-link-spawn-passing-wrong-arg0.patch
        0018-QueryUnbiasedInterruptTime-must-be-load-from-kernel3.patch
        0019-strace-quiet-be-really-quiet.patch
        0020-Default-to-disable_pcon.patch
        0021-Introduce-the-enable_pcon-value-for-MSYS.patch
        0022-popen-call-usr-bin-sh-instead-of-bin-sh.patch
        0023-CI-add-a-GHA-for-doing-a-basic-build-test.patch
        0024-Set-up-a-GitHub-Action-to-keep-in-sync-with-Cygwin.patch
        0025-Expose-full-command-lines-to-other-Win32-processes-b.patch
        0026-Disable-the-cygwin-GitHub-workflow.patch
        0027-Do-not-show-Error-dialogs-by-default.patch
        0028-Add-a-helper-to-obtain-a-function-s-address-in-kerne.patch
        0029-Emulate-GenerateConsoleCtrlEvent-upon-Ctrl-C.patch
        0030-kill-kill-Win32-processes-more-gently.patch
        0031-Cygwin-make-option-for-native-inner-link-handling.patch
        0032-docs-skip-building-texinfo-and-PDF-files.patch
        0033-install-libs-depend-on-the-toollibs.patch
        0034-POSIX-ify-the-SHELL-variable.patch
        0035-Handle-ORIGINAL_PATH-just-like-PATH.patch
        0036-uname-allow-setting-the-system-name-to-CYGWIN.patch
        0037-fixup-Revert-Default-to-disable_pcon.patch
        0038-Pass-environment-variables-with-empty-values.patch
        0039-Optionally-disallow-empty-environment-values-again.patch
        0040-build_env-respect-the-MSYS-environment-variable.patch)
sha256sums=('SKIP'
            '8e26c673b763366a085eada2250bfcaf182d1f357eb23a62c57a20fe8a0346f3'
            'c4cc134ab6fa1c17b5ee428c8604861f1a4580ac14aef25740da8597de84800e'
            'f30d275031db52430c7ddef7ed1e54eb8d934251b11c812725d32d1ba380a3e9'
            '09effb95abc166cde9c3a3d7f9aa1bd53c1161c3706c6c5e85c29802dfe7fe63'
            '7208bf2dffe1c27f9a8ff63e24ee3b4f9e7448aef8a5bd6f014b50b63bb49bae'
            'c0c833ee0f038fcb29d56a49df62278d0e489ca85e6e4ca8b7ae6600c7bc69a3'
            '99e2e82848a9c4e56f75bec6d016f8d38869373f9c809029ed2179eef5efe033'
            'bd6174a58ebe88c29421f23ccfaee157e4fa18a4b497e2d391875b2507875a63'
            '2d36efb8c2355c8d6eb4652cea2abcf7b5cec1e8931dc33d90dc7ee0ca69e1df'
            '92e8553378e380b8db643d7ca3604b84b13a0dba38263d6aca33169bcf62214c'
            'a6cd762892edd38bf8c62b513fe3be1c8bdcfdabe5596814185dbf37c40cdec3'
            '1b0bd86c9d9d489d00396ab259f0a5fd6489aef24c47ba51c1ba85b0d23e0765'
            '337674665b7bfd58a80766d5d7d91390796ac2f4426db9cc071e00e3914e29d5'
            '779949b0f9143b68733ec7fe0f01f2eaea8e589df5fd1dd2fe4fc9046ff73b18'
            '15236a2889c68d6f97dde91059fb725c855c3085f89ca8735d82a5727c8e05c1'
            '1bf0f0f554c57e3d75fd936eb6e1a0dbffe192e91164ffe16ba0e95da6298326'
            'd0d704e9c8163c8bd731aab8785396d64262a16580e219426052895f63929187'
            '35948eb27c4eec64f986eea4689963af547dba4c8722d2c97607a2164a9bb78c'
            '324e313839c4d78e1a2bb8e37eee143f9ba3bf8f8e6c97c09f20fc945503734b'
            'ddcb985482a8409227252e95e790ce3de3c256d18af29fa8bb3e1887fbeee585'
            '535dbc7eae11e9cc5307b7023e967824ce187ab981e31663af289107cfc9757e'
            'a35032b628032deadad31fde7c7dd04c3a28f15095fb10a31f26e0f489f35d69'
            '3f43a8ae895f0100399b198ad66897ae59d74da7c2e8ceba0fc6d82582d4ba34'
            '5680983a9ac7943800aec6b1504cd0c7300d9590d3e0321a9fd2091005595aa7'
            'd33334697d2b345e9cd54e8a879aef497d5f699ed194072123fed7268a7155a7'
            'a0b5a0d39b7585885b52d8df3522ab7bf9a4b831f7e64a4ce4842135b363124b'
            '52e924fe006400dbe2def51ac04a09173a6e0fabaa6417b43f5114bc35e28ca1'
            'fc0605ba52e4091081063a98f7757110d2f353027d2e146960531299d2c3b3bc'
            '3dc45ac3b3bf2b32bb7e17639e3dd8ed599bad636c9883aa0fa77dfa79471432'
            '4c340f2b767833a11b0b57d33780f6150bba231946f2cece334dbb46475499a6'
            '835ac7031ff00e4a70bb2b7e5a4e4c75dc8c449b4d8095fc59dada42068fb380'
            'd19dee1e9255789387a4d79d1f6f469adb8d0cb3424c6469049c917df589b0a6'
            '4631a6186534f7b9abbd5685f858cffc40634df540cb7db5f3102bf7e6f6dfc6'
            'ba34f8b5bc39e273a5e594ba3f893367359fb72ed5fe8f9ed80d03a7702041c9'
            '8ebe2924fd3bfcc5fe51e731ddb29b800f0f49c7776ce2af084300888cb966c2'
            '90f482551aa593215c0eb574863f5d6f492b0cca63469a8c80eaae72b82dee74'
            'f4641a5c4c9571362ef7dc855d3340cb9bf7fe512a2042e92e6b8e3f93a5293f'
            '57d4380abbd5536dacb9efea24749756af31c6926473f78a62681d65b431b3a9'
            'c4d5ad6a5960b735ddd900445dc2c3428c1d1cea1454150a8b8b77ff09c42dc9'
            'd12ae4e081ef49e5f00579baf45d9c3e245ed1c3542ea988f81ac38b5f00c71d')

# Helper macros to help make tasks easier #
apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying $_patch"
    patch -Nbp1 -i "${srcdir}/${_patch}"
  done
}

apply_git_am_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying $_patch"
    git apply "${srcdir}/${_patch}"
  done
}

del_file_exists() {
  for _fname in "$@"
  do
    if [ -f $_fname ]; then
      rm -rf $_fname
    fi
  done
}
# =========================================== #

prepare() {
  cd "${srcdir}"/msys2-runtime
  if test true != "$(git config core.symlinks)"
  then
    git config core.symlinks true &&
    /usr/bin/git reset --hard
  fi
  del_file_exists winsup/cygwin/msys2_path_conv.cc \
    winsup/cygwin/msys2_path_conv.h
 apply_git_am_with_msg 0001-Add-MSYS2-triplet.patch \
  0002-Fix-msys-library-name-in-import-libraries.patch \
  0003-Rename-dll-from-cygwin-to-msys.patch \
  0004-Add-functionality-for-converting-UNIX-paths-in-argum.patch \
  0005-Add-functionality-for-changing-OS-name-via-MSYSTEM-e.patch \
  0006-Move-root-to-usr.-Change-sorting-mount-points.-By-de.patch \
  0007-Instead-of-creating-Cygwin-symlinks-use-deep-copy-by.patch \
  0008-Automatically-rewrite-TERM-msys-to-TERM-cygwin-With-.patch \
  0009-Do-not-convert-environment-for-strace.patch \
  0010-Special-case-for-converting-root-directory-to-have-t.patch \
  0011-dcrt0.cc-Untangle-allow_glob-from-winshell.patch \
  0012-dcrt0.cc-globify-Don-t-quote-literal-strings-differe.patch \
  0013-strace.cc-Don-t-set-MSYS-noglob.patch \
  0014-Add-debugging-for-build_argv.patch \
  0015-Add-debugging-for-strace-make_command_line.patch \
  0016-environ.cc-New-facility-environment-variable-MSYS2_E.patch \
  0017-Fix-native-symbolic-link-spawn-passing-wrong-arg0.patch \
  0018-QueryUnbiasedInterruptTime-must-be-load-from-kernel3.patch \
  0019-strace-quiet-be-really-quiet.patch \
  0020-Default-to-disable_pcon.patch \
  0021-Introduce-the-enable_pcon-value-for-MSYS.patch \
  0022-popen-call-usr-bin-sh-instead-of-bin-sh.patch \
  0023-CI-add-a-GHA-for-doing-a-basic-build-test.patch \
  0024-Set-up-a-GitHub-Action-to-keep-in-sync-with-Cygwin.patch \
  0025-Expose-full-command-lines-to-other-Win32-processes-b.patch \
  0026-Disable-the-cygwin-GitHub-workflow.patch \
  0027-Do-not-show-Error-dialogs-by-default.patch \
  0028-Add-a-helper-to-obtain-a-function-s-address-in-kerne.patch \
  0029-Emulate-GenerateConsoleCtrlEvent-upon-Ctrl-C.patch \
  0030-kill-kill-Win32-processes-more-gently.patch \
  0031-Cygwin-make-option-for-native-inner-link-handling.patch \
  0032-docs-skip-building-texinfo-and-PDF-files.patch \
  0033-install-libs-depend-on-the-toollibs.patch \
  0034-POSIX-ify-the-SHELL-variable.patch \
  0035-Handle-ORIGINAL_PATH-just-like-PATH.patch \
  0036-uname-allow-setting-the-system-name-to-CYGWIN.patch \
  0037-fixup-Revert-Default-to-disable_pcon.patch \
  0038-Pass-environment-variables-with-empty-values.patch \
  0039-Optionally-disallow-empty-environment-values-again.patch \
  0040-build_env-respect-the-MSYS-environment-variable.patch
}

build() {
  [[ -d "${srcdir}"/build-${CHOST} ]] && rm -rf "${srcdir}"/build-${CHOST}
  mkdir -p "${srcdir}"/build-${CHOST} && cd "${srcdir}"/build-${CHOST}

  # Gives more verbose compile output when debugging.
  local -a extra_config
  if check_option "debug" "y"; then
    export CCWRAP_VERBOSE=1
    OPTIM="-O0"
    extra_config+=(--enable-debugging)
  else
    OPTIM="-O2"
  fi

  CFLAGS="$OPTIM -pipe -ggdb -Wno-error=deprecated -Wno-error=stringop-truncation -Wno-error=missing-attributes -Wno-error=maybe-uninitialized" #-Wno-error=class-memaccess
  CXXFLAGS="$OPTIM -pipe -ggdb -Wno-error=deprecated -Wno-error=stringop-truncation -Wno-error=missing-attributes -Wno-error=maybe-uninitialized" #-Wno-error=class-memaccess

  (cd "${srcdir}/msys2-runtime/winsup" && ./autogen.sh)

  "${srcdir}"/msys2-runtime/configure \
    --prefix=/usr \
    --build=${CHOST} \
    --sysconfdir=/etc \
    "${extra_config[@]}"
  LC_ALL=C make
  LC_ALL=C make -j1 DESTDIR="${srcdir}"/dest install

  #pushd ${CHOST}/winsup/cygwin > /dev/null
  #LANG=C make libmsys2_s.a
  #cp libmsys2_s.a "${srcdir}"/dest/usr/${CHOST}/lib/
  #popd > /dev/null

  rm -rf "${srcdir}"/dest/etc
}

package_msys2-runtime() {
  pkgdesc="Posix emulation engine for Windows"
  options=('!strip')
  conflicts=('catgets' 'libcatgets')
  replaces=('catgets' 'libcatgets')

  mkdir -p "${pkgdir}"/usr
  cp -rf "${srcdir}"/dest/usr/bin "${pkgdir}"/usr/
  cp -rf "${srcdir}"/dest/usr/libexec "${pkgdir}"/usr/
  rm -f "${pkgdir}"/usr/bin/msys-2.0.dbg
  rm -f "${pkgdir}"/usr/bin/cyglsa-config
  rm -f "${pkgdir}"/usr/bin/cyglsa.dll
  rm -f "${pkgdir}"/usr/bin/cyglsa64.dll
  rm -f "${pkgdir}"/usr/bin/cygserver-config
  cp -rf "${srcdir}"/dest/usr/share "${pkgdir}"/usr/
}

package_msys2-runtime-devel() {
  pkgdesc="MSYS2 headers and libraries"
  depends=("msys2-runtime=${pkgver}")
  options=('staticlibs' '!strip')
  conflicts=('libcatgets-devel')
  replaces=('libcatgets-devel')

  mkdir -p "${pkgdir}"/usr/bin
  cp -f "${srcdir}"/dest/usr/bin/msys-2.0.dbg "${pkgdir}"/usr/bin/
  cp -rLf "${srcdir}"/dest/usr/${CHOST}/include "${pkgdir}"/usr/
  rm -f "${pkgdir}"/usr/include/iconv.h
  rm -f "${pkgdir}"/usr/include/unctrl.h
  # provided by libtirpc
  rm -fr "${pkgdir}"/usr/include/rpc/

  cp -rLf "${srcdir}"/dest/usr/${CHOST}/lib "${pkgdir}"/usr/
}

# return 0
# To hack on this:
# cd /c/repo-MSYS2/msys2-runtime/
# pushd src/build-i686-pc-msys
# LANG=C make && make -j1 DESTDIR=/c/repo-MSYS2/msys2-runtime/src/dest install
# popd
# makepkg -sRLf
# pacman -U msys2-runtime*.xz

# Quicker:
# open cmd.exe
# set "PATH=C:\\msys32\\usr\\bin;%PATH%"
# E:
# pushd E:\m2\repo-MSYS2\msys2-runtime\src\build-i686-pc-msys\i686-pc-msys\winsup\cygwin
# C:/msys32/usr/bin/bash -c "LANG=C && make"
# copy /y new-msys-2.0.dll C:\msys32\usr\bin\msys-2.0.dll
# popd
# C:
# C:/msys32/usr/bin/strace ls / > C:/strace.txt 2>&1
#
