# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Martell Malone <martellmalone@gmail.com>
# Contributor: Ray Donnelly <mingw.android@gmail.com>

pkgname=pacman
pkgver=6.0.1
pkgrel=11
pkgdesc="A library-based package manager with dependency support (MSYS2 port)"
arch=('i686' 'x86_64')
url="https://www.archlinux.org/pacman/"
license=('GPL')
groups=('base-devel')
depends=('bash>=4.2.045'
         'gettext'
         'gnupg'
         'curl'
         'pacman-mirrors'
         'msys2-keyring'
         'which'
         'bzip2'
         'xz'
         'zstd')
checkdepends=('python')
makedepends=('asciidoc'
             'doxygen'
             'git'
             'gcc'
             'meson'
             'ninja'
             'gettext-devel'
             'heimdal-devel'
             'libarchive-devel'
             'libcurl-devel'
             'libgpgme-devel'
             'libsqlite-devel'
             'libunistring-devel')
backup=("etc/pacman.conf"
        "etc/makepkg.conf"
        "etc/makepkg_mingw.conf")
_commit="0a6fecd07271a54d9009ea7204c0e6288a44212b"
source=(pacman-${pkgver}::git+https://gitlab.archlinux.org/pacman/pacman.git#commit=${_commit}
        "pacman.conf"
        "makepkg.conf"
        "makepkg_mingw.conf"
        "makepkg-mingw"
        0001-Msysize.patch
        0002-More-debugging-info.patch
        0003-Core-update.patch
        0004-Remove-ldconfig.patch
        0005-Change-the-epoch-separator.patch
        0006-makepkg-avoid-creating-.tar-files-with-extended-attr.patch
        0007-pacman-libalpm-ignore-file-conflicts-for-foo.exe-foo.patch
        0008-Change-default-answer-of-all-queries-to-yes.patch
        0009-Use-pipe-instead-of-socket.patch
        0010-pacman-make-file-list-comparisons-between-packages-c.patch
        0011-Translate-MSYS2-specific-messages-into-Japanese.patch
        0012-Workaround-compressing-packages-with-bsdtar-under-Ac.patch
        0013-Fix-asciidoc-argparse.patch
        0014-Remove-fakeroot.patch
        0015-Remove-sudo.patch
        0016-Use-msys-tools.patch
        0017-Export-CC-and-CXX-variables-explicitly.patch
        0018-pacman.c-handle-cr-on-stdin-as-well.patch
        0019-Add-a-CI-job.patch
        0020-Strip-static-libraries-as-well.patch
        0021-Fix-building-staticlibs-with-LTO.patch
        0022-fixup-Export-CC-and-CXX-variables-explicitly.patch)
validpgpkeys=('6645B0A8C7005E78DB1D7864F99FFE0FEAE999BD'  # Allan McRae <allan@archlinux.org>
              'B8151B117037781095514CA7BBDFFC92306B1121') # Andrew Gregory (pacman) <andrew@archlinux.org>
sha256sums=('SKIP'
            'fff047d9514c4810eee9ab798b51d3300cbf7ee1b5e2e494e1350499d28dd448'
            'a418696bbe34cb1a17e6d3054fb5b1b65148a2dfed035d1773c72b494c6e5de2'
            '815676330c53fd5ca047d9cc5ce14551c426add51eee3b20527811bc20e03d3a'
            '98198e1f0f252eae0560d271bee4b9149e127399dd0d3fd5d8d24579d9e0550f'
            'fe9db024623179e42242af57ca80867cee6cfa8634214cca6721f42800aecb09'
            '160e1135fe69341af6bc67e58b13b2f12bfdaf8152425f4c2c314ff339a6559b'
            '4c0e2fe7f7ac921037e90ae6aac1b1778f67fa3b80dc1a5491d3bc6e406f2779'
            '65ad517673b02eef4031279ed1a34a98dda33a88ec090f37a794be6c42c156f0'
            'c7622ed144f1e915954637b5d8063cfafd41c711837181f80cb6b6be47ee39cc'
            '608012fde55d67c998425bf948f1b6cced6996f8f23ffa8519fabc64da7c32c0'
            'ec0d34468457d655e731dd1c21ed492198d1f7001b83c471d77c492dd78f6ea9'
            '0453d869ff8de53390833b137a974a301b1dd6018a204203dae612df7b28b35a'
            'b8d0567b4475373c5acca79cf309120a856266991901500ca6cdff30ce4d9930'
            'a903302fee8549d6d60f917cec4a0546b09bf03d83fca167ea7dedc86796c5ea'
            '45edc0291534491db8ffe721894756d3b0f7f471052d991ccb9bce939d45b33b'
            'c03191c52fec5b60edaf8351e564c931e0a6b2c9ddafd49d5043da5d88366ab9'
            'b542b4be1d170c2eaad9c5913d68f3b3e81bca63148c973a4af2cb7d295fc8b8'
            '5aa736f44ad2bde1a4646ad96b1d264aabafcf64d7cc767f514f47b1ec435822'
            '4adf61ad10b30c62a9786fe35f06554b4b6a0b6582e157d1c4e2a514c5962baa'
            '8a7c23d1230ddb22a6f165a8a7d7e154d1a4ed658f22766f501fd5d300d138c9'
            'dee87cbe931f59053f7eeaf1640e0f17b9628216951e110b34ab78a2d59c05f9'
            '214bbebd943e6aa80fd71cbd474ed40935517110ebb61524cafa9337369a4200'
            '8f2fee02f186c0108d7b8b57ec5567b7c5590a1bf5550ad1cc87bba707dcc7e2'
            '66a9d277c91df8ef1e7e157f4f976a2b17a53f5984b72386a4d3eaf3ee525f81'
            'c28e00baa470ba9849c5fa98c646e869bf5b6b3afa183c0cd88826fc26ed9a9a'
            '7aa85f11fb929a0234b228354ae1f6135567d77bfaf424340bd1afc65fadc1b4')

apply_git_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying $_patch"
    git apply "${srcdir}/${_patch}"
  done
}

prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}

  # fixup symlinks
  git config core.symlinks true
  git reset HEAD --hard

 apply_git_with_msg 0001-Msysize.patch \
  0002-More-debugging-info.patch \
  0003-Core-update.patch \
  0004-Remove-ldconfig.patch \
  0005-Change-the-epoch-separator.patch \
  0006-makepkg-avoid-creating-.tar-files-with-extended-attr.patch \
  0007-pacman-libalpm-ignore-file-conflicts-for-foo.exe-foo.patch \
  0008-Change-default-answer-of-all-queries-to-yes.patch \
  0009-Use-pipe-instead-of-socket.patch \
  0010-pacman-make-file-list-comparisons-between-packages-c.patch \
  0011-Translate-MSYS2-specific-messages-into-Japanese.patch \
  0012-Workaround-compressing-packages-with-bsdtar-under-Ac.patch \
  0013-Fix-asciidoc-argparse.patch \
  0014-Remove-fakeroot.patch \
  0015-Remove-sudo.patch \
  0016-Use-msys-tools.patch \
  0017-Export-CC-and-CXX-variables-explicitly.patch \
  0018-pacman.c-handle-cr-on-stdin-as-well.patch \
  0019-Add-a-CI-job.patch \
  0020-Strip-static-libraries-as-well.patch \
  0021-Fix-building-staticlibs-with-LTO.patch \
  0022-fixup-Export-CC-and-CXX-variables-explicitly.patch
}

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  LDFLAGS+=" -static-libgcc" \
  meson build-${CARCH} \
    --buildtype=plain \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --default-library=static \
    -Dbuildstatic=true \
    -Ddoc=enabled \
    -Ddoxygen=enabled \
    -Dgpgme=enabled \
    -Dcurl=enabled \
    -Duse-git-version=false \
    -Dpkg-ext=.pkg.tar.xz \
    -Dscriptlet-shell=/usr/bin/bash

  meson compile -C build-${CARCH}
}

check() {
  cd ${srcdir}/${pkgname}-${pkgver}
  meson test -C build-${CARCH}
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  DESTDIR=${pkgdir} meson install -C build-${CARCH}

  # install Arch specific stuff
  install -dm755 ${pkgdir}/etc
  install -m644 ${srcdir}/pacman.conf ${pkgdir}/etc/pacman.conf

  # Fixup .pc file
  sed -i "s|/usr/.*libintl.a|-lintl|g" ${pkgdir}/usr/lib/pkgconfig/libalpm.pc

  case "${CARCH}" in
  i686)
    mycarch="i686"
    mychost="i686-pc-msys"
    myflags="-march=i686"
  ;;
  x86_64)
    mycarch="x86_64"
    mychost="x86_64-pc-msys"
    myflags="-march=x86-64"
  ;;
  esac

  install -m644 ${srcdir}/makepkg.conf ${pkgdir}/etc/
  install -m644 ${srcdir}/makepkg_mingw.conf ${pkgdir}/etc/
  install -m755 ${srcdir}/makepkg-mingw ${pkgdir}/usr/bin/

  # set things correctly in the default conf file
  sed -i ${pkgdir}/etc/makepkg.conf \
    -e "s|@CARCH[@]|${mycarch}|g" \
    -e "s|@CHOST[@]|${mychost}|g" \
    -e "s|@CARCHFLAGS[@]|${myflags}|g"
}
