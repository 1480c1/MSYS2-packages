# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Martell Malone <martellmalone@gmail.com>
# Contributor: Ray Donnelly <mingw.android@gmail.com>

pkgname=pacman
pkgver=5.2.2
pkgrel=24
pkgdesc="A library-based package manager with dependency support (MSYS2 port)"
arch=('i686' 'x86_64')
url="https://www.archlinux.org/pacman/"
license=('GPL')
groups=('base-devel')
depends=('bash>=4.2.045'
         'gettext'
         'gnupg'
         'curl'
         'pacman-mirrors'
         'msys2-keyring'
         'which'
         'bzip2'
         'xz'
         'zstd')
checkdepends=('python')
makedepends=('asciidoc'
             'doxygen'
             'git'
             'meson'
             'ninja'
             'gettext-devel'
             'heimdal-devel'
             'libarchive-devel'
             'libcurl-devel'
             'libgpgme-devel'
             'libsqlite-devel'
             'libunistring-devel')
backup=("etc/pacman.conf"
        "etc/makepkg.conf"
        "etc/makepkg_mingw.conf")
source=(pacman-${pkgver}::git+https://git.archlinux.org/pacman.git#tag=v${pkgver}
        "pacman.conf"
        "makepkg.conf"
        "makepkg_mingw.conf"
        "makepkg-mingw"
        0001-Msysize.patch
        0002-More-debugging-info.patch
        0003-Core-update.patch
        0004-Remove-ldconfig.patch
        0005-Change-the-epoch-separator.patch
        0006-makepkg-avoid-creating-.tar-files-with-extended-attr.patch
        0007-pacman-libalpm-ignore-file-conflicts-for-foo.exe-foo.patch
        0008-Change-default-answer-of-all-queries-to-yes.patch
        0009-Use-pipe-instead-of-socket.patch
        0010-pacman-make-file-list-comparisons-between-packages-c.patch
        0011-Translate-MSYS2-specific-messages-into-Japanese.patch
        0012-Workaround-compressing-packages-with-bsdtar-under-Ac.patch
        0013-build-add-libintl-dependency-to-meson-and-the-.pc-fi.patch
        0014-Fix-asciidoc-argparse.patch
        0015-Remove-fakeroot.patch
        0016-Remove-sudo.patch
        0017-Use-msys-tools.patch
        0018-pacman-correct-length-of-.files.sig-string.patch
        0019-Export-CC-and-CXX-variables-explicitly.patch)
validpgpkeys=('6645B0A8C7005E78DB1D7864F99FFE0FEAE999BD'  # Allan McRae <allan@archlinux.org>
              'B8151B117037781095514CA7BBDFFC92306B1121') # Andrew Gregory (pacman) <andrew@archlinux.org>
sha256sums=('SKIP'
            'f5a84c5642d57044ddb40387822bfcb01bdbdcd885b46bc93d116aaf0e5741a6'
            '178ff34e13f7d558a4410f909667c07faeaf7a83b15b00e7db342c0796675733'
            'b9ad2fb4b0e686b96db0a50d411f4d9344ec8950f50906fb502272037246e4b8'
            '2bd27c3fc5443b367e5025c9b9a35670b02202e48e92eead90755fef8d08fa83'
            '3b6e0dee7b5f1ff84ffffff1fb5cbc77bc6037212d92bd3fbffc91ff64aa732f'
            '084ecde8013312553671ebe53dc8fcd1c1f987bbfaa31898e0f0cd5e637dc89d'
            '635988f9138ea50f2eed81d596abc99c47d02f16db3e0c20ea65abf05869327f'
            '7a8c08ea12206793b6e40576415d19cd6ffd27f3b6339caffd4074cf9d6794c3'
            '37a7b1f97f527a14df3f791431af4777217ed52d34f1b74525f4ad4ce7d88dc2'
            '3a91fa92465a771700b69275c2376760b487c057212638e6f79e374ebf15bd78'
            'a00a4820a5a267901ee84165b4d5a4ad09dc540dbec56ddca5e1d60aba513fd4'
            '309d602867b671d54752081308a14e7fcf33b7b15c34e4194036ade2fa90f9b4'
            '3587f7891920bba891933ea8879d82fae397864f240ae3e029493fd91022d7e7'
            'acc1d0e211c0f1aa5585f6f9250552f533786ee79c8a17d95bfc4998b10677d9'
            '5fc5f39bc00ba65275dbdd2209bcb42d1251ec1c1e63f18891c909ac9c635209'
            '6585f7db153ddd3b8201ee23af77bf9f716775e7c228ee360be46ea08aa61b5e'
            '6506d8a010a65050927dcf4499fba058865ceb22b5c76af58421d4cdec7b4a17'
            'a9797a3d73d18e0188f90fb58878e9ce5bef1661dd953da12a6cfb5a34d2e279'
            'bd3acbae2c2f37d148d28cb424d1ab9491b8be4e0899b6deee4bbe8e11e14801'
            'b3744175bf561ba178608c47a4bc75016ce358197e5cc184bf786cdd209580e6'
            '2f22490f9021399c735e6dd83417206a834fb80e0eb586b66779ca53f76095d8'
            '2b7dd9f9f4c39e2ee24d54318e1f608569d78c93e856c088592db34d5c3dab5f'
            '0737f9cf11af9235c2242d1d9ec9280cf707e50290621ef4625c2512c1dae443')

apply_git_am_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying $_patch"
    git am --committer-date-is-author-date "${srcdir}/${_patch}"
  done
}

prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}

  # fixup symlinks
  git config core.symlinks true
  git reset HEAD --hard

 apply_git_am_with_msg 0001-Msysize.patch \
  0002-More-debugging-info.patch \
  0003-Core-update.patch \
  0004-Remove-ldconfig.patch \
  0005-Change-the-epoch-separator.patch \
  0006-makepkg-avoid-creating-.tar-files-with-extended-attr.patch \
  0007-pacman-libalpm-ignore-file-conflicts-for-foo.exe-foo.patch \
  0008-Change-default-answer-of-all-queries-to-yes.patch \
  0009-Use-pipe-instead-of-socket.patch \
  0010-pacman-make-file-list-comparisons-between-packages-c.patch \
  0011-Translate-MSYS2-specific-messages-into-Japanese.patch \
  0012-Workaround-compressing-packages-with-bsdtar-under-Ac.patch \
  0013-build-add-libintl-dependency-to-meson-and-the-.pc-fi.patch \
  0014-Fix-asciidoc-argparse.patch \
  0015-Remove-fakeroot.patch \
  0016-Remove-sudo.patch \
  0017-Use-msys-tools.patch \
  0018-pacman-correct-length-of-.files.sig-string.patch \
  0019-Export-CC-and-CXX-variables-explicitly.patch
}

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  LDFLAGS+=" -static-libgcc" \
  meson build-${CARCH} \
    --buildtype=plain \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --default-library=static \
    -Dbuildstatic=true \
    -Ddoc=enabled \
    -Ddoxygen=enabled \
    -Dgpgme=enabled \
    -Dcurl=enabled \
    -Duse-git-version=false \
    -Dpkg-ext=.pkg.tar.xz \
    -Dscriptlet-shell=/usr/bin/bash

  meson compile -C build-${CARCH}
}

check() {
  cd ${srcdir}/${pkgname}-${pkgver}
  meson test -C build-${CARCH}
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  DESTDIR=${pkgdir} meson install -C build-${CARCH}

  # install Arch specific stuff
  install -dm755 ${pkgdir}/etc
  install -m644 ${srcdir}/pacman.conf ${pkgdir}/etc/pacman.conf

  case "${CARCH}" in
  i686)
    mycarch="i686"
    mychost="i686-pc-msys"
    myflags="-march=i686"
  ;;
  x86_64)
    mycarch="x86_64"
    mychost="x86_64-pc-msys"
    myflags="-march=x86-64"
  ;;
  esac

  install -m644 ${srcdir}/makepkg.conf ${pkgdir}/etc/
  install -m644 ${srcdir}/makepkg_mingw.conf ${pkgdir}/etc/
  install -m755 ${srcdir}/makepkg-mingw ${pkgdir}/usr/bin/

  # set things correctly in the default conf file
  sed -i ${pkgdir}/etc/makepkg.conf \
    -e "s|@CARCH[@]|${mycarch}|g" \
    -e "s|@CHOST[@]|${mychost}|g" \
    -e "s|@CARCHFLAGS[@]|${myflags}|g"
}
