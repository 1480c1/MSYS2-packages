# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Martell Malone <martellmalone@gmail.com>
# Contributor: Ray Donnelly <mingw.android@gmail.com>

pkgname=pacman
pkgver=6.0.0
pkgrel=7
pkgdesc="A library-based package manager with dependency support (MSYS2 port)"
arch=('i686' 'x86_64')
url="https://www.archlinux.org/pacman/"
license=('GPL')
groups=('base-devel')
depends=('bash>=4.2.045'
         'gettext'
         'gnupg'
         'curl'
         'pacman-mirrors'
         'msys2-keyring'
         'which'
         'bzip2'
         'xz'
         'zstd')
checkdepends=('python')
makedepends=('asciidoc'
             'doxygen'
             'git'
             'meson'
             'ninja'
             'gettext-devel'
             'heimdal-devel'
             'libarchive-devel'
             'libcurl-devel'
             'libgpgme-devel'
             'libsqlite-devel'
             'libunistring-devel')
backup=("etc/pacman.conf"
        "etc/makepkg.conf"
        "etc/makepkg_mingw.conf")
_commit="542910d684191eb7f25ddc5d3d8fe3060028a267"
source=(pacman-${pkgver}::git+https://gitlab.archlinux.org/pacman/pacman.git#commit=${_commit}
        "pacman.conf"
        "makepkg.conf"
        "makepkg_mingw.conf"
        "makepkg-mingw"
        "fix-sig-download-name.patch"
        0001-Msysize.patch
        0002-More-debugging-info.patch
        0003-Core-update.patch
        0004-Remove-ldconfig.patch
        0005-Change-the-epoch-separator.patch
        0006-makepkg-avoid-creating-.tar-files-with-extended-attr.patch
        0007-pacman-libalpm-ignore-file-conflicts-for-foo.exe-foo.patch
        0008-Change-default-answer-of-all-queries-to-yes.patch
        0009-Use-pipe-instead-of-socket.patch
        0010-pacman-make-file-list-comparisons-between-packages-c.patch
        0011-Translate-MSYS2-specific-messages-into-Japanese.patch
        0012-Workaround-compressing-packages-with-bsdtar-under-Ac.patch
        0013-Fix-asciidoc-argparse.patch
        0014-Remove-fakeroot.patch
        0015-Remove-sudo.patch
        0016-Use-msys-tools.patch
        0017-Export-CC-and-CXX-variables-explicitly.patch
        0018-Add-a-CI-job.patch)
validpgpkeys=('6645B0A8C7005E78DB1D7864F99FFE0FEAE999BD'  # Allan McRae <allan@archlinux.org>
              'B8151B117037781095514CA7BBDFFC92306B1121') # Andrew Gregory (pacman) <andrew@archlinux.org>
sha256sums=('SKIP'
            'fff047d9514c4810eee9ab798b51d3300cbf7ee1b5e2e494e1350499d28dd448'
            'ee9f8a5ec60a1334725adb102f531f38badfe1bb66bde82c4aeb3131a2becc2f'
            '606e4b2808a40e856b7043244fc993d426dab25bc2e03b2b8ee5b869f5102507'
            '2bd27c3fc5443b367e5025c9b9a35670b02202e48e92eead90755fef8d08fa83'
            '3687b6ed122d607fc549c0ec2fc29b61e7da246fd42443680e442e8526a18ab2'
            '6dd930f5da349959deb128b0fa875da11184df90afe250a51862b39beffd1240'
            '4986792235e02c62316d974e47059bcf1223c4a2d86efa1eb56d8ef6993988ad'
            'd35854bf5a14984ddb57a36be6f4e9ac62748fa2bf10c8f844a8eb17402e8839'
            '2bbeae9c91cd42f92956967b97b777e7f9bd2e2b9c160c767fc604b183571656'
            '9cf5b82e00eb6ad3b79d4e7fc7a9cc824e5748bd0fd26a5a06478f2e9ca6f3e8'
            'c5f2430aaff70e9aaf37fe54cbc2a557004f2ef74283adc51c6b99e3c9ec4801'
            '49e746cb0ee391be3ef6d60de589f61cf25bcbfa16a49fd113a2445e5c559a9a'
            '3ac53499f19e95bdb84ab9dad8a1c551b4bb75f940b8e09ae4cd4bdea2a48999'
            '5215e5a10d6e1d7b0850683f3d27114a31143ac3112103a59a1094e5702954ca'
            '6f865181a93c499b21e7d31d8a423cecc9b8b6c205dbb8c9bb460ac5a86f7725'
            'b880a0d9e07b4a6198b3a2b318611dd53419978e60bf397ba9910c4358656de7'
            'd81b84c2a4e4a03dca169ec8b05f967bd8f5c3f3b7bc2895c0a9cbd0fa719d4b'
            '1de924b4bf41e0603fac3b3a2b83db18211ae9e65e15ac0dbaf5448c587dfb76'
            '8d9a5dd13df1f18e94abe8520dfc1f1f80ac45677e93ff64b4c3a8f5adbc9fa1'
            'e3aef7b31ff56d128ac8b213437987c57a1490f52b31185199241da43233547d'
            'bc16b167293aca73eec011d42b6d69a88e7a51be778e56e4d9a0ab1bc421efaa'
            '8388d3c5edfe66f5f023fe5df275e7223cc5d92f9163a8a85a99987f538b30f0'
            '94f1f32544311713ec4f76a1058625d4215e0a7582ca3f08208841c0bf4ed723')

apply_git_am_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying $_patch"
    git am --committer-date-is-author-date "${srcdir}/${_patch}"
  done
}

prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}

  # fixup symlinks
  git config core.symlinks true
  git reset HEAD --hard

  # https://bugs.archlinux.org/task/71274#comment200612
  patch -Np1 -i "${srcdir}/fix-sig-download-name.patch"

 apply_git_am_with_msg 0001-Msysize.patch \
  0002-More-debugging-info.patch \
  0003-Core-update.patch \
  0004-Remove-ldconfig.patch \
  0005-Change-the-epoch-separator.patch \
  0006-makepkg-avoid-creating-.tar-files-with-extended-attr.patch \
  0007-pacman-libalpm-ignore-file-conflicts-for-foo.exe-foo.patch \
  0008-Change-default-answer-of-all-queries-to-yes.patch \
  0009-Use-pipe-instead-of-socket.patch \
  0010-pacman-make-file-list-comparisons-between-packages-c.patch \
  0011-Translate-MSYS2-specific-messages-into-Japanese.patch \
  0012-Workaround-compressing-packages-with-bsdtar-under-Ac.patch \
  0013-Fix-asciidoc-argparse.patch \
  0014-Remove-fakeroot.patch \
  0015-Remove-sudo.patch \
  0016-Use-msys-tools.patch \
  0017-Export-CC-and-CXX-variables-explicitly.patch \
  0018-Add-a-CI-job.patch
}

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  LDFLAGS+=" -static-libgcc" \
  meson build-${CARCH} \
    --buildtype=plain \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --default-library=static \
    -Dbuildstatic=true \
    -Ddoc=enabled \
    -Ddoxygen=enabled \
    -Dgpgme=enabled \
    -Dcurl=enabled \
    -Duse-git-version=false \
    -Dpkg-ext=.pkg.tar.xz \
    -Dscriptlet-shell=/usr/bin/bash

  meson compile -C build-${CARCH}
}

check() {
  cd ${srcdir}/${pkgname}-${pkgver}
  meson test -C build-${CARCH}
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  DESTDIR=${pkgdir} meson install -C build-${CARCH}

  # install Arch specific stuff
  install -dm755 ${pkgdir}/etc
  install -m644 ${srcdir}/pacman.conf ${pkgdir}/etc/pacman.conf

  case "${CARCH}" in
  i686)
    mycarch="i686"
    mychost="i686-pc-msys"
    myflags="-march=i686"
  ;;
  x86_64)
    mycarch="x86_64"
    mychost="x86_64-pc-msys"
    myflags="-march=x86-64"
  ;;
  esac

  install -m644 ${srcdir}/makepkg.conf ${pkgdir}/etc/
  install -m644 ${srcdir}/makepkg_mingw.conf ${pkgdir}/etc/
  install -m755 ${srcdir}/makepkg-mingw ${pkgdir}/usr/bin/

  # set things correctly in the default conf file
  sed -i ${pkgdir}/etc/makepkg.conf \
    -e "s|@CARCH[@]|${mycarch}|g" \
    -e "s|@CHOST[@]|${mychost}|g" \
    -e "s|@CARCHFLAGS[@]|${myflags}|g"
}
