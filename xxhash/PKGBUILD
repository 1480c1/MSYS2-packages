# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgbase=xxhash
pkgname=("xxhash" "libxxhash" "libxxhash-devel")
pkgver=0.8.1
pkgrel=1
pkgdesc="Extremely fast non-cryptographic hash algorithm"
arch=('i686' 'x86_64')
url="https://github.com/Cyan4973/xxHash"
license=('BSD')
makedepends=("gcc"
             "cmake")
source=("${pkgname}-${pkgver}.tar.gz::https://codeload.github.com/Cyan4973/xxHash/tar.gz/v${pkgver}"
        "https://github.com/Cyan4973/xxHash/pull/649.patch")
sha256sums=('3bb6b7d6f30c591dd65aaaff1c8b7a5b94d81687998ca9400082c739a690436c'
            '7e3fb95799838458161afac6d44e1f11e90be1e87b6acd28b95bd8e309c81e3b')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 -i "${srcdir}/649.patch"
}

build() {
  [[ -d "${srcdir}/build-${CARCH}" ]] && rm -rf "${srcdir}/build-${CARCH}"
  mkdir -p "${srcdir}/build-${CARCH}" && cd "${srcdir}/build-${CARCH}"

  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    "../${pkgname}-${pkgver}/cmake_unofficial"

  make
  make DESTDIR="${srcdir}/dest" install
}

package_xxhash() {
  depends=("libxxhash=${pkgver}")
  cd "${srcdir}/dest"
  mkdir -p ${pkgdir}/usr/bin
  cp usr/bin/*.exe ${pkgdir}/usr/bin/
}

package_libxxhash() {
  groups=("libraries")
  cd "${srcdir}/dest"
  mkdir -p ${pkgdir}/usr/bin
  cp usr/bin/*.dll ${pkgdir}/usr/bin/
  install -Dm644 ${srcdir}/${pkgbase}-${pkgver}/LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}

package_libxxhash-devel() {
  pkgdesc="Development headers and library for xxhash"
  depends=("libxxhash=${pkgver}")
  options=('staticlibs')
  groups=("development")
  cd "${srcdir}/dest/usr"
  mkdir -p ${pkgdir}/usr
  cp -rf {include,lib,share} ${pkgdir}/usr/
}
