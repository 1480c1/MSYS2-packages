# Maintainer: Christoph Reiter <reiter.christoph@gmail.com>

_realname=dulwich
pkgname="python-${_realname}"
pkgver=0.20.45
pkgrel=1
pkgdesc='Pure-Python implementation of the Git file formats and protocols'
arch=('any')
url='https://www.dulwich.io'
license=('GPL')
depends=(
  'python'
  'python-certifi'
)
makedepends=('python-setuptools' 'python-devel' 'gcc')
source=("https://pypi.org/packages/source/${_realname::1}/${_realname}/${_realname}-${pkgver}.tar.gz")
sha256sums=('70710dd9ca2a442190c7e506892db074c318ac762e221f7529b8ce34802041b7')

prepare(){
  cd "${srcdir}/${_realname}-${pkgver}"
}

build() {
  cd "${srcdir}/${_realname}-${pkgver}"
  /usr/bin/python setup.py build
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"

  /usr/bin/python setup.py install --root="${pkgdir}" --optimize=1 --skip-build

  # We are missing runtime deps, so don't bother installing the cli tools.
  # dulwich alone is enough to get breezy working.
  rm -Rf "${pkgdir}"/usr/bin

  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
