# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Martell Malone <martellmalone@gmail.com>
# Contributor: Ray Donnelly <mingw.android@gmail.com>

pkgname=pacman-contrib
pkgver=1.3.0
pkgrel=1
pkgdesc="Contributed scripts and tools for pacman systems (MSYS2 port)"
arch=('i686' 'x86_64')
url="https://git.archlinux.org/pacman-contrib.git/about/"
license=('GPL')
depends=('perl'
         'pacman'
         'bash')
optdepends=('vim')
makedepends=('asciidoc'
             'autoconf'
             'automake-wrapper'
             'libtool'
             'libarchive-devel'
             'libcurl-devel'
             'libgpgme-devel'
             )
source=(https://git.archlinux.org/pacman-contrib.git/snapshot/${pkgname}-${pkgver}.tar.gz
        "0100-contrib.patch"
        "0101-contrib-pactree-gpgdir.patch")
sha256sums=('2b8f614651dbe272c9b518c73e53fc194405c06fd8637159a8ac9012c84e9452'
            'd7d0ea4effd6b0698959ee4d01d225cec1fe7671f0b84d83ffc9e5f036adb294'
            '4ff3f10a99e894f3ae8576227f60bff7e21a1d7a775f89d936e43501693e9618')

prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}

  patch -p1 -i ${srcdir}/0100-contrib.patch
  patch -p1 -i ${srcdir}/0101-contrib-pactree-gpgdir.patch

  ./autogen.sh
}

check() {
  cd ${srcdir}/${pkgname}-${pkgver}

  make check
}

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-git-version

  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}

  make -j1 DESTDIR=${pkgdir} install
}
