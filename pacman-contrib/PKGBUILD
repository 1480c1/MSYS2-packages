# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Martell Malone <martellmalone@gmail.com>
# Contributor: Ray Donnelly <mingw.android@gmail.com>

pkgname=pacman-contrib
pkgver=1.5.2
pkgrel=1
pkgdesc="Contributed scripts and tools for pacman systems (MSYS2 port)"
arch=('i686' 'x86_64')
url="https://git.archlinux.org/pacman-contrib.git/about/"
license=('GPL')
depends=('perl'
         'pacman'
         'bash')
optdepends=('vim')
makedepends=('asciidoc'
             'libarchive-devel'
             'libcurl-devel'
             'libgpgme-devel'
             'autotools'
             'gcc'
             )
source=(https://gitlab.archlinux.org/pacman/pacman-contrib/-/archive/v$pkgver/$pkgname-v$pkgver.tar.gz
        "0100-contrib.patch")
sha256sums=('38352c85e8dc08b04d9e0f28a613dd8c9af2bd095742959695030d03badcaddb'
            'c9ee7a9a7c6ef43749e66c64886c7bdf3ac73a86c99ca0355207577538b52875')

prepare() {
  cd ${srcdir}/${pkgname}-v${pkgver}

  patch -p1 -i ${srcdir}/0100-contrib.patch

  ./autogen.sh
}

check() {
  cd ${srcdir}/${pkgname}-v${pkgver}

  make check
}

build() {
  cd ${srcdir}/${pkgname}-v${pkgver}

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-git-version

  make
}

package() {
  cd ${srcdir}/${pkgname}-v${pkgver}

  make -j1 DESTDIR=${pkgdir} install
}
