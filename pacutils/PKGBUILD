# Maintainer: Jeremy Drake <github@jdrake.com>

pkgname=(pacutils pacutils-devel)
pkgver=0.11.0
pkgrel=1
pkgdesc="Utility library for libalpm front-ends (MSYS2 port)"
arch=('i686' 'x86_64')
url="https://github.com/andrewgregory/pacutils"
license=('MIT')
depends=('pacman')
makedepends=('libarchive-devel'
             'libcurl-devel'
             'libgpgme-devel'
             'perl')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/andrewgregory/${pkgname}/archive/refs/tags/v${pkgver}.tar.gz"
        "0001-isctype-cast.patch"
        "0002-printf-time_t.patch"
        "0003-msys.patch")
sha256sums=('7a7f3aba619c95b23afbd24f64334d2cb20e659a6c03d1d5543f3f2da66f75ae'
            'f8f2d79557ea676f48bb5dab157e8c0455e61db6497ebdd91b31cfcb66d756f9'
            '81309edafce537d6bfa7f49c9ee8c6271d80476a6050b74b6b9c1e6f8cd1a920'
            'b996829204eb9b10d00e7470d7e55988cc33c0614a7652ef8a525ede92da1476')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  patch -Np1 -i "${srcdir}/0001-isctype-cast.patch"
  patch -Np1 -i "${srcdir}/0002-printf-time_t.patch"
  patch -Np1 -i "${srcdir}/0003-msys.patch"
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make check PREFIX=/usr LOCALSTATEDIR=/var SYSCONFDIR=/etc SOEXT=dll
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make all doc PREFIX=/usr LOCALSTATEDIR=/var SYSCONFDIR=/etc SOEXT=dll
  make -j1 DESTDIR="${srcdir}/prefix-${CARCH}" install PREFIX=/usr LOCALSTATEDIR=/var SYSCONFDIR=/etc SOEXT=dll
}

package_pacutils() {
  cd "${srcdir}"

  cp -fR "${srcdir}/prefix-${CARCH}/"* "${pkgdir}"
  rm -rf "${pkgdir}/usr/"{include,lib,share/man/man3}
}

package_pacutils-devel() {
  cd "${srcdir}"

  mkdir -p "${pkgdir}/usr/share/man"
  cp -fR "${srcdir}/prefix-${CARCH}/usr/"{include,lib} "${pkgdir}/usr/"
  cp -fR "${srcdir}/prefix-${CARCH}/usr/share/man/man3" "${pkgdir}/usr/share/man/"
}
