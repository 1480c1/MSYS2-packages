# Maintainer: Christoph Reiter <reiter.christoph@gmail.com>

_realname=flit
pkgname="python-$_realname-core"
pkgver=3.7.1
pkgrel=1
pkgdesc='Simplified packaging of Python modules (core backend)'
arch=('any')
url='https://github.com/takluyver/flit'
license=('spdx:BSD-3-Clause')
depends=("python")
source=("${_realname}-$pkgver.tar.gz"::"$url/archive/$pkgver.tar.gz")
sha256sums=('59d8a4dea4c9d6678853f2643abac028f5efeb9db42e14dd846524b061cb9446')

prepare() {
  cd "${_realname}-${pkgver}"
}

build() {
  cd "${_realname}-${pkgver}/flit_core"

  PYTHONPATH=flit_core python -m flit_core.wheel
}

package() {
  cd "${_realname}-${pkgver}/flit_core"

  _pythonpath=`python -c "import sysconfig; print(sysconfig.get_path('platlib'))"`
  _site_packages="${pkgdir}${_pythonpath}"
  mkdir -p "$_site_packages"
  python bootstrap_install.py -i"${_site_packages}" dist/flit_core-*.whl

  python -m compileall -o 0 -o 1 -q "${pkgdir}/usr/lib/python"*

  install -Dm 644 ../LICENSE "$pkgdir/usr/share/licenses/${_realname}-core/LICENSE"
}
