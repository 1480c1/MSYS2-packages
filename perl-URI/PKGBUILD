# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=URI
pkgname=perl-${_realname}
pkgver=5.19
pkgrel=1
pkgdesc="Uniform Resource Identifiers (absolute and relative)"
arch=('any')
url="https://metacpan.org/dist/URI"
groups=('perl-modules')
license=('PerlArtistic')
depends=('perl>=5.10.0')
checkdepends=('perl-Test-Needs')
provides=('perl-URI-Escape=3.30')
options=('!emptydirs')
source=("https://cpan.metacpan.org/authors/id/S/SI/SIMBABQUE/${_realname}-${pkgver}.tar.gz")
sha256sums=('8fed5f819905c8a8e18f4447034322d042c3536b43c13ac1f09ba92e1a50a394')

build() {
  cd "${srcdir}/${_realname}-$pkgver"
  # install module in vendor directories.
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "${srcdir}/${_realname}-${pkgver}"
  make test
}

package() {
  cd "${srcdir}/${_realname}-$pkgver"
  make install DESTDIR="${pkgdir}"
}
