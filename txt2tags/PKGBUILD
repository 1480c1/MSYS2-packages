# borrowed from Arch without changes
# Maintainer: Norbert Pfeiler <norbert.pfeiler+aur Ã¤t gmail.com>

pkgname=txt2tags
pkgver=3.8
pkgrel=1
pkgdesc='A text formatting and conversion tool.'
arch=('any')
url='https://www.txt2tags.org/'
license=('spdx:GPL-2.0-or-later')
depends=('python')
makedepends=(
  "python-setuptools"
  "python-wheel"
  "python-build"
  "python-installer"
)
source=(https://pypi.python.org/packages/source/t/txt2tags/txt2tags-${pkgver}.tar.gz)
sha256sums=('379869e866ed85225181ac65583827781a166c907de8bb40a9f3daf7b16c3483')

build() {
  cd "${pkgname}-${pkgver}"

  python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd "${pkgname}-${pkgver}"

  python -m installer --destdir="${pkgdir}" dist/*.whl
}
