# Maintainer: Oleg Titov <oleg.titov@gmail.com>

pkgname=iperf
pkgver=2.2.0
pkgrel=1
pkgdesc='A tool to measure maximum TCP bandwidth'
arch=('i686' 'x86_64')
url="https://sourceforge.net/projects/iperf2/"
msys2_references=(
  "anitya: 1388"
  "cpe: cpe:2.3:a:iperf2_project:iperf2"
)
license=('custom')
depends=('gcc-libs')
makedepends=('autotools' 'gcc')
source=("https://downloads.sourceforge.net/iperf2/iperf-$pkgver.tar.gz"
        "0001-build-fix.patch")
sha256sums=('16810a9575e4c6dd65e4a18ab5df3cdac6730b3c832cf080a8990f132f68364a'
            '65baf3f40cd7caa8312e02a0264579d329b5f1bb11fa2ff36907ed6685f8ef9f')

prepare() {
  cd "${pkgname}-${pkgver}"

  # https://sourceforge.net/p/iperf2/tickets/328/
  # https://sourceforge.net/p/iperf2/code/ci/97c193ac9aecd7884988a16a2454346253590b66
  patch -Np1 -i "${srcdir}"/0001-build-fix.patch
}

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --build=${CHOST} \
              --prefix=/usr \
              --enable-ipv6 \
              --enable-multicast \
              --enable-threads

  make
}

check() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make check
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/iperf/LICENSE"
}
