# Maintainer: Christoph Reiter <reiter.christoph@gmail.com>

_pyname=hatchling
pkgname=python-hatchling
pkgver=1.13.0
pkgrel=1
pkgdesc="A modern project, package, and virtual env manager (backend)"
arch=('any')
url="https://github.com/pypa/hatch"
license=('spdx:MIT')
depends=(
  'python'
  'python-tomli'
  'python-packaging'
  'python-pathspec'
  'python-pluggy'
  'python-editables'
)
makedepends=(
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-wheel'
)
source=("https://pypi.org/packages/source/${_pyname::1}/${_pyname}/${_pyname}-${pkgver}.tar.gz")
sha256sums=('f8d275a2cc720735286b7c2e2bc35da05761e6d3695c2fa416550395f10c53c7')

build() {
  cd "${_pyname}-${pkgver}"

  python -m build --wheel --no-isolation
}

package() {
  cd "${_pyname}-${pkgver}"

  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README.md"
  install -Dm644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.txt"
}
