pkgname=python-tomli
pkgver=2.0.1
pkgrel=1
pkgdesc="A lil' TOML parser"
url="https://github.com/hukkin/tomli"
license=('spdx:MIT')
arch=('any')
depends=('python')
source=("https://pypi.debian.net/tomli/tomli-$pkgver-py3-none-any.whl"
        "install_wheel.py")
sha256sums=('939de3e7a6161af0c887ef91b7d41a53e7c5a1ca976325f429cb46ea9bc30ecc'
            '668e64f912d6360330c403edb6100b21f6aa6dd6a7803d342fab8c35d8176ad9')

build() {
  mkdir dist
  cp "tomli-$pkgver-py3-none-any.whl" dist
}

package() {
  _pythonpath=`python -c "import sysconfig; print(sysconfig.get_path('platlib'))"`
  _site_packages="${pkgdir}${_pythonpath}"
  mkdir -p "$_site_packages"
  python "${srcdir}/install_wheel.py" -i"${_site_packages}" dist/*.whl
  python -m compileall \
    -o 0 -o 1 -q -s"${pkgdir}" -p"/" "${_site_packages}"

  install -Dm644 tomli*.dist-info/LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
