# Maintainer: Carlos Antunes <cmantunes@gmail.com>
# Contributor: Renato Silva <br.renatosilva@gmail.com>

pkgname="lemon"
_sqlite_year=2020
_sqlite_version=3320300
pkgver=3.32.3
pkgrel=1
pkgdesc="The LEMON LALR Parser Generator used in SQLite ${pkgver} (MSYS2)"
arch=('i686' 'x86_64')
license=(PublicDomain)
groups=('base-devel')
url="https://www.sqlite.org"
depends=("gcc-libs")
source=("https://www.sqlite.org/${_sqlite_year}/sqlite-src-${_sqlite_version}.zip"
        "0010-lemon-share.patch")
sha256sums=('9312f0865d3692384d466048f746d18f88e7ffd1758b77d4f07904e03ed5f5b9'
            '9f71ba784e9ee5dbb514d2d5b15697bc707d9ffb195955208ceddf76b9e6e3eb')
options=("staticlibs")

prepare() {
  cd "${srcdir}"
  cp -v sqlite-src-${_sqlite_version}/tool/lemon.c .
  cp -v sqlite-src-${_sqlite_version}/tool/lempar.c .
  patch -Np0 -i ${srcdir}/0010-lemon-share.patch
}

build() {
  make lemon
}

package() {
  install -Dm755 lemon ${pkgdir}/usr/bin/lemon
  install -Dm644 lempar.c ${pkgdir}/usr/share/lemon/lempar.c
  mkdir -p "${pkgdir}/usr/share/licenses/${pkgname}"
  echo "This package is under public domain." > "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
