# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=urllib3
pkgname="python-${_realname}"
pkgver=1.26.13
pkgrel=2
pkgdesc="HTTP library with thread-safe connection pooling and file post support"
url='https://github.com/urllib3/urllib3'
license=('spdx:MIT')
arch=('any')
depends=("python"
         "python-certifi")
makedepends=(
  "python-setuptools"
  "python-wheel"
  "python-build"
  "python-installer"
)
source=("${_realname}-${pkgver}.tar.gz::https://github.com/urllib3/urllib3/archive/${pkgver}.tar.gz")
sha256sums=('94c8e787aeb2820ac4b17e0a750cca25ea427ec911d0999636808bb6ef2f7164')

build() {
  cd "${srcdir}/${_realname}-${pkgver}"

  python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"

  python -m installer --destdir="${pkgdir}" dist/*.whl
  install -D -m644 LICENSE.txt "${pkgdir}"/usr/share/licenses/python-${_realname}/LICENSE.txt
}
