# Maintainer: Christoph Reiter <reiter.christoph@gmail.com>

_pyname=hatch-vcs
pkgname=python-hatch-vcs
pkgver=0.3.0
pkgrel=3
pkgdesc="Hatch plugin for versioning with your preferred VCS"
url="https://github.com/ofek/hatch-vcs"
license=('spdx:MIT')
arch=('any')
depends=(
  'python-hatchling'
  'python-setuptools-scm'
)
makedepends=(
  'python-build'
  'python-installer'
)
source=("https://pypi.org/packages/source/${_pyname::1}/${_pyname}/${_pyname/-/_}-${pkgver}.tar.gz")
sha256sums=('cec5107cfce482c67f8bc96f18bbc320c9aa0d068180e14ad317bbee5a153fee')

build() {
  cd "${_pyname/-/_}-${pkgver}"

  python -m build --wheel --no-isolation
}

package() {
  cd "${_pyname/-/_}-${pkgver}"

  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE.txt -t "$pkgdir/usr/share/licenses/$pkgname/"
}

