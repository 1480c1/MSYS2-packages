# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=rsync
pkgver=3.2.2
pkgrel=1
pkgdesc="A file transfer program to keep remote files in sync"
arch=('i686' 'x86_64')
url="https://rsync.samba.org"
groups=('net-utils')
license=('GPL3')
depends=('perl' 'libiconv' 'liblz4' 'libopenssl' 'libxxhash' 'libzstd')
makedepends=('libiconv-devel' 'liblz4-devel' 'libxxhash-devel' 'libzstd-devel' 'openssl-devel')
conflicts=('rsync2')
replaces=('rsync2')
source=("https://download.samba.org/pub/rsync/${pkgname}-${pkgver}.tar.gz"
        rsync-3.2.2-msysize.patch)
sha256sums=('644bd3841779507665211fd7db8359c8a10670c57e305b4aab61b4e40037afa8'
            '86445272e3304548d1f943f9bc690ff88282d342e06ee66a0de5a6acf207b9f4')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 -i ${srcdir}/rsync-3.2.2-msysize.patch
  autoreconf -fiv
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure \
      --build=${CHOST} \
      --prefix=/usr
  
  make reconfigure
  make man all
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make test
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -Dm755 support/rrsync "${pkgdir}/usr/lib/rsync/rrsync"

  install -Dm644 ${srcdir}/${pkgname}-${pkgver}/COPYING ${pkgdir}/usr/share/licenses/${pkgname}/COPYING
}
