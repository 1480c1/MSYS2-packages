
_realname=byacc
pkgname=btyacc
pkgver=20210520
pkgrel=1
pkgdesc="btyacc - an LALR(1) parser generator with support for backtracking"
arch=('i686' 'x86_64')
url="https://invisible-island.net/byacc"
license=('Public Domain')
groups=('base-devel')

source=("${_realname}-${pkgver}.tar.gz::https://github.com/ThomasDickey/byacc-snapshots/archive/t${pkgver}.tar.gz"
        '0001-btyacc-20210520-tests-fix-handle-dynamically-determined-YYINT.patch')
sha256sums=('bd83953a3599ed3acc9ed76323f468154e724b3ab0a408218e53a441cc6d5b82'
            '5bb5a18dd52e95621c113331e4e04a22efb913bcb79f3d64ac3b8a643058580d')


prepare() {
  cd ${_realname}-snapshots-t${pkgver}
  patch -b -p1 -i '../0001-btyacc-20210520-tests-fix-handle-dynamically-determined-YYINT.patch'
  autoreconf -vfi
}

build() {
  cd ${_realname}-snapshots-t${pkgver}
  mkdir -p build-${MSYSTEM_CHOST}
  pushd build-${MSYSTEM_CHOST}
  ../configure -C \
    --enable-btyacc \
    --program-prefix=bt \
    --with-max-table-size=18000 \
    --program-transform-name='s,\^,bt,'
  make
}

check() {
  cd ${_realname}-snapshots-t${pkgver}/build-${MSYSTEM_CHOST}
  make check
}

package() {
  cd ${_realname}-snapshots-t${pkgver}
  mkdir -p "${pkgdir}/usr/share/licenses/${pkgname}"
  cp -pv package/debian/copyright "${pkgdir}/usr/share/licenses/${pkgname}"
  cd build-${MSYSTEM_CHOST}
  make DESTDIR=${pkgdir} install
}
