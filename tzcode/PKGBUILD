# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=tzcode
pkgver=2022c
pkgrel=1
pkgdesc="Sources for time zone and daylight saving time data"
arch=('i686' 'x86_64')
url="https://www.iana.org/time-zones"
license=('GPL')
options=('!emptydirs')
depends=('coreutils' 'gawk' 'sed')
makedepends=('make' 'gcc')
source=(
        https://www.iana.org/time-zones/repository/releases/tzdata${pkgver}.tar.gz{,.asc}
        https://www.iana.org/time-zones/repository/releases/${pkgname}${pkgver}.tar.gz{,.asc}
        tzcode-makefile.patch
        tzcode-makefile-exe-extensions.patch)
sha256sums=('6974f4e348bf2323274b56dff9e7500247e3159eaa4b485dfa0cd66e75c14bfe'
            'SKIP'
            '3e7ce1f3620cc0481907c7e074d69910793285bffe0ca331ef1a6d1ae3ea90cc'
            'SKIP'
            'a118e7b535bd21d494b9f3837a13939a0c826c0c6859dd8702a5256b898ad51a'
            'faf73eb5f07c56b019b540005f4262f33927a4ff2acf3752a055e8530decd749')
validpgpkeys=('7E3792A9D8ACF7D633BC1588ED97E90E62AA7E34') # Paul Eggert <eggert@cs.ucla.edu>

_timezones=('africa' 'antarctica' 'asia' 'australasia'
           'europe' 'northamerica' 'southamerica'
           'etcetera' 'backward' 'factory')

prepare() {
  cd ${srcdir}
  patch -p1 -i ${srcdir}/tzcode-makefile.patch
  patch -p1 -i ${srcdir}/tzcode-makefile-exe-extensions.patch
}

build() {
 cd ${srcdir}
 make CFLAGS="${CFLAGS} -std=gnu99"
}

package() {
  cd ${srcdir}

  make -j1 DESTDIR=${pkgdir} install
  mv ${pkgdir}/usr/share/zoneinfo-leaps ${pkgdir}/usr/share/zoneinfo/right
  mv ${pkgdir}/usr/share/zoneinfo-posix ${pkgdir}/usr/share/zoneinfo/posix

  ./zic -y ./yearistype -d ${pkgdir}/usr/share/zoneinfo ${_timezones[@]}
  ./zic -y ./yearistype -d ${pkgdir}/usr/share/zoneinfo/posix ${_timezones[@]}
  ./zic -y ./yearistype -d ${pkgdir}/usr/share/zoneinfo/right -L leapseconds ${_timezones[@]}

  ./zic -y ./yearistype -d ${pkgdir}/usr/share/zoneinfo -p America/New_York
  install -m644 -t ${pkgdir}/usr/share/zoneinfo iso3166.tab leap-seconds.list zone1970.tab zone.tab SECURITY

  install -Dm644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
