# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgbase=gpgme
pkgname=('libgpgme' 'libgpgme-devel' 'libgpgme-python')
pkgver=1.18.0
pkgrel=4
pkgdesc="A C wrapper library for GnuPG"
arch=('i686' 'x86_64')
url="https://gnupg.org/related_software/gpgme/"
license=('LGPL')
depends=('libassuan' 'libgpg-error' 'gnupg')
makedepends=('libassuan-devel' 'libgpg-error-devel' 'python-devel' 'swig' 'autotools' 'gcc')
options=('!libtool' '!emptydirs')
source=(https://gnupg.org/ftp/gcrypt/${pkgbase}/${pkgbase}-${pkgver}.tar.bz2{,.sig}
        'D545.diff::https://dev.gnupg.org/D545?download=true'
        'D546.diff::https://dev.gnupg.org/D546?download=true'
        py311.patch)
sha256sums=('361d4eae47ce925dba0ea569af40e7b52c645c4ae2e65e5621bf1b6cdd8b0e9e'
            'SKIP'
            'e2aa6413a8dbe048d95dd190d9bcdd091e70f31052711dd2ad35b6a0a44a5f74'
            '1315ffae572459bda3c6dac90019ac2221b263f7aecbbb700b4de3966c036c98'
            '5de7eff30910e27bd98fadfea5ebbfac23322a260d5fe31b7cb6ca174646b2e2')
#These might be signed by any of these keys https://gnupg.org/signature_key.html
validpgpkeys=('5B80C5754298F0CB55D8ED6ABCEF7E294B092E28'
              '6DAA6E64A76D2840571B4902528897B826403ADA'
              'AC8E115BF73E2D8D47FA9908E98E9B2D19C6C8BD')

prepare() {
  cd ${srcdir}/${pkgbase}-${pkgver}

  # https://dev.gnupg.org/D545
  patch -p1 -i "${srcdir}/D545.diff"
  # https://dev.gnupg.org/D546
  patch -p1 -i "${srcdir}/D546.diff"

  patch -p1 -i "${srcdir}/py311.patch"

  autoreconf -ivf
}

build() {
  cd ${srcdir}/${pkgbase}-${pkgver}

  local CYGWIN_CHOST="${CHOST/-msys/-cygwin}"
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --build=${CYGWIN_CHOST} \
    --disable-gpgsm-test \
    --disable-gpgconf-test \
    --disable-g13-test \
    --enable-shared \
    --enable-static \
    --enable-fixed-path="/usr/bin:/bin:/usr/sbin:/usr/local/bin"

  make no_undefined=-no-undefined
  make DESTDIR=${srcdir}/dest install
}

check() {
  cd ${srcdir}/${pkgbase}-${pkgver}
  make check || true
}

package_libgpgme() {
  groups=('libraries')

  mkdir -p ${pkgdir}/usr/{bin,share}
  cp -f ${srcdir}/dest/usr/bin/*.exe ${pkgdir}/usr/bin/
  cp -f ${srcdir}/dest/usr/bin/*.dll ${pkgdir}/usr/bin/
  #cp -rf ${srcdir}/dest/usr/share/doc ${pkgdir}/usr/share/
  cp -rf ${srcdir}/dest/usr/share/info ${pkgdir}/usr/share/
}

package_libgpgme-devel() {
  pkgdesc="libgpgme headers and libraries"
  groups=('development')
  depends=("libgpgme=${pkgver}" 'libassuan-devel' 'libgpg-error-devel')
  options=('staticlibs')

  mkdir -p ${pkgdir}/usr/{bin,lib,share}
  cp -f ${srcdir}/dest/usr/bin/*-config ${pkgdir}/usr/bin/
  cp -rf ${srcdir}/dest/usr/include ${pkgdir}/usr/
  cp -rf ${srcdir}/dest/usr/lib/cmake ${pkgdir}/usr/lib/
  cp -rf ${srcdir}/dest/usr/lib/pkgconfig ${pkgdir}/usr/lib/
  cp -f ${srcdir}/dest/usr/lib/*.a ${pkgdir}/usr/lib/
  cp -rf ${srcdir}/dest/usr/share/aclocal ${pkgdir}/usr/share/
  cp -rf ${srcdir}/dest/usr/share/common-lisp ${pkgdir}/usr/share/
}

package_libgpgme-python() {
  pkgdesc="libgpgme bindings for Python"
  groups=('development')
  depends=("libgpgme=${pkgver}" "python")
  provides=("libgpgme-python3")
  conflicts=("libgpgme-python3")
  replaces=("libgpgme-python3")
  options=('staticlibs')

  local _pyver=$(/usr/bin/python -c "import sys;sys.stdout.write('.'.join(map(str, sys.version_info[:2])))")
  mkdir -p ${pkgdir}/usr/lib
  cp -rf ${srcdir}/dest/usr/lib/python${_pyver} ${pkgdir}/usr/lib
}
