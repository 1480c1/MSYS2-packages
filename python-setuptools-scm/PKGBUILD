# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=setuptools-scm
pkgbase="python-${_realname}"
pkgname=("python-${_realname}")
pkgver=7.0.5
pkgrel=2
pkgdesc="Handles managing your python package versions in scm metadata"
arch=('any')
license=('spdx:MIT')
url='https://github.com/pypa/setuptools_scm'
depends=(
  'python'
  'python-setuptools'
  'python-tomli'
  'python-packaging'
)
makedepends=(
  "python-wheel"
  "python-build"
  "python-installer"
)
provides=("python3-${_realname}")
replaces=("python3-${_realname}")
conflicts=("python3-${_realname}")
source=("https://pypi.org/packages/source/${_realname::1}/${_realname}/${_realname/-/_}-${pkgver}.tar.gz")
sha256sums=('031e13af771d6f892b941adb6ea04545bbf91ebc5ce68c78aaf3fff6e1fb4844')

prepare() {
  cd "${srcdir}/${_realname/-/_}-${pkgver}"
}

build() {
  cd "${srcdir}/${_realname/-/_}-${pkgver}"

  python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd "${srcdir}/${_realname/-/_}-${pkgver}"

  python -m installer --destdir="${pkgdir}" dist/*.whl
  install -Dm644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
