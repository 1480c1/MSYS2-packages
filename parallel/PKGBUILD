# Maintainer: David Mott <mott.david.j@gmail.com>
# Contributor: Niklas Holm <jadedcyborg@gmail.com>

pkgname='parallel'
pkgver=20231022
pkgrel='1'
pkgdesc="A shell tool for executing jobs in parallel"
arch=('any')
url='https://www.gnu.org/software/parallel/'
license=('GPL3')
depends=('perl')
makedepends=('autotools')
source=("https://ftpmirror.gnu.org/gnu/${pkgname}/${pkgname}-${pkgver}.tar.bz2"{,".sig"}
        "0001-support-MSys2.patch")
validpgpkeys=('CDA01A4208C4F74506107E7BD1AB451688888888')
sha256sums=('93f2b94f1850798a4b5dda22bdae86dab6a6565e3525860e08e46f2563f3268c'
            'SKIP'
            'e6b4cf54ede3509dc3c6d118b93d149a5f344b94844773cbe4124e495652729a')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 -i "${srcdir}/0001-support-MSys2.patch"

  # ACLOCAL_PATH is needed to pick up local m4 macro
  ACLOCAL_PATH="${srcdir}/${pkgname}-${pkgver}" autoreconf -vif
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure \
    --build="${CHOST}" \
    --host="${CHOST}" \
    --target="${CHOST}" \
    --prefix="/usr"
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
