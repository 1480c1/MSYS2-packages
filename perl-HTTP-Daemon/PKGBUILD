# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=HTTP-Daemon
pkgname=perl-${_realname}
pkgver=6.10
pkgrel=1
pkgdesc="A simple http server class"
arch=('any')
url="https://metacpan.org/release/HTTP-Daemon"
groups=('perl-modules')
license=('PerlArtistic' 'GPL')
depends=('perl' 'perl-HTTP-Date' 'perl-HTTP-Message' 'perl-IO-Socket-IP' 'perl-LWP-MediaTypes')
makedepends=('perl-Module-Build-Tiny')
options=('!emptydirs')
source=(https://www.cpan.org/authors/id/O/OA/OALDERS/${_realname}-${pkgver}.tar.gz)
sha256sums=('30cede486a1334136d873f6a6d9f2aa3e7455b4b17d76d8e3aab325d67d13f7d')

build() {
  cd "${srcdir}/${_realname}-${pkgver}"
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "${srcdir}/${_realname}-${pkgver}"
  make test
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
