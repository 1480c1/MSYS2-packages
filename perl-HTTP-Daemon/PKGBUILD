# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=HTTP-Daemon
pkgname=perl-${_realname}
pkgver=6.14
pkgrel=1
pkgdesc="A simple http server class"
arch=('any')
url="https://metacpan.org/dist/HTTP-Daemon"
groups=('perl-modules')
license=('spdx:Artistic-1.0-Perl' 'spdx:GPL-1.0-or-later')
depends=('perl' 'perl-HTTP-Date' 'perl-HTTP-Message' 'perl-IO-Socket-IP' 'perl-LWP-MediaTypes')
makedepends=('perl-Module-Build-Tiny')
options=('!emptydirs')
source=(https://cpan.metacpan.org/authors/id/O/OA/OALDERS/${_realname}-${pkgver}.tar.gz)
sha256sums=('f0767e7f3cbb80b21313c761f07ad8ed253bce9fa2d0ba806b3fb72d309b2e1d')

build() {
  cd "${srcdir}/${_realname}-${pkgver}"
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "${srcdir}/${_realname}-${pkgver}"
  make test
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
