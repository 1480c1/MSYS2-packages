# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=('libunistring' 'libunistring-devel')
pkgver=0.9.10
pkgrel=2
pkgdesc="Library for manipulating Unicode strings and C strings."
url="https://www.gnu.org/software/libunistring/"
arch=('i686' 'x86_64')
license=('GPL')
depends=('libiconv')
makedepends=('libiconv-devel' 'autotools' 'gcc')
source=(https://ftp.gnu.org/gnu/${pkgname}/${pkgname}-${pkgver}.tar.gz
        libunistring-0.9.3-2.src.patch)
sha256sums=('a82e5b333339a88ea4608e4635479a1cfb2e01aafb925e1290b65710d43f610b'
            '8b00b7960b3e2f2e0db1f65bd2b2fea8eb36de98f48a40c8520d8fb00b57f249')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p2 -i ${srcdir}/libunistring-0.9.3-2.src.patch

  autoreconf -fi
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  export gl_cv_have_weak=no
  local CYGWIN_CHOST="${CHOST/-msys/-cygwin}"
  ./configure \
    --build=${CYGWIN_CHOST} \
    --prefix=/usr

  make
  make DESTDIR="${srcdir}/dest" install
}

package_libunistring() {
  groups=('libraries')

  mkdir -p ${pkgdir}/usr
  cp -rf ${srcdir}/dest/usr/bin ${pkgdir}/usr/
  cp -rf ${srcdir}/dest/usr/share ${pkgdir}/usr/
}

package_libunistring-devel() {
  pkgdesc="libunistring headers and libraries"
  groups=('development')
  options=('staticlibs')
  depends=("libunistring=${pkgver}" "libiconv-devel")

  mkdir -p ${pkgdir}/usr
  cp -rf ${srcdir}/dest/usr/include ${pkgdir}/usr/
  cp -rf ${srcdir}/dest/usr/lib ${pkgdir}/usr/
}
