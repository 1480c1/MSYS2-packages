# Maintainer: Alexey Pavlov <Alexpux@gmail.com>

pkgname=tio
pkgver=2.6
pkgrel=1
pkgdesc="A simple TTY terminal I/O application"
arch=('i686' 'x86_64')
url="https://tio.github.io/"
license=('spdx:GPL-2.0-or-later')
depends=('libinih')
makedepends=('gcc' 'pkgconf' 'python' 'meson' 'ninja' 'libinih-devel')
source=(https://github.com/tio/tio/releases/download/v${pkgver}/tio-${pkgver}.tar.xz{,.asc})
sha256sums=('2ce4e8810eb620a40b2a69c4e89ed42df7e48a9ee70cba04d84c5a31aaf5764c'
            'SKIP')
validpgpkeys=('101BAC1C15B216DBE07A3EEA2BDB4A0944FA00B1') # Martin Lund <martin.lund@keep-it-simple.com>

build() {
  mkdir "${srcdir}"/build && cd "${srcdir}"/build

  /usr/bin/meson \
    --wrap-mode=nodownload \
    --auto-features=enabled \
    --buildtype=plain \
    --prefix=/usr \
    "../tio-${pkgver}"

  /usr/bin/meson compile
}

package() {
  /usr/bin/meson install -C build --destdir "${pkgdir}"
  install -Dm644 tio-${pkgver}/LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
