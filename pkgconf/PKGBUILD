# Maintainer: Bartlomiej Piotrowski <nospam@bpiotrowski.pl>

pkgname=pkgconf
pkgver=2.0.2
pkgrel=2
pkgdesc='pkg-config compatible utility which does not depend on glib'
url='https://github.com/pkgconf/pkgconf'
arch=('i686' 'x86_64')
license=('spdx:ISC')
makedepends=('meson'
             'ninja'
             'gcc')
conflicts=('pkg-config')
provides=('pkg-config')
replaces=('pkg-config')
source=(https://github.com/pkgconf/pkgconf/archive/refs/tags/$pkgname-$pkgver.tar.gz
        384ade5f316367f15da9dbf09853e06fe29bf2bf.patch)
sha256sums=('663e74fe84d18c7f010705a618690064296833598d647ecd96b63d0d78155d6c'
            '537b58772815bc04a4d2f51648f4e008e5172c9513ae2c684416eba744df725c')

prepare() {
  mv "${pkgname}-${pkgname}-${pkgver}" "${pkgname}-${pkgver}"
  cd "${srcdir}/${pkgname}-${pkgver}"

  # https://github.com/pkgconf/pkgconf/issues/326
  patch -R -p1 -i ${srcdir}/384ade5f316367f15da9dbf09853e06fe29bf2bf.patch
}

build() {
  mkdir "${srcdir}"/build && cd "${srcdir}"/build

  meson \
    --buildtype=plain \
     --prefix=/usr \
     -Dtests=disabled \
    "../${pkgname}-${pkgver}"

  meson compile
}

package() {
  cd "${srcdir}"/build

  DESTDIR="$pkgdir" meson install

  cp "$pkgdir"/usr/bin/pkgconf "$pkgdir/usr/bin/${MSYSTEM_CHOST}-pkg-config"
  cp "$pkgdir"/usr/bin/pkgconf "$pkgdir"/usr/bin/pkg-config
}
