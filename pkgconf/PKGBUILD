# Maintainer: Bartlomiej Piotrowski <nospam@bpiotrowski.pl>

pkgname=pkgconf
pkgver=1.7.1
pkgrel=1
pkgdesc='pkg-config compatible utility which does not depend on glib'
url='https://github.com/pkgconf/pkgconf'
arch=('i686' 'x86_64')
license=('ISC')
makedepends=('meson'
             'ninja')
conflicts=('pkg-config')
provides=('pkg-config')
source=(https://distfiles.dereferenced.org/pkgconf/$pkgname-$pkgver.tar.xz
        001-no-cygpath-conv.patch)
sha256sums=('a1a3e2ddca34551600b51a1ef2c1b77198065f34f4d2eeb35fe668187c86c349'
            '3bbff179662d718c703c11b0ec34c118c73dc39fab05a90e7a55fccf15c2eb93')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  patch -p1 -i ${srcdir}/001-no-cygpath-conv.patch
}

build() {
  mkdir "${srcdir}"/build && cd "${srcdir}"/build

  meson \
    --buildtype=plain \
     --prefix=/usr \
     -Dtests=false \
    "../${pkgname}-${pkgver}"

  meson compile
}

package() {
  cd "${srcdir}"/build

  DESTDIR="$pkgdir" meson install

  cp "$pkgdir"/usr/bin/pkgconf "$pkgdir/usr/bin/$(gcc -dumpmachine)-pkg-config"
  cp "$pkgdir"/usr/bin/pkgconf "$pkgdir"/usr/bin/pkg-config
}
