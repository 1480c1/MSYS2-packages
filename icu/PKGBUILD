# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgbase=icu
pkgname=('icu' 'icu-devel')
pkgver=72.1
pkgrel=1
pkgdesc="International Components for Unicode library"
arch=('i686' 'x86_64')
url="https://icu.unicode.org"
license=('spdx:ICU')
depends=('gcc-libs' 'sh')
makedepends=('autoconf-archive' 'autotools' 'gcc')
source=("https://github.com/unicode-org/${pkgname}/releases/download/release-${pkgver//./-}/${pkgname}4c-${pkgver//./_}-src.tgz"{,.asc}
        icu-59.1-msys2.patch)
sha512sums=('848c341b37c0ff077e34a95d92c6200d5aaddd0ee5e06134101a74e04deb08256a5e817c8aefab020986abe810b7827dd7b2169a60dacd250c298870518dcae8'
            'SKIP'
            '6f2f7f4d18a709a5d4e5c53eabe65a74c8e59dfd525ebdd90d64560f9a88b2e18c36ccc8764097308e19f46e64e83c6d60dc91e18f18e5f0c47cd1b785e60aaa')
validpgpkeys=('0E51E7F06EF719FBD072782A5F56E5AFA63CCD33'  # Craig Cornelius (For use with ICU releases) <ccornelius@google.com>
              '3DA35301A7C330257B8755754058F67406EAA6AB') # Craig Cornelius <ccornelius@google.com>

prepare() {
  cd ${srcdir}/icu/source

  patch -p2 -i ${srcdir}/icu-59.1-msys2.patch

  libtoolize --force --copy
  autoreconf -fi
}

build() {
  cd ${srcdir}/icu/source

  local CYGWIN_CHOST="${CHOST/-msys/-cygwin}"

  CC=/usr/bin/gcc \
  CXX=/usr/bin/g++ \
  ./configure \
    --prefix=/usr \
    --build=${CYGWIN_CHOST} \
    --sysconfdir=/etc \
    --mandir=/usr/share/man \
    --sbindir=/usr/bin
  make

  make -j1 DESTDIR=${srcdir}/dest install

  # remove extra implib
  rm ${srcdir}/dest/usr/bin/libicudata${pkgver%%.*}.dll.a
  # remove unversioned DLL
  rm ${srcdir}/dest/usr/bin/msys-icudata.dll

  pushd ${srcdir}/dest/usr/lib
    # create unversioned implib symlinks
    for l in *.dll.a; do
      cp ${l} ${l//${pkgver%%.*}}
    done
  popd

  sed -e 's/l${LIBICU}/l${ICUPREFIX}/g' \
    -e '/l${ICUPREFIX}/ s/${ICULIBSUFFIX_VERSION}//g' \
    -i ${srcdir}/dest/usr/bin/icu-config \
    || error "sed icu-config failed"

  # Install license
  install -Dm644 ${srcdir}/icu/license.html ${srcdir}/dest/usr/share/licenses/icu/license.html
}

check() {
  cd icu/source
  make -k check # passes all
}

package_icu() {
  depends=('gcc-libs')
  groups=('libraries')
  mkdir -p $pkgdir/usr/bin
  cp -f $srcdir/dest/usr/bin/*.dll $pkgdir/usr/bin/
}

package_icu-devel() {
  pkgdesc="ICU headers and libraries"
  groups=('development')
  depends=("icu=${pkgver}")

  cp -rf $srcdir/dest/usr $pkgdir/
  rm -f $pkgdir/usr/bin/*.dll
}
