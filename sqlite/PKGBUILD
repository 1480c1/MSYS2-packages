# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Renato Silva <br.renatosilva@gmail.com>

pkgbase="sqlite"
pkgname=('sqlite' 'libsqlite' 'libsqlite-devel' 'sqlite-doc'
        'tcl-sqlite'
         'sqlite-extensions'
         )
_amalgamationver=3320300
pkgver=3.32.3
pkgrel=1
pkgdesc="A C library that implements an SQL database engine"
arch=('i686' 'x86_64')
license=(PublicDomain)
url="https://www.sqlite.org/"
depends=('libreadline' 'icu' 'zlib' 'tcl')
makedepends=('libreadline-devel' 'icu-devel' 'zlib-devel' 'tcl')
source=(https://www.sqlite.org/2020/sqlite-src-${_amalgamationver}.zip
        https://www.sqlite.org/2020/sqlite-doc-${_amalgamationver}.zip
        LICENSE
        0001-sqlite3.32.3-configure.ac-fix-OS-recognition-on-MSYS2.patch
        0002-sqlite3.32.3-Makefile.in-fix-rule-compiling-rbu.exe.patch
        0007-sqlite3.32.3-Makefile.in-fix-libtclsqlite-package-installation-bug.patch
        Makefile.ext.in README.md.in)
options=('!libtool' '!emptydirs' '!debug' 'strip')
sha256sums=('9312f0865d3692384d466048f746d18f88e7ffd1758b77d4f07904e03ed5f5b9'
            '36920536daf7f8b19c2e646dc79db62e13cc1a992f562ba9a11fa7c191f24a4e'
            '0b76663a90e034f3d7f2af5bfada4cedec5ebc275361899eccc5c18e6f01ff1f'
            '589b7182343dba3dd8225c13ff1d7d275e5f714e7793bac1c88f7cfdd569d9e0'
            'b717c73804be44b82048d90bbb8e0c61ff081339a5757194e9769bd4d9c048e5'
            '12507108d633a6b0a01534e30bcc807a4dcd5c8f955e6336b9ebe505a986fce0'
            '572fe2e1492de5643018bcbb2ba7d69747d20a2b021c32cec868b87c92ccb18f'
            'ef3a1faa02a88e05539d160d51679f164dfa23cc5a637ff36bc504bf04d6d7a6')

_sqlite_tools="sqlite3.exe sqldiff.exe rbu.exe"

# Helper macros to help make tasks easier #
apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying $_patch"
    patch -Nbp1 -i "${srcdir}/${_patch}"
  done
}

apply_patch_with_msg_p2() {
  for _patch in "$@"
  do
    msg2 "Applying $_patch"
    patch -Nbp2 -i "${srcdir}/${_patch}"
  done
}



prepare() {

  msg2 "Preparing sqlite package..."
  cd "${srcdir}"/sqlite-src-${_amalgamationver}
  apply_patch_with_msg 0001-sqlite3.32.3-configure.ac-fix-OS-recognition-on-MSYS2.patch
  apply_patch_with_msg 0002-sqlite3.32.3-Makefile.in-fix-rule-compiling-rbu.exe.patch
  apply_patch_with_msg 0007-sqlite3.32.3-Makefile.in-fix-libtclsqlite-package-installation-bug.patch
  autoreconf -vfi

}

build() {
  msg2 "Building sqlite package..."
  export CPPFLAGS="$CPPFLAGS \
    -DHAVE_MALLOC_H \
    -DHAVE_MALLOC_USABLE_SIZE \
    -DSQLITE_ENABLE_DBSTAT_VTAB=1 \
    -DSQLITE_ENABLE_DBPAGE_VTAB=1 \
    -DSQLITE_ENABLE_STAT4=1 \
    -DSQLITE_ENABLE_FTS5=1 \
    -DSQLITE_ENABLE_UNLOCK_NOTIFY=1 \
    -DSQLITE_ENABLE_UPDATE_DELETE_LIMIT=1 \
    -DSQLITE_OMIT_LOOKASIDE \
    -DSQLITE_OS_UNIX=1 \
    -DSQLITE_OS_WIN=1 \
    -DSQLITE_SECURE_DELETE=1 \
    -DSQLITE_ENABLE_SESSION=1 \
    -DSQLITE_ENABLE_PREUPDATE_HOOK=1 \
    -DSQLITE_SOUNDEX=1 \
    -DSQLITE_TEMP_STORE=1 \
    -DSQLITE_USE_FCNTL_TRACE=1 \
    -DUSE_SYSTEM_SQLITE=1 \
    -DSQLITE_ENABLE_FTS3_PARENTHESIS=1"

  mkdir -p build-${_amalgamationver}-${MSYSTEM_CHOST}
  cd build-${_amalgamationver}-${MSYSTEM_CHOST}
  ../sqlite-src-${_amalgamationver}/configure -C \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --build=${CHOST} \
    --enable-shared \
    --enable-static \
    --enable-readline --disable-editline \
    --enable-fts5 --enable-json1

  make -j all ${_sqlite_tools}
  make check

  msg2 "Building sqlite extensions..."
  ./config.status --file=ext/misc/Makefile:../Makefile.ext.in 2> /dev/null
  make -j -C ext/misc
}

_install_license() {
  install -D -m644 ${srcdir}/LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}

package_sqlite() {
  pkgdesc="A C library that implements an SQL database engine"
  depends=('libreadline' 'libsqlite')
  depends+=('icu')
  provides=("sqlite3=${pkgver}")
  replaces=("sqlite3")
  replaces+=('sqlite-icu' 'sqlite-rbu')
  optdepends=('sqlite-extensions: various extensions for SQLite')

  mkdir -p ${pkgdir}/usr/bin
  cd build-${_amalgamationver}-${MSYSTEM_CHOST}/.libs
  cp -v ${_sqlite_tools}  ${pkgdir}/usr/bin/

  # documentation from TEA directory
  mkdir -p ${pkgdir}/usr/share/man/man1/
  cd ${srcdir}/sqlite-src-${_amalgamationver}
  cp autoconf/tea/doc/*  ${pkgdir}/usr/share/man/man1/

  _install_license
}

package_libsqlite() {
  pkgdesc="Sqlite3 library"
  groups=('libraries')
  options+=(!staticlibs)

  cd build-${_amalgamationver}-${MSYSTEM_CHOST}
  make lib_install DESTDIR=${pkgdir}
  rm -fr ${pkgdir}/usr/lib

  _install_license
}

package_libsqlite-devel() {
  pkgdesc="Sqlite3 headers and libraries"
  groups=('development')
  depends=("libsqlite=${pkgver}")
  optdepends=('tcl-sqlite: Tcl bindings for SQLite')
  options+=('staticlibs')

  cd build-${_amalgamationver}-${MSYSTEM_CHOST}
  make install DESTDIR=${pkgdir}
  rm -fr ${pkgdir}/usr/bin
  rm -fr ${pkgdir}/usr/lib/tcl*

  _install_license
}

package_sqlite-doc() {
  pkgdesc="Most of the static HTML files that comprise Sqlite ${pkgver} website, including all of the SQL Syntax and the C/C++ interface specs and other miscellaneous documentation"
  provides=("sqlite3-doc=${pkgver}")
  depends=("sqlite=${pkgver}")

  cd ${srcdir}/sqlite-doc-${_amalgamationver}
  mkdir -p ${pkgdir}/usr/share/doc/${pkgbase}
  cp -R *  ${pkgdir}/usr/share/doc/${pkgbase}/

  _install_license
}


package_sqlite-extensions() {
  pkgdesc="Various extensions for the SQL database engine ${pkgver}"
  depends=("sqlite=${pkgver}" "libsqlite=${pkgver}" 'zlib')
  # sqlite 3.32.3: The replaced packages are merged into 'sqlite-extensions'.
  replaces=('sqlite-compress' 'sqlite-vfslog')

  cd build-${_amalgamationver}-${MSYSTEM_CHOST}
  make -C ext/misc install DESTDIR=${pkgdir}

  mkdir -p "${pkgdir}/usr/share/sqlite/extensions/"
  cat "${srcdir}/README.md.in" |         \
    sed -e "s|@VERSION@|${pkgver}|g;"    \
    > "${pkgdir}/usr/share/sqlite/extensions/README.md"

  _install_license
}

package_tcl-sqlite() {
  pkgdesc="Tcl bindings for SQLite"
  depends=("libsqlite=${pkgver}" 'tcl')

  cd build-${_amalgamationver}-${MSYSTEM_CHOST}
  make tcl_install DESTDIR=${pkgdir}
  rm -fr ${pkgdir}/usr/bin
  find ${pkgdir}/usr/lib -type f -name '*.a' -delete

   _install_license
}

