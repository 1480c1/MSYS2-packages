# Maintainer: Alexey Pavlov <Alexpux@gmail.com>

pkgname=('libiconv' 'libiconv-devel')
pkgver=1.16
pkgrel=2
pkgdesc='Libiconv is a conversion library'
groups=('libraries')
arch=('i686' 'x86_64')
makedepends=('autotools' 'gcc' 'gettext-devel')
url='https://www.gnu.org/software/libiconv/'
license=('LGPL')
source=("https://ftp.gnu.org/pub/gnu/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        1.16-aliases.patch
        1.16-cross-install.patch
        1.16-wchar.patch
        libiconv-1.16-msysize.patch)
sha256sums=('e6a1b1b589654277ee790cce3734f07876ac4ccfaecbee8afa0b649cf529cc04'
            'af92deee987d6258729a3b938ef841e63312de860c87f8a4821f272082b42e6a'
            '8d4622bd319e0820fd52ae2cdd4a017c13a58b168cd571a646eb8276c5c48d33'
            '4e34425c3de2a2c976c703438521314101409e3af8f410e02679714737a8b76f'
            '8a43391e9402e99f70ef3f5dfbb8dda7ab7a30d91c00189ce5ab38c1bc6352d8')
options=(!libtool)

apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying $_patch"
    patch -Nbp1 -i "${srcdir}/$_patch"
  done
}

prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}

  apply_patch_with_msg \
    1.16-aliases.patch \
    1.16-cross-install.patch \
    1.16-wchar.patch \
    libiconv-1.16-msysize.patch

  cp -f srcm4/* m4/

  (cd libcharset && autoreconf -fiv)
  autoreconf -fiv
}

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  local CYGWIN_CHOST="${CHOST/-msys/-cygwin}"
  ./configure \
    --prefix=/usr \
    --build=${CYGWIN_CHOST} \
    --without-libintl-prefix \
    --enable-static \
    --enable-shared \
    --enable-extra-encodings

  make
  make DESTDIR=${srcdir}/dest install
}

check() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make check
}

package_libiconv() {
  depends=('gcc-libs' 'libintl')

  mkdir -p ${pkgdir}/usr
  cp -rf ${srcdir}/dest/usr/bin ${pkgdir}/usr/
  cp -rf ${srcdir}/dest/usr/share ${pkgdir}/usr/
}

package_libiconv-devel() {
  groups=('development')
  options=('staticlibs')
  depends=("libiconv=${pkgver}")
  pkgdesc="libiconv headers and libraries"

  mkdir -p ${pkgdir}/usr
  cp -rf $srcdir/dest/usr/include ${pkgdir}/usr/
  cp -rf $srcdir/dest/usr/lib ${pkgdir}/usr/
  rm -f ${pkgdir}/usr/lib/charset.alias
}
