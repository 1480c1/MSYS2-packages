# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname="msys2-w32api-runtime"
pkgver=9.0.0.6357.eac8c38c1
pkgrel=1
pkgdesc="Win32 API import libs for MSYS2 toolchain"
arch=('i686' 'x86_64')
url="https://mingw-w64.sourceforge.io/"
license=('custom')
depends=('msys2-w32api-headers')
options=('staticlibs' '!strip' '!buildflags')
makedepends=('git' 'mingw-w64-cross-gcc' 'autotools' 'gcc')
_commit='eac8c38c10ca4e22feb2f1e5e530b1e2daedc20f'
source=('mingw-w64'::"git+https://git.code.sf.net/p/mingw-w64/mingw-w64#commit=$_commit")
sha256sums=('SKIP')

MINGW_CHOST=${CARCH}-w64-mingw32

pkgver() {
  cd "${srcdir}/mingw-w64"

  printf "9.0.0.%s.%s" "$(git rev-list --count $_commit)" "$(git rev-parse --short $_commit)"
}

prepare() {
  cd ${srcdir}/mingw-w64
}

build() {
  cd ${srcdir}/mingw-w64/mingw-w64-crt

  local _libs=
  case ${CARCH} in
    i686)
      _libs="--enable-lib32 --disable-lib64"
    ;;
    x86_64)
      _libs="--enable-lib64 --disable-lib32"
    ;;
  esac

  ./configure \
  --prefix=/crt \
  --build=${CHOST} \
  --host=${MINGW_CHOST} \
  --enable-w32api \
  ${_libs} \
  CFLAGS='-I/usr/include/w32api' \
  CXXFLAGS='-I/usr/include/w32api' \
  CPPFLAGS='-I/usr/include/w32api'

  make
}

package() {
  cd ${srcdir}/mingw-w64/mingw-w64-crt

  make DESTDIR=${pkgdir} install
  mv ${pkgdir}/crt ${pkgdir}/usr
}
