# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname="msys2-w32api-runtime"
pkgver=11.0.1.r750.g05598db99
pkgrel=1
pkgdesc="Win32 API import libs for MSYS2 toolchain"
arch=('i686' 'x86_64')
url="https://mingw-w64.sourceforge.io/"
license=('custom')
depends=('msys2-w32api-headers')
options=('staticlibs' '!strip' '!buildflags')
makedepends=('git' 'mingw-w64-cross-gcc' 'autotools')
_commit='05598db991767e79b3d701ca0979df050d82eb11'
source=('mingw-w64'::"git+https://git.code.sf.net/p/mingw-w64/mingw-w64#commit=$_commit")
sha256sums=('b2ce4b2ddad1291ffad9ba06f174dd4f17d192a26c5da45bc5932b3304d6e908')
msys2_references=(
  'cygwin: w32api-runtime'
  'cpe: cpe:/a:mingw-w64:mingw-w64'
)

pkgver() {
  cd "${srcdir}/mingw-w64"
  git describe --long ${_commit} | sed 's/\([^-]*-g\)/r\1/;s/-/./g;s/^v//g' | sed 's/11\.0\.0/11.0.1/g'
}

prepare() {
  cd ${srcdir}/mingw-w64
}

build() {
  cd ${srcdir}/mingw-w64/mingw-w64-crt

  local _libs=
  case ${CARCH} in
    i686)
      _libs="--enable-lib32 --disable-lib64"
    ;;
    x86_64)
      _libs="--enable-lib64 --disable-lib32"
    ;;
  esac

  unset CC CXX

  ./configure \
  --prefix=/crt \
  --build=${CHOST} \
  --host=${CARCH}-w64-mingw32 \
  --enable-w32api \
  --disable-dependency-tracking \
  ${_libs} \
  CFLAGS='-I/usr/include/w32api' \
  CXXFLAGS='-I/usr/include/w32api' \
  CPPFLAGS='-I/usr/include/w32api'

  make
}

package() {
  cd ${srcdir}/mingw-w64/mingw-w64-crt

  make DESTDIR=${pkgdir} install
  mv ${pkgdir}/crt ${pkgdir}/usr
}
