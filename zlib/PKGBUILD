# Maintainer: Alexey Pavlov <Alexpux@gmail.com>

pkgname=('zlib' 'zlib-devel')
pkgver=1.2.12
pkgrel=1
pkgdesc='Compression library implementing the deflate compression method found in gzip and PKZIP'
arch=('i686' 'x86_64')
groups=('libraries')
license=('custom')
url="https://www.zlib.net/"
makedepends=('gcc' 'make' 'autotools')
source=("https://zlib.net/current/zlib-${pkgver}.tar.gz"
        #"https://sourceforge.net/projects/libpng/files/zlib/${pkgver}/zlib-${pkgver}.tar.gz"
        1.2.7-minizip-cygwin.patch
        1.2.7-zlib-symbols.patch
        zlib-1.2.11-msys2.patch
        "https://patch-diff.githubusercontent.com/raw/madler/zlib/pull/607.patch")
sha256sums=('91844808532e5ce316b3c010929493c0244f3d37593afd6de04f71821d5136d9'
            '58daf0d9041ed6687fa17cfa244562a0f8db1ef9924d57b8a427adf40a8ecfb0'
            '16e9ce18df14a700dda0572843cdb42b5cb3d398c64706af0acc7cd77c181b7e'
            '65c946a22df5f7beab8fbf556e69c66269c8c0e251f370378d566afe10defe5d'
            'f35eb05334a4f8d7b40b6c5610a6369f654863b5fa1a19c2507888f918025238')

prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}
  grep -A 24 '^  Copyright' zlib.h > LICENSE

  patch -p2 -i ${srcdir}/1.2.7-minizip-cygwin.patch
  patch -p2 -i ${srcdir}/1.2.7-zlib-symbols.patch
  patch -p1 -i ${srcdir}/zlib-1.2.11-msys2.patch
  patch -p1 -i ${srcdir}/607.patch
}

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --prefix=/usr
  make
  make install DESTDIR="${srcdir}/dest"
}

check() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make test
}

package_zlib() {
  depends=('gcc-libs')

  mkdir -p ${pkgdir}/usr
  cp -rf $srcdir/dest/usr/bin ${pkgdir}/usr/
  cp -rf $srcdir/dest/usr/share ${pkgdir}/usr/
  install -D -m644 ${srcdir}/${pkgname}-${pkgver}/LICENSE ${pkgdir}/usr/share/licenses/zlib/LICENSE
}

package_zlib-devel() {
  pkgdesc="zlib headers and libraries"
  groups=('development')
  options=('staticlibs')  # needed by binutils testsuite
  depends=("zlib=${pkgver}")

  mkdir -p ${pkgdir}/usr
  cp -rf $srcdir/dest/usr/include ${pkgdir}/usr/
  cp -rf $srcdir/dest/usr/lib ${pkgdir}/usr/
  sed -e "s| -L\${sharedlibdir}||g" -i ${pkgdir}/usr/lib/pkgconfig/zlib.pc
}
