# Maintainer: Christoph Reiter <reiter.christoph@gmail.com>

_realname=certifi
pkgname="python-${_realname}"
pkgver=2022.9.24
pkgrel=1
pkgdesc="Python package for providing Mozilla's CA Bundle"
url='https://pypi.python.org/pypi/certifi'
license=('spdx:MPL-2.0')
arch=('any')
depends=("python")
makedepends=("python-setuptools")
source=("https://pypi.io/packages/source/${_realname:0:1}/${_realname}/${_realname}-${pkgver}.tar.gz")
sha256sums=('0d9c601124e5a6ba9712dbc60d9c53c21e34f5f641fe83002317394311bdce14')

build() {
  cd "${srcdir}/${_realname}-${pkgver}"

  python setup.py build
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"

  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -D -m644 LICENSE "${pkgdir}${MINGW_PREFIX}"/share/licenses/python-${_realname}/LICENSE
}
