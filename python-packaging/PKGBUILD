# Maintainer: atom2013 <atom.long@hotmail.com>

_realname=packaging
pkgbase="python-${_realname}"
pkgname=("python-${_realname}")
pkgver=24.0
pkgrel=1
pkgdesc="Core utilities for Python packages"
arch=('any')
url="https://github.com/pypa/packaging"
license=('spdx:BSD-2-Clause OR Apache-2.0')
depends=('python')
makedepends=('python-installer' 'python-flit-core' )
provides=("python3-${_realname}")
replaces=("python3-${_realname}")
conflicts=("python3-${_realname}")
source=("https://pypi.io/packages/source/p/packaging/packaging-${pkgver}.tar.gz")
sha256sums=('eb82c5e3e56209074766e6885bb04b8c38a0c015d0a30036ebe7ece34c9989e9')

build() {
  cd "${srcdir}"/packaging-${pkgver}
  /usr/bin/python -m flit_core.wheel
}

package() {
  cd "${srcdir}"/packaging-${pkgver}
  /usr/bin/python -m installer --destdir="${pkgdir}" dist/*.whl
}

