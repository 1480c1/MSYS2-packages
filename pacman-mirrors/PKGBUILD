# Maintainer: Ray Donnelly <mingwandroid@gmail.com>

pkgname=pacman-mirrors
pkgver=20221014
pkgrel=1
pkgdesc="MSYS2 mirror list for use by pacman"
arch=('any')
url="https://www.msys2.org/dev/mirrors/"
license=('GPL')
source=(mirrorlist.msys
        mirrorlist.mingw)
sha256sums=('248d905f479b6eeed41fec84ed0b841f234bb04e81e3f795644b587001b0fc7e'
            '4153558b2cd3dd00ee1c112f00cb3ca7aa75e8a347b9c92e2a227c6617e7ba15')
backup=(
  'etc/pacman.d/mirrorlist.msys'
  'etc/pacman.d/mirrorlist.mingw'
  'etc/pacman.d/mirrorlist.mingw32'
  'etc/pacman.d/mirrorlist.mingw64'
  'etc/pacman.d/mirrorlist.ucrt64'
  'etc/pacman.d/mirrorlist.clang64'
  'etc/pacman.d/mirrorlist.clang32'
)
package() {
  mkdir -p ${pkgdir}/etc/pacman.d
  install -m644 ${srcdir}/mirrorlist.msys ${pkgdir}/etc/pacman.d/
  install -m644 ${srcdir}/mirrorlist.mingw ${pkgdir}/etc/pacman.d/

  # For backwards compatibility
  install -m644 ${srcdir}/mirrorlist.mingw ${pkgdir}/etc/pacman.d/mirrorlist.mingw32
  sed -s 's|$repo|i686|g' -i ${pkgdir}/etc/pacman.d/mirrorlist.mingw32
  install -m644 ${srcdir}/mirrorlist.mingw ${pkgdir}/etc/pacman.d/mirrorlist.mingw64
  sed -s 's|$repo|x86_64|g' -i ${pkgdir}/etc/pacman.d/mirrorlist.mingw64
  install -m644 ${srcdir}/mirrorlist.mingw ${pkgdir}/etc/pacman.d/mirrorlist.ucrt64
  sed -s 's|$repo|ucrt64|g' -i ${pkgdir}/etc/pacman.d/mirrorlist.ucrt64
  install -m644 ${srcdir}/mirrorlist.mingw ${pkgdir}/etc/pacman.d/mirrorlist.clang64
  sed -s 's|$repo|clang64|g' -i ${pkgdir}/etc/pacman.d/mirrorlist.clang64
  install -m644 ${srcdir}/mirrorlist.mingw ${pkgdir}/etc/pacman.d/mirrorlist.clang32
  sed -s 's|$repo|clang32|g' -i ${pkgdir}/etc/pacman.d/mirrorlist.clang32
}
