# Maintainer: Jo√£o Guerra <joca.bt@gmail.com>

pkgname=global
pkgver=6.6.9
pkgrel="1"
pkgdesc="A source code tagging system"
arch=("i686" "x86_64")
url="https://www.gnu.org/software/global/"
license=('GPL3')
depends=("libltdl")
optdepends=("ctags" "python3-pygments")
makedepends=("ncurses-devel" 'autotools' 'gperf' 'gcc')
options=(libtool !emptydirs)
source=("https://ftp.gnu.org/gnu/global/${pkgname}-${pkgver}.tar.gz"{,.sig})
sha256sums=('aacba0fa8d60ca645e62f312dcd23b47ed48a081aa0fb1563cff4702d9d1fad5'
            'SKIP')
validpgpkeys=("26F631B43D624A927E6F1C33969C3BE389DDA6EB") # Shigio YAMAGUCHI <shigio@gnu.org>

prepare() {
  cd "${pkgname}-${pkgver}"
  autoreconf -fiv
}

build () {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --build=${CHOST} \
              --host=${CHOST} \
              --enable-shared \
              --disable-static \
              --with-exuberant-ctags=ctags
  make
}

package () {
  cd "${pkgname}-${pkgver}"
  make prefix="${pkgdir}/usr" install
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
