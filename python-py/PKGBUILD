# $Id: PKGBUILD 135906 2015-06-26 14:54:21Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Sebastian Wiesner <lunaryorn googlemail com>
# Contributor: Igor Ramos Tiburcio <irtigor@yahoo.com.br>
# Contributor: Wieland Hoffmann <themineo+aur@gmail.com>
# Contributor: Hervé Cauwelier <herve ¤ oursours.net>
# Contributor: Felix Kaiser <felix.kaiser@fxkr.net>

pkgbase=python-py
pkgname=('python-py')
pkgver=1.8.2
pkgrel=1
pkgdesc="library with cross-python path, ini-parsing, io, code, log facilities"
arch=('any')
license=('MIT')
url="https://pylib.readthedocs.io/"
depends=('python')
makedepends=('python-setuptools'
             'python-setuptools-scm')
provides=("python3-py")
replaces=("python3-py")
conflicts=("python3-py")
source=("${pkgbase}-${pkgver}.tar.gz::https://github.com/pytest-dev/py/archive/${pkgver}.tar.gz")
sha512sums=('d11ba9e16a0824b79e49a2e0c089f44ba6206aad70f380813fbf09fcb6e7c89b01ed05352dc5a23faebc6b73da2e24bb819de19a3b52b63a979ae126b5ef2ebd')

prepare() {
  export LC_CTYPE=en_US.UTF-8
  export SETUPTOOLS_SCM_PRETEND_VERSION=${pkgver}
}

build() {
  cd "${srcdir}/py-${pkgver}"
  /usr/bin/python setup.py build
}

package() {
  cd "${srcdir}/py-${pkgver}"
  /usr/bin/python setup.py install --root="${pkgdir}" --optimize=1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
