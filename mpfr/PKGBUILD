# Maintainer: Alexey Pavlov <Alexpux@gmail.com>

pkgname=('mpfr' 'mpfr-devel')
_pkgver=4.1.1
pkgver=${_pkgver}.p1
pkgrel=1
pkgdesc="Multiple-precision floating-point library"
arch=('i686' 'x86_64')
url="https://www.mpfr.org/"
license=('spdx:LGPL-3.0-or-later')
depends=('gmp>=5.0')
makedepends=('gmp-devel>=5.0' 'autotools' 'gcc' 'autoconf-archive')
source=(https://www.mpfr.org/mpfr-current/mpfr-${_pkgver}.tar.xz{,.asc}
        patches.patch)
# wget https://www.mpfr.org/mpfr-4.1.1/allpatches -O patches.patch
validpgpkeys=('A534BE3F83E241D918280AEB5831D11A0D4DB02A') # Vincent Lefevre <vincent@vinc17.net>
sha256sums=('ffd195bd567dbaffc3b98b23fd00aad0537680c9896171e44fe3ff79e28ac33d'
            'SKIP'
            '80a3c2709be21acaac12a9cc99888d63a00fa77fb75576f205fe8ba1984ff44a')

prepare() {
  cd "${srcdir}/${pkgname}-${_pkgver}"

  patch -p1 -i ${srcdir}/patches.patch

  autoreconf -fi
}

build() {
  cd "${srcdir}/${pkgname}-${_pkgver}"

  local CYGWIN_CHOST="${CHOST/-msys/-cygwin}"
  ./configure --build=${CYGWIN_CHOST} \
    --prefix=/usr \
    --enable-shared \
    --disable-static
  make
  make DESTDIR="${srcdir}/dest" install
}

check() {
  cd "${srcdir}/${pkgname}-${_pkgver}"
  make check
}

package_mpfr() {
  groups=('libraries')

  mkdir -p ${pkgdir}/usr

  cp -rf ${srcdir}/dest/usr/bin ${pkgdir}/usr/
  cp -rf ${srcdir}/dest/usr/share ${pkgdir}/usr/
}

package_mpfr-devel() {
  groups=('development')
  options=('staticlibs')
  depends=("mpfr=${pkgver}" 'gmp-devel')
  pkgdesc="MPFR headers and libraries"

  mkdir -p ${pkgdir}/usr
  cp -rf ${srcdir}/dest/usr/include ${pkgdir}/usr/
  cp -rf ${srcdir}/dest/usr/lib ${pkgdir}/usr/
}
