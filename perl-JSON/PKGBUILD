# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_perlmod='JSON'
pkgname="perl-${_perlmod}"
pkgver=4.04
pkgrel=1
pkgdesc="JSON (JavaScript Object Notation) encoder/decoder"
arch=('any')
url="https://metacpan.org/release/JSON"
license=('GPL' 'PerlArtistic')
groups=('perl-modules')
depends=('perl>=5.10.0')
options=('!emptydirs')
source=(http://search.cpan.org/CPAN/authors/id/I/IS/ISHIGAKI/JSON-${pkgver}.tar.gz)
sha256sums=('66b6c6ea1ed6d609b0b70df29fdcbd281f3fe13c661e5b2847b4e4e8c12b5380')

build() {
  cd  "${srcdir}"/JSON-${pkgver}
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "${srcdir}"/JSON-${pkgver}
  make test
}

package() {
  cd  "${srcdir}"/JSON-${pkgver}
  make install DESTDIR="${pkgdir}"
  find "${pkgdir}" -name '.packlist' -delete
  find "${pkgdir}" -name '*.pod' -delete
}
