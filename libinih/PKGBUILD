# Maintainer: Biswapriyo Nath <nathbappai@gmail.com>

pkgbase=libinih
pkgname=(libinih libinih-devel)
pkgver=57
pkgrel=2
pkgdesc='A simple .INI file parser written in C'
license=('spdx:BSD-3-Clause')
url='https://github.com/benhoyt/inih'
msys2_references=(
  'archlinux: libinih'
  'cygwin: inih'
)
arch=('i686' 'x86_64')
makedepends=('gcc' 'pkgconf' 'python' 'meson' 'ninja')
source=(https://github.com/benhoyt/inih/archive/refs/tags/r${pkgver}.tar.gz
        "https://patch-diff.githubusercontent.com/raw/benhoyt/inih/pull/157.patch")
sha256sums=('f03f98ca35c3adb56b2358573c8d3eda319ccd5287243d691e724b7eafa970b3'
            '289c3c3ab5b30387410e42d2061a4ab5aa96e134045be280ad34f6303a8407f8')

prepare() {
  cd "${srcdir}"/inih-r${pkgver}

  patch -p1 -i "${srcdir}"/157.patch
}

build() {
  mkdir "${srcdir}"/build && cd "${srcdir}"/build

  /usr/bin/meson \
    --wrap-mode=nodownload \
    --auto-features=enabled \
    --buildtype=plain \
    --prefix=/usr \
    -Ddefault_library=shared \
    -Ddistro_install=true \
    -Dwith_INIReader=true \
    "../inih-r${pkgver}"

  /usr/bin/meson compile

  DESTDIR="${srcdir}/dest" /usr/bin/meson install
}

package_libinih() {
  depends=('gcc-libs')
  groups=('libraries')

  mkdir -p ${pkgdir}/usr
  cp -rf ${srcdir}/dest/usr/bin ${pkgdir}/usr/
}

package_libinih-devel() {
  depends=("libinih=${pkgver}")
  groups=('development')
  options=('staticlibs')
  pkgdesc="libinih headers and libraries"

  mkdir -p ${pkgdir}/usr
  cp -rf ${srcdir}/dest/usr/include ${pkgdir}/usr/
  cp -rf ${srcdir}/dest/usr/lib ${pkgdir}/usr/
}
