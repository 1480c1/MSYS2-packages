# Maintainer: J. Peter Mugaas <jpmugaas@suddenlink.net>

pkgbase=python-pytest
pkgname=('python-pytest')
pkgver=5.4.2
pkgrel=1
provides=("python3-pytest")
replaces=("python3-pytest")
conflicts=("python3-pytest")
pkgdesc="Simple powerful testing with Python"
arch=('any')
license=('MIT')
url="https://pytest.org/"
depends=('python'
         'python-atomicwrites'
         'python-attrs'
         'python-more-itertools'
         'python-pluggy'
         'python-py'
         'python-setuptools'
         'python-six')
makedepends=('python-setuptools-scm')
source=("pytest-${pkgver}.tar.gz::https://github.com/pytest-dev/pytest/archive/${pkgver}.tar.gz")
sha512sums=('26c061409fb5eed446ac9f45930c9b96d885ec57f1f45810848ce17d866b1aba0808623eb7188ea9a8e50d2787509b4e6771f129a8b5fc799cd120c050d30cfc')

prepare() {
  sed -i "s/'_pytest.vendored_packages'//" pytest-${pkgver}/setup.py
  sed -i 's/assert numentries == 0/assert numentries == 26/' pytest-${pkgver}/testing/python/collect.py
}

build() {
  export SETUPTOOLS_SCM_PRETEND_VERSION=${pkgver}
  
  cd "${srcdir}/pytest-${pkgver}"
  /usr/bin/python setup.py build
}

package() {
  cd pytest-${pkgver}
  /usr/bin/python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
