# Maintainer: Oleg Titov <oleg.titov@gmail.com>

pkgname=iperf3
pkgver=3.14
pkgrel=1
pkgdesc='TCP, UDP, and SCTP network bandwidth measurement tool'
arch=('i686' 'x86_64')
url="https://github.com/esnet/iperf"
license=('BSD')
depends=('gcc-libs' 'openssl')
makedepends=('openssl-devel' 'autotools' 'gcc')
source=("https://downloads.es.net/pub/iperf/iperf-${pkgver}.tar.gz")
sha256sums=('723fcc430a027bc6952628fa2a3ac77584a1d0bd328275e573fc9b206c155004')

build() {
  cd ${srcdir}/iperf-${pkgver}
  ./configure \
    --build=${CHOST} \
    --host=${CHOST} \
    --target=${CHOST} \
    --prefix=/usr \
    ac_cv_func_SetProcessAffinityMask=no

  make
}

check() {
  cd ${srcdir}/iperf-${pkgver}
  make check
}

package() {
  cd ${srcdir}/iperf-${pkgver}
  make DESTDIR=${pkgdir} install
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 examples/*{.am,.in,.c} -t "${pkgdir}/usr/share/doc/${pkgname}/examples"
}
