
_realname=reflex
pkgname=${_realname}
pkgver=20221012
pkgrel=1
pkgdesc="A variant of the flex fast lexical scanner"
arch=('i686' 'x86_64')
url="https://invisible-island.net/reflex"
license=('BSD')
makedepends=('autotools' 'gcc')

source=("${_realname}-${pkgver}.tar.gz::https://github.com/ThomasDickey/${_realname}-snapshots/archive/t${pkgver}.tar.gz"
        '0001-t20210808-Makefile.in-fix-target-install.man.patch')
sha256sums=('4324a201d755c3cc33359dd2395fbf731918bc4449dfa089ceae6fd47333fa98'
            '30f8bd875e9a6e93c7958dde4d1c99af76bed160726c3af675b0e13f5348b7fd')


prepare() {
  pushd "${_realname}-snapshots-t${pkgver}"
  patch -p1 < '../0001-t20210808-Makefile.in-fix-target-install.man.patch'
  autoreconf -vfi
  popd
}

build() {
  mkdir -p build-${pkgver}-${MSYSTEM_CHOST}
  pushd build-${pkgver}-${MSYSTEM_CHOST}
  ../${_realname}-snapshots-t${pkgver}/configure -C
  make
  popd
}

check() {
  make -C build-${pkgver}-${MSYSTEM_CHOST} bigcheck
}

package() {
  make -C build-${pkgver}-${MSYSTEM_CHOST} DESTDIR=${pkgdir} install
  install -Dv -t "${pkgdir}/usr/share/licenses/${pkgname}" \
    "${_realname}-snapshots-t${pkgver}/package/debian/copyright"
}
