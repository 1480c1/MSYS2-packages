# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=psmisc
pkgver=23.6
pkgrel=1
pkgdesc="Miscellaneous procfs tools"
arch=('i686' 'x86_64')
url="https://gitlab.com/psmisc/psmisc"
license=('GPL')
groups=('sys-utils')
depends=('gcc-libs' 'ncurses' 'libiconv' 'libintl')
makedepends=('gcc' 'ncurses-devel' 'libiconv-devel' 'gettext-devel' 'autotools' 'gcc')
source=(${pkgname}-${pkgver}.tar.gz::https://gitlab.com/psmisc/psmisc/-/archive/v${pkgver}/${pkgname}-v${pkgver}.tar.gz
        "0000-fix-build.patch")
sha256sums=('58022dc47e0fb855468ba6c3c36af225b2caa79b61c8d68132438004fe8b79f3'
            '99f27495fc1c4560246442e30f966b072d9209040dc6d98962d60ff52ce9b73c')

prepare() {
  cd ${pkgname}-v${pkgver}

  patch -Np1 -i ../0000-fix-build.patch

  echo ${pkgver} > .tarball-version
  ./autogen.sh
}

build() {
  cd ${pkgname}-v${pkgver}
  ./configure \
    --prefix=/usr \
    --without-libiconv-prefix \
    --without-libintl-prefix \
    --disable-harden-flags \
    CPPFLAGS="${CPPFLAGS} -I/usr/include/ncursesw"
  make
}

package() {
  cd ${pkgname}-v${pkgver}
  make DESTDIR=${pkgdir} install
}
