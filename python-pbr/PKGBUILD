# Maintainer: J. Peter Mugaas <jpmugaas@suddenlink.net>

_realname=pbr
pkgbase=python-${_realname}
pkgname=(python-${_realname})
pkgver=5.11.0
pkgrel=1
pkgdesc="Python Build Reasonableness"
arch=('any')
url='https://pypi.python.org/pypi/pbr'
license=('spdx:Apache-2.0')
depends=('python-setuptools')
makedepends=(
  "python-wheel"
  "python-build"
  "python-installer"
)
provides=("python3-${_realname}")
conflicts=("python3-${_realname}")
replaces=("python3-${_realname}")
source=("${pkgbase}-${pkgver}.tar.gz::https://github.com/openstack-dev/pbr/archive/${pkgver}.tar.gz")
sha256sums=('74cab0b3937842629c6e02f95a64fb230ae9e63b9407eee8d4fe0f1c419dd871')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  export PBR_VERSION=${pkgver}
}

build() {
  cd "${srcdir}/${_realname}-${pkgver}"

  python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"

  python -m installer --destdir="${pkgdir}" dist/*.whl

  install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
