# Maintainer: Christoph Reiter <reiter.christoph@gmail.com>

_realname=installer
pkgname="python-${_realname}"
pkgver=0.5.1
pkgrel=2
pkgdesc="A low-level library for installing from a Python wheel distribution"
arch=('any')
url='https://github.com/pypa/installer'
license=('spdx:MIT')
depends=("python")
makedepends=(
  "python-flit-core"
)
source=("${_realname}-$pkgver.tar.gz"::"https://pypi.org/packages/source/${_realname::1}/${_realname}/${_realname}-${pkgver}.tar.gz")
sha256sums=('f970995ec2bb815e2fdaf7977b26b2091e1e386f0f42eafd5ac811953dc5d445')

build() {
  cd "${srcdir}/${_realname}-${pkgver}"

  python -m flit_core.wheel
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"

  PYTHONPATH="${srcdir}/${_realname}-${pkgver}/src" \
    python -m installer --destdir="${pkgdir}" dist/*.whl

  # remove windows entrypoint scripts executables
  rm "$pkgdir"/usr/lib/python*/site-packages/installer/_scripts/*.exe
}
