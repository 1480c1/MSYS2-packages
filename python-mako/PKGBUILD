# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=mako
pkgbase="python-${_realname}"
pkgname=("python-${_realname}")
pkgver=1.1.6
pkgrel=3
pkgdesc="A super-fast templating language that borrows the best ideas from the existing templating languages"
arch=('any')
url="https://pypi.python.org/pypi/Mako"
license=('MIT')
depends=("python-markupsafe"
         "python-beaker")
makedepends=(
  "python-build"
  "python-installer"
  "python-setuptools"
  "python-wheel"
)
checkdepends=("python-pytest")
provides=("python3-${_realname}")
conflicts=("python3-${_realname}")
replaces=("python3-${_realname}")
options=('staticlibs')
source=("https://pypi.python.org/packages/source/M/Mako/Mako-${pkgver}.tar.gz")
sha256sums=('4e9e345a41924a954251b95b4b28e14a301145b544901332e658907a7464b6b2')

build() {
  cd "${srcdir}/Mako-${pkgver}"

  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd "${srcdir}/Mako-${pkgver}"

  python -m pytest
}

package() {
  cd "${srcdir}/Mako-${pkgver}"

  python -m installer --destdir="${pkgdir}" dist/*.whl

  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/python-${_realname}/COPYING"
}
