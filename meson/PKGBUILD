# Maintainer: Christoph Reiter <reiter.christoph@gmail.com>

pkgname=meson
pkgver=0.64.1
pkgrel=2
pkgdesc='High-productivity build system'
arch=('any')
url="https://mesonbuild.com/"
license=('spdx:Apache-2.0')
depends=('python' 'ninja')
makedepends=(
  "python-setuptools"
  "python-wheel"
  "python-build"
  "python-installer"
)
source=("https://github.com/mesonbuild/${pkgname}/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.gz"{,.asc}
        "0001-cygwin-disguise.patch")
sha256sums=('3a8e030c2334f782085f81627062cc6d4a6771edf31e055ffe374f9e6b089ab9'
            'SKIP'
            '240f37b8c64446ff5d4e97a2775d48e9c5e48b62926c19900b4cead220f89614')
validpgpkeys=("19E2D6D9B46D8DAA6288F877C24E631BABB1FE70") # Jussi Pakkanen <jpakkane@gmail.com>

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 -i "${srcdir}"/0001-cygwin-disguise.patch
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  python -m installer --destdir="${pkgdir}" dist/*.whl

  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
