# Maintainer: Christoph Reiter <reiter.christoph@gmail.com>

pkgname=meson
pkgver=0.55.3
pkgrel=1
pkgdesc='High-productivity build system'
arch=('any')
url="https://mesonbuild.com/"
license=('Apache 2')
depends=('python' 'python-setuptools' 'ninja')
source=("https://github.com/mesonbuild/${pkgname}/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.gz"
        "cygwin-disguise.patch")
sha256sums=('6bed2a25a128bbabe97cf40f63165ebe800e4fcb46db8ab7ef5c2b5789f092a5'
            '94eccd752284a08811c41b2afd1801ccc0274affac7ada5356b413a8f0f43164')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 -i "${srcdir}"/cygwin-disguise.patch
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  /usr/bin/python setup.py build
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  /usr/bin/python setup.py install --root="${pkgdir}/" --optimize=1

  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
