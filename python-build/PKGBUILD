# Maintainer: Christoph Reiter <reiter.christoph@gmail.com>

_realname=build
pkgname="python-${_realname}"
pkgver=0.8.0
pkgrel=1
pkgdesc="A simple, correct PEP517 package builder"
arch=('any')
url='https://github.com/pypa/build'
license=('spdx:MIT')
depends=("python-packaging"
         "python-pep517"
         "python-tomli")
makedepends=("python-setuptools")
source=("${_realname}-$pkgver.tar.gz"::"https://pypi.org/packages/source/${_realname::1}/${_realname}/${_realname}-${pkgver}.tar.gz")
sha256sums=('887a6d471c901b1a6e6574ebaeeebb45e5269a79d095fe9a8f88d6614ed2e5f0')

build() {
  cd "${srcdir}/${_realname}-${pkgver}"

  python setup.py build
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"

  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build

  install -D -m644 LICENSE "${pkgdir}"/usr/share/licenses/python-${_realname}/LICENSE
}
