# Maintainer: Christoph Reiter <reiter.christoph@gmail.com>

_realname=build
pkgname="python-${_realname}"
pkgver=0.9.0
pkgrel=1
pkgdesc="A simple, correct PEP517 package builder"
arch=('any')
url='https://github.com/pypa/build'
license=('spdx:MIT')
depends=("python-packaging"
         "python-pep517"
         "python-tomli")
makedepends=("python-setuptools")
source=("${_realname}-$pkgver.tar.gz"::"https://pypi.org/packages/source/${_realname::1}/${_realname}/${_realname}-${pkgver}.tar.gz")
sha256sums=('1a07724e891cbd898923145eb7752ee7653674c511378eb9c7691aab1612bc3c')

build() {
  cd "${srcdir}/${_realname}-${pkgver}"

  python setup.py build
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"

  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build

  install -D -m644 LICENSE "${pkgdir}"/usr/share/licenses/python-${_realname}/LICENSE
}
