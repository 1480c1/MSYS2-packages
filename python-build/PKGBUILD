# Maintainer: Christoph Reiter <reiter.christoph@gmail.com>

_realname=build
pkgname="python-${_realname}"
pkgver=1.1.1
pkgrel=1
pkgdesc="A simple, correct Python build frontend"
arch=('any')
url='https://github.com/pypa/build'
msys2_references=(
  "pypi:build"
)
license=('spdx:MIT')
depends=("python-packaging"
         "python-pyproject-hooks")
makedepends=(
  "python-flit-core"
  "python-installer"
)
source=("${_realname}-$pkgver.tar.gz"::"https://pypi.org/packages/source/${_realname::1}/${_realname}/${_realname}-${pkgver}.tar.gz")
sha256sums=('8eea65bb45b1aac2e734ba2cc8dad3a6d97d97901a395bd0ed3e7b46953d2a31')

build() {
  cd "${srcdir}/${_realname}-${pkgver}"

  python -m flit_core.wheel
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"

  python -m installer --destdir="${pkgdir}" dist/*.whl

  install -D -m644 LICENSE "${pkgdir}"/usr/share/licenses/python-${_realname}/LICENSE
}
