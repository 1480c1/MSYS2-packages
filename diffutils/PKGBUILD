# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=diffutils
pkgver=3.8
pkgrel=4
pkgdesc="Utility programs used for creating patch files"
arch=('i686' 'x86_64')
url="https://www.gnu.org/software/diffutils"
license=('spdx:GPL-3.0-or-later')
depends=('sh' 'libiconv' 'libintl')
makedepends=('autotools' 'gcc' 'gettext-devel' 'libiconv-devel')
source=(https://ftp.gnu.org/gnu/${pkgname}/${pkgname}-${pkgver}.tar.xz{,.sig}
        gnulib-weak.patch)
sha256sums=('a6bdd7d1b31266d11c4f4de6c1b748d4607ab0231af5188fc2533d0ae2438fec'
            'SKIP'
            'bf5185de612006c938ca1e82ea3e59f177948bfd2cbcaba7d647e00e0e380a3b')
validpgpkeys=('155D3FC500C834486D1EEA677FD9FCCB000BEEEE') # Jim Meyering <jim@meyering.net>

prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}

  patch -p1 -i ${srcdir}/gnulib-weak.patch

  autoreconf -fiv
}

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  # make sure tests are running OK
  sed -i "s/mkdir -m 0700/mkdir /" tests/init.sh

  local CYGWIN_CHOST="${CHOST/-msys/-cygwin}"
  ./configure \
    --build=${CYGWIN_CHOST} \
    --prefix=/usr \
    --without-libiconv-prefix \
    --without-libintl-prefix
  make
}

check() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make check || true
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
