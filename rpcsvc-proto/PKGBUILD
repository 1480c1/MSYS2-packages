# Maintainer: Biswapriyo Nath <nathbappai@gmail.com>

pkgname=rpcsvc-proto
pkgver=1.4.3
pkgrel=1
pkgdesc="rpcsvc protocol definitions from glibc."
arch=('i686' 'x86_64')
url="https://github.com/thkukuk/rpcsvc-proto"
license=('BSD')
depepnds=('libintl' 'libiconv')
makedepends=('autotools' 'gcc' 'gettext-devel' 'libiconv-devel')
source=("${pkgname}-${pkgver}.tar.gz"::"${url}/archive/v${pkgver}.tar.gz")
sha512sums=('44afac7626147b7c4166bdd90556172bd5d7655f5837245985db7af3cc610de00e215c8cccc4ac6b6a0dd6d0a65d062dc19dab4d1cec0ef45818a7ff709df513')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # Replace stat64 with stat, cygwin/msys2 does not export it.
  sed -s "s|stat64|stat|g" -i "rpcgen/rpc_main.c"

  autoreconf -fiv
}

build() {
  [[ -d "${srcdir}/build-${MSYSTEM_CHOST}" ]] && rm -rf "${srcdir}/build-${MSYSTEM_CHOST}"
  mkdir -p "${srcdir}/build-${MSYSTEM_CHOST}" && cd "${srcdir}/build-${MSYSTEM_CHOST}"

  CHOST="${CHOST/-msys/-cygwin}" \
  ../${pkgname}-${pkgver}/configure \
    --prefix=/usr \
    --build=${CHOST} \
    --host=${CHOST} \
    --target=${CHOST}

  make
}

package() {
  cd "${srcdir}/build-${MSYSTEM_CHOST}"
  make DESTDIR="${pkgdir}" install

  install -Dm644 "${srcdir}/${pkgname}-${pkgver}/COPYING" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
