# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=('xz' 'liblzma' 'liblzma-devel')
pkgver=5.2.6
pkgrel=1
pkgdesc='Library and command line tools for XZ and LZMA compressed files'
arch=('i686' 'x86_64')
url='https://tukaani.org/xz/'
license=('GPL' 'LGPL' 'custom')
depends=('sh' 'libiconv' 'gettext')
makedepends=('libiconv-devel' 'gettext-devel' 'autotools' 'gcc')
options=('staticlibs')
source=("https://tukaani.org/${pkgname}/${pkgname}-${pkgver}.tar.gz"{,.sig})
sha256sums=('a2105abee17bcd2ebd15ced31b4f5eda6e17efd6b10f921a01cda4a44c91b3a0'
            'SKIP')
validpgpkeys=('3690C240CE51B4670D30AD1C38EE757D69184620')  # Lasse Collin <lasse.collin@tukaani.org>

prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}

  autoreconf -ivf
}

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  export lt_cv_deplibs_check_method='pass_all'
  export MSYSTEM=CYGWIN
  local CYGWIN_CHOST="${CHOST/-msys/-cygwin}"
  ./configure \
    --build=${CHOST} \
    --prefix=/usr \
    --enable-shared \
    --enable-static
  make
  make DESTDIR="${srcdir}/dest" install
}

check() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make check
}

package_xz() {
  groups=('compression')
  depends=("liblzma=${pkgver}" 'libiconv' 'libintl')

  mkdir -p ${pkgdir}/usr/bin
  cp -rf ${srcdir}/dest/usr/bin ${pkgdir}/usr/
  rm -f ${pkgdir}/usr/bin/*.dll
  cp -rf ${srcdir}/dest/usr/share ${pkgdir}/usr/
}

package_liblzma() {
  pkgdesc="Library for XZ and LZMA compressed files"
  groups=('libraries')

  mkdir -p ${pkgdir}/usr/bin
  cp -f ${srcdir}/dest/usr/bin/*.dll ${pkgdir}/usr/bin/
}

package_liblzma-devel() {
  pkgdesc="Liblzma headers and libraries"
  groups=('development')
  depends=("liblzma=${pkgver}" 'libiconv-devel' 'gettext-devel')

  mkdir -p ${pkgdir}/usr
  cp -rf ${srcdir}/dest/usr/include ${pkgdir}/usr/
  cp -rf ${srcdir}/dest/usr/lib ${pkgdir}/usr/
}
