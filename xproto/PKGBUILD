# Maintainer: Alexey Pavlov <Alexpux@gmail.com>

pkgname=xproto
pkgver=7.0.31
pkgrel=1
pkgdesc="X11 core wire protocol and auxiliary headers"
arch=(any)
license=('custom')
groups=('development')
url="https://xorg.freedesktop.org/"
makedepends=('util-macros' 'autotools' 'gcc')
source=(${url}/releases/individual/proto/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('c6f9747da0bd3a95f86b17fb8dd5e717c8f3ab7f0ece3ba1b247899ec1ef7747')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  autoreconf -ivf
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  export MSYSTEM=CYGWIN
  local CYGWIN_CHOST="${CHOST/-msys/-cygwin}"
  ./configure --build=${CYGWIN_CHOST} --prefix=/usr
  make
}

check() {
  cd "${srcdir}/$pkgname-$pkgver"
  make -k check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
