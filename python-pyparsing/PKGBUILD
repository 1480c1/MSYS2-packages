# Maintainer: atom2013 <atom.long@hotmail.com>

_realname=pyparsing
pkgbase="python-${_realname}"
pkgname=("python-${_realname}")
pkgver=3.0.9
pkgrel=1
pkgdesc='General parsing module for Python'
arch=('any')
url='https://github.com/pyparsing/pyparsing/'
depends=('python')
makedepends=(
  'python-flit-core'
  'python-installer'
)
provides=("python3-${_realname}")
replaces=("python3-${_realname}")
conflicts=("python3-${_realname}")
license=('MIT')
source=("https://github.com/pyparsing/pyparsing/archive/pyparsing_$pkgver.tar.gz")
sha256sums=('7e8ce1684c517f57f945698fd3bbf86b36a2e60cd223f74886d3af04deb06306')

build() {
  cd "${srcdir}"/pyparsing-pyparsing_${pkgver}

  python -m flit_core.wheel
}

package() {
  cd pyparsing-pyparsing_${pkgver}

  python -m installer --destdir="${pkgdir}" dist/*.whl
  install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
