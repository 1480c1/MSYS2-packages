# Maintainer: Oleg Titov <oleg.titov@gmail.com>

pkgname=python-brotli
pkgver=1.0.9
pkgrel=8
pkgdesc='Brotli compression library - python library'
arch=('i686' 'x86_64')
license=('MIT')
url='https://github.com/google/brotli'
depends=('python')
makedepends=(
  'python-devel'
  'gcc'
  "python-setuptools"
  "python-wheel"
  "python-build"
  "python-installer"
)
source=("brotli-${pkgver}.tar.gz::https://github.com/google/brotli/archive/v${pkgver}.tar.gz")
sha256sums=('f9e8d81d0405ba66d181529af42a3354f838c939095ff99930da6aa9cdf6fe46')

build() {
  cd "${srcdir}"/brotli-${pkgver}

  python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd brotli-${pkgver}

  python -m installer --destdir="${pkgdir}" dist/*.whl
  install -D -m644 "${srcdir}"/brotli-${pkgver}/LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
