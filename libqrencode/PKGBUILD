# Maintainer: micbou <contact@micbou.com>

pkgbase=libqrencode
pkgname=('libqrencode' 'libqrencode-devel')
pkgver=4.1.0
pkgrel=1
pkgdesc="QR Code encoding library"
conflicts=(${pkgbase}-git)
replaces=(${pkgbase}-git)
arch=('i686' 'x86_64')
url='https://fukuchi.org/works/qrencode/'
license=('LGPL')
#depends=('libpng')
makedepends=('autoconf' 'automake' 'libtool' 'pkg-config' 'gcc')
source=("https://github.com/fukuchi/libqrencode/archive/v${pkgver}.tar.gz")
sha256sums=('6279648767fa996779921291cc32dc4a043d4f5d61e70eed0fcce1d5d0f540c6')

prepare() {
  cd ${srcdir}/${pkgbase}-${pkgver}
  ./autogen.sh
}

build() {
  mkdir -p build-${pkgbase}-${pkgver}-${CHOST}
  cd build-${pkgbase}-${pkgver}-${CHOST}
  ../${pkgbase}-${pkgver}/configure -C \
    --prefix=/usr \
    --build=${CHOST} \
    --with-tests

  make LDFLAGS="${LDFLAGS} -no-undefined"
  make DESTDIR=${srcdir}/dest install
}

check () {
  cd build-${pkgbase}-${pkgver}-${CHOST}
  make check -j
}

package_libqrencode() {
  groups=('libraries')

  mkdir -p ${pkgdir}/usr
  cp -rf ${srcdir}/dest/usr/bin ${pkgdir}/usr/

  mkdir -p ${pkgdir}/usr/share
  cp -rf ${srcdir}/dest/usr/share/man ${pkgdir}/usr/share/
  install -Dm644 ${srcdir}/${pkgname}-${pkgver}/COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}

package_libqrencode-devel() {
  pkgdesc="libqrencode headers and libraries"
  groups=('development')
  options=('staticlibs')
  depends=(libqrencode=${pkgver})

  mkdir -p ${pkgdir}/usr
  cp -rf ${srcdir}/dest/usr/include ${pkgdir}/usr/
  cp -rf ${srcdir}/dest/usr/lib ${pkgdir}/usr/
}
