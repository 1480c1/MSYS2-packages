# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgbase=libatomic_ops
pkgname=("${pkgbase}" "${pkgbase}-devel")
pkgver=7.6.14
pkgrel=1
pkgdesc="Provides semi-portable access to hardware provided atomic memory operations"
arch=('any')
url="https://www.hboehm.info/gc"
makedepends=('autotools' 'gcc')
license=('GPL2' 'MIT')
source=("https://github.com/ivmai/${pkgbase}/releases/download/v${pkgver}/${pkgbase}-${pkgver}.tar.gz"
        libatomic_ops-7.6.6-msysize.patch)
sha256sums=('390f244d424714735b7050d056567615b3b8f29008a663c262fb548f1802d292'
            '68a68763e8ec2979f1a259faee7ef6d7275e1e603f4dda533c2ffd202b4ab298')

prepare(){
  cd ${srcdir}/${pkgbase}-${pkgver}
  patch -p1 -i ${srcdir}/libatomic_ops-7.6.6-msysize.patch
  autoreconf -fiv
}

build() {
  [[ -d ${srcdir}/build-${CHOST} ]] && rm -rf ${srcdir}/build-${CHOST}
  mkdir -p ${srcdir}/build-${CHOST} && cd ${srcdir}/build-${CHOST}

  ../${pkgbase}-${pkgver}/configure \
    --build=${CHOST} \
    --host=${CHOST} \
    --target=${CHOST} \
    --prefix=/usr \
    --enable-shared \
    --enable-static
  make
}

check() {
  cd ${srcdir}/build-${CHOST}
  PATH="${srcdir}/build-${CHOST}/src/.libs:$PATH" make check
}

package_libatomic_ops() {
  cd "${srcdir}/build-${CHOST}"
  make DESTDIR=${pkgdir} install

  rm -rf ${pkgdir}/usr/{include,lib,share}
  install -Dm644 ${srcdir}/${pkgbase}-${pkgver}/doc/LICENSING.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_libatomic_ops-devel() {
  groups=('development')
  options=('staticlibs')
  depends=("libatomic_ops=${pkgver}")

  cd "${srcdir}/build-${CHOST}"

  make DESTDIR=${pkgdir} install

  rm -rf ${pkgdir}/usr/bin
}
