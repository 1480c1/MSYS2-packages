
_realname=git-extras
pkgname=${_realname}
replaces=("${_realname}-git")
conflicts=("${_realname}-git")
pkgver=7.2.0
pkgrel=1
pkgdesc="GIT utilities -- repo summary, commit counting, repl, changelog population and more"
arch=('any')
license=('MIT')
url='https://github.com/tj/git-extras'
depends=('git')
source=("${_realname}-${pkgver}.tar.gz"::"https://github.com/tj/git-extras/archive/$pkgver.tar.gz")
noextract=("${_realname}-${pkgver}.tar.gz")
sha256sums=('f570f19b9e3407e909cb98d0536c6e0b54987404a0a053903a54b81680c347f1')

build(){
  cd "${srcdir}"
  bsdtar -zxf "${_realname}-${pkgver}.tar.gz" || true

  cd "${srcdir}/${_realname}-${pkgver}"
  cp bin/git-scp bin/git-rscp
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"
  make DESTDIR="$pkgdir" PREFIX=/usr SYSCONFDIR=/etc install
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${_realname}/LICENSE"
}
