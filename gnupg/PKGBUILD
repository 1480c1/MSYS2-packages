# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=gnupg
pkgver=2.2.28
pkgrel=3
pkgdesc='Complete and free implementation of the OpenPGP standard'
provides=('dirmngr' "gnupg2=${pkgver}")
url='https://gnupg.org/'
license=('GPL')
arch=('i686' 'x86_64')
makedepends=('gettext-devel'
             'libassuan-devel'
             'libbz2-devel'
             'libcurl-devel'
             'libgcrypt-devel'
             'libgnutls-devel'
             'libgpg-error-devel'
             'libiconv-devel'
             'libksba-devel'
             'libnettle-devel'
             'libnpth-devel'
             'libp11-kit-devel'
             'libreadline-devel'
             'libsqlite-devel'
             'libtasn1-devel'
             'libunistring-devel'
             'nettle'
             'tar'
             'zlib-devel')
optdepends=('curl: gpg2keys_curl')
depends=('bzip2'
         'libassuan'
         'libbz2'
         'libcurl'
         'libgcrypt'
         'libgpg-error'
         'libgnutls'
         'libiconv'
         'libintl'
         'libksba'
         'libnpth'
         'libreadline'
         'libsqlite'
         'nettle'
         'pinentry'
         'zlib'
        )
source=("https://gnupg.org/ftp/gcrypt/${pkgname}/${pkgname}-${pkgver}.tar.bz2"{,.sig}
        '0001-gnupg-2.2.8-msys2.patch'
        'fix-build-with-disable-ldap.patch::https://git.gnupg.org/cgi-bin/gitweb.cgi?p=gnupg.git;a=patch;h=c8b2162'
        'remove-use-of-USE_LDAPWRAPPER.patch::https://git.gnupg.org/cgi-bin/gitweb.cgi?p=gnupg.git;a=patch;h=8ee4c8d1e0d7677d4f8b9538c12b32bb6393c2c5'
        'use-keyserver.ubuntu.com-as-the-default-keyserver.patch::https://git.gnupg.org/cgi-bin/gitweb.cgi?p=gnupg.git;a=patch;h=47c4e3e00a7ef55f954c14b3c237496e54a853c1')
sha256sums=('6ff891fc7583a9c3fb9f097ee0d1de0a12469d4b53997e7ba5064950637dfaec'
            'SKIP'
            'cb23f1a61fd213c25e85b6ba8afb190b7da14a8cfa59cc56ce82df941db8c3c9'
            '5a4e9dbc453d472eab0d0b450d2ce259038871940461a9feb07b6f0a2032550c'
            '58207af5c4b059781a4fbb1de9a809faec11073e24b1069aa11678a768804847'
            'db06cf1ff079e3387012886643554e3d6a2df0e44ebd781fcbc2b89a2e226ba9')
validpgpkeys=('D8692123C4065DEA5E0F3AB5249B39D24F25E3B6'
              '46CC730865BB5C78EBABADCF04376F3EE0856959'
              '031EC2536E580D8EA286A9F22071B08A33BD3F06'
              '5B80C5754298F0CB55D8ED6ABCEF7E294B092E28'
              '6DAA6E64A76D2840571B4902528897B826403ADA')
install=${pkgname}.install

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed '/noinst_SCRIPTS = gpg-zip/c sbin_SCRIPTS += gpg-zip' -i tools/Makefile.in

  patch -p1 -i ${srcdir}/0001-gnupg-2.2.8-msys2.patch

  # Upstream commit dirmngir: Fix build with --disable-ldap.
  patch -Np1 -i "${srcdir}/fix-build-with-disable-ldap.patch"

  # https://bugs.archlinux.org/task/71078#comment200369
  patch -p1 -i ${srcdir}/remove-use-of-USE_LDAPWRAPPER.patch
  patch -p1 -i ${srcdir}/use-keyserver.ubuntu.com-as-the-default-keyserver.patch

  ./autogen.sh --force

  sed 's/development_version=yes/development_version=no/' -i configure
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure \
    --build=${CHOST} \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --sbindir=/usr/bin \
    --libexecdir=/usr/lib/gnupg \
    --disable-libdns \
    --enable-maintainer-mode

  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
