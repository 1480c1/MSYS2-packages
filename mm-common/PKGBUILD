# Maintainer: Jeremy Drake <github@jdrake.com>

pkgname=mm-common
pkgver=1.0.6
pkgrel=1
pkgdesc="Common build files of the C++ bindings"
url="https://www.gtkmm.org/"
arch=('i686' 'x86_64')
makedepends=('meson' 'gcc')
license=('GPL2')
source=("https://download.gnome.org/sources/mm-common/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz"
        https://gcc.gnu.org/onlinedocs/libstdc++/latest-doxygen/libstdc++.tag)
sha256sums=('b55c46037dbcdabc5cee3b389ea11cc3910adb68ebe883e9477847aa660862e7'
            'dfb6521cb807fc827c34bcd47dbcf2f7564b2fc1b6014ebb13bed349ddf55af0')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  cp ../libstdc++.tag doctags
}

build() {
  mkdir "${srcdir}/build-${CARCH}" && cd "${srcdir}/build-${CARCH}"
  meson \
    --buildtype=plain \
    --prefix=/usr \
    "../${pkgname}-${pkgver}"

  meson compile
}

check() {
  cd "${srcdir}/build-${CARCH}"
  meson test --print-errorlogs
}

package() {
  cd "${srcdir}/build-${CARCH}"
  DESTDIR="${pkgdir}" meson install
}
