# Maintainer: Biswapriyo Nath <nathbappai@gmail.com>

_realname=lxml
pkgbase="python-${_realname}"
pkgname=("python-${_realname}")
pkgver=4.9.0
pkgrel=1
pkgdesc="Python binding for the libxml2 and libxslt libraries"
arch=('any')
url="https://lxml.de/"
license=('spdx:BSD-3-Clause')
depends=("python" "libxslt" "libxml2")
makedepends=("gcc" "cython" "libxslt-devel" "libxml2-devel" "pkgconf" "python-setuptools")
source=("https://github.com/lxml/lxml/archive/${_realname}-${pkgver}.tar.gz")
sha256sums=('9c7159b061c93e8658b4c4d9c4916ae493e771365512281c0e45ea4651262c7d')

prepare() {
  mv "lxml-lxml-${pkgver}" "lxml-${pkgver}"

  # Setting LC_CTYPE to workaround encoding issue
  export LC_CTYPE=en_US.UTF-8
}

build() {
  cd "${_realname}-${pkgver}"
  /usr/bin/python setup.py build
}

package() {
  cd "${_realname}-${pkgver}"
  /usr/bin/python setup.py install --prefix=/usr --root="${pkgdir}" -O1
  install -Dm644 LICENSES.txt "${pkgdir}/usr/share/licenses/python-${_realname}/LICENSES.txt"
}
