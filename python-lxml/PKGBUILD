# Maintainer: Biswapriyo Nath <nathbappai@gmail.com>

_realname=lxml
pkgbase="python-${_realname}"
pkgname=("python-${_realname}")
pkgver=4.9.1
pkgrel=2
pkgdesc="Python binding for the libxml2 and libxslt libraries"
arch=('any')
url="https://lxml.de/"
license=('spdx:BSD-3-Clause')
depends=("python" "libxslt" "libxml2")
makedepends=(
  "gcc"
  "cython"
  "libxslt-devel"
  "libxml2-devel"
  "pkgconf"
  "python-setuptools"
  "python-wheel"
  "python-build"
  "python-installer"
)
source=("https://github.com/lxml/lxml/archive/${_realname}-${pkgver}.tar.gz")
sha256sums=('e46a74cef580e069586b7a98e9a74b49e56b849822130654d28e00826db75095')

prepare() {
  mv "lxml-lxml-${pkgver}" "lxml-${pkgver}"

}

build() {
  cd "${_realname}-${pkgver}"

  python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd "${_realname}-${pkgver}"

  python -m installer --destdir="${pkgdir}" dist/*.whl

  install -Dm644 LICENSES.txt "${pkgdir}/usr/share/licenses/python-${_realname}/LICENSES.txt"
}
